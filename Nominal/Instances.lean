import Mathlib.Algebra.Group.Action.Sum
import Nominal.Equivariant

open MulAction Finperm

variable {ğ”¸ : Type*} [DecidableEq ğ”¸]

/-!
# Sets
-/
section Set
open scoped Pointwise

theorem Finset.smul_coe_eq_coe_iff {G Î± : Type*} [Group G] [MulAction G Î±]
    (g : G) (s : Finset Î±) :
    g â€¢ (s : Set Î±) = s â†” g â€¢ s = s := by
  simp only [Set.ext_iff, Finset.mem_coe, Finset.ext_iff,
    Set.mem_smul_set_iff_inv_smul_mem, Finset.mem_smul_iff]

theorem Finset.supports_coe_iff {Î± : Type*} [MulAction (Finperm ğ”¸) Î±] (s : Finset Î±) (t : Set ğ”¸) :
    Supports (Finperm ğ”¸) t (s : Set Î±) â†” Supports (Finperm ğ”¸) t s := by
  unfold Supports
  simp only [Finset.smul_coe_eq_coe_iff]

theorem Finset.coe_finitelySupported_iff {Î± : Type*} [MulAction (Finperm ğ”¸) Î±] (s : Finset Î±) :
    FinitelySupported ğ”¸ (s : Set Î±) â†” FinitelySupported ğ”¸ s := by
  simp only [FinitelySupported, supports_coe_iff]

theorem Set.finitelySupported_of_finite [Infinite ğ”¸] {Î± : Type*} [Nominal ğ”¸ Î±] (s : Set Î±) (hs : s.Finite) :
    FinitelySupported ğ”¸ s := by
  lift s to Finset Î± using hs
  rw [Finset.coe_finitelySupported_iff]
  apply Nominal.supported

theorem FinitelySupported.compl {Î± : Type*} [MulAction (Finperm ğ”¸) Î±]
    {s : Set Î±} (hs : FinitelySupported ğ”¸ s) :
    FinitelySupported ğ”¸ sá¶œ := by
  obtain âŸ¨t, htâŸ© := hs
  refine âŸ¨t, ?_âŸ©
  intro Ï€ hÏ€
  rw [Set.smul_set_compl, ht Ï€ hÏ€]

end Set

/-!
# Discrete structures
-/

set_option linter.unusedVariables false in
/-- A type alias to endow a type `Î±` with its discrete nominal structure. -/
def Discrete (ğ”¸ : Type*) (Î± : Type*) :=
  Î±

instance {Î± : Type*} : MulAction (Finperm ğ”¸) (Discrete ğ”¸ Î±) where
  smul _ := id
  one_smul _ := rfl
  mul_smul _ _ _ := rfl

instance {Î± : Type*} : Nominal ğ”¸ (Discrete ğ”¸ Î±) where
  supported _ := âŸ¨âˆ…, Î» _ _ â†¦ rflâŸ©

/-- Typeclass for discrete nominal structures. -/
class IsDiscrete (ğ”¸ : Type*) [DecidableEq ğ”¸] (Î± : Type*) [MulAction (Finperm ğ”¸) Î±] : Prop where
  smul_eq : âˆ€ Ï€ : Finperm ğ”¸, âˆ€ x : Î±, Ï€ â€¢ x = x

attribute [simp] IsDiscrete.smul_eq

instance {Î± : Type*} : IsDiscrete ğ”¸ (Discrete ğ”¸ Î±) where
  smul_eq _ _ := rfl

theorem equivariant_of_discrete {Î± Î² : Type*} [MulAction (Finperm ğ”¸) Î±] [MulAction (Finperm ğ”¸) Î²]
    (f : Discrete ğ”¸ Î± â†’ Discrete ğ”¸ Î²) :
    Equivariant ğ”¸ f :=
  Î» _ _ â†¦ rfl

/--
An object is called a *global section* if it is fixed under all permutations of names.
-/
def IsGlobalSection (ğ”¸ : Type*) {Î± : Type*} [DecidableEq ğ”¸] [MulAction (Finperm ğ”¸) Î±] (x : Î±) :=
  âˆ€ Ï€ : Finperm ğ”¸, Ï€ â€¢ x = x

theorem isGlobalSection_of_isDiscrete (ğ”¸ : Type*) [DecidableEq ğ”¸]
    {Î± : Type*} [MulAction (Finperm ğ”¸) Î±] [IsDiscrete ğ”¸ Î±] (x : Î±) :
    IsGlobalSection ğ”¸ x := by
  simp [IsGlobalSection]

theorem isGlobalSection_iff_supp_eq_empty [Infinite ğ”¸] {Î± : Type*} [Nominal ğ”¸ Î±] (x : Î±) :
    IsGlobalSection ğ”¸ x â†” supp ğ”¸ x = âˆ… := by
  constructor
  Â· intro h
    rw [Finset.eq_empty_iff_forall_not_mem]
    intro a ha
    rw [Nominal.mem_supp_iff] at ha
    have := ha âˆ… (Î» Ï€ _ â†¦ h Ï€)
    cases this
  Â· intro h Ï€
    have := Nominal.supp_supports ğ”¸ x
    rw [h] at this
    exact this Ï€ (Î» _ h â†¦ by cases h)

theorem IsGlobalSection.map {Î± : Type*} [MulAction (Finperm ğ”¸) Î±] {x : Î±} (h : IsGlobalSection ğ”¸ x)
    {Î² : Type*} [MulAction (Finperm ğ”¸) Î²]
    {f : Î± â†’ Î²} (hf : Equivariant ğ”¸ f) :
    IsGlobalSection ğ”¸ (f x) := by
  intro Ï€
  rw [hf, h]

theorem Equivariant.apply_isGlobalSection_of_isDiscrete
    {Î± Î² : Type*} [MulAction (Finperm ğ”¸) Î±] [MulAction (Finperm ğ”¸) Î²] [IsDiscrete ğ”¸ Î±]
    {f : Î± â†’ Î²} (hf : Equivariant ğ”¸ f) (x : Î±) :
    IsGlobalSection ğ”¸ (f x) :=
  (isGlobalSection_of_isDiscrete ğ”¸ x).map hf

/-- One part of the adjunction between the discrete and global sections functors. -/
def liftDiscrete {Î± Î² : Type*} [MulAction (Finperm ğ”¸) Î²] (f : Î± â†’ Î²)
    (_hf : âˆ€ x, IsGlobalSection ğ”¸ (f x)) :
    Discrete ğ”¸ Î± â†’ Î² :=
  f

theorem liftDiscrete_equivariant {Î± Î² : Type*} [MulAction (Finperm ğ”¸) Î²] (f : Î± â†’ Î²)
    (hf : âˆ€ x, IsGlobalSection ğ”¸ (f x)) :
    Equivariant ğ”¸ (liftDiscrete f hf) := by
  intro Ï€ x
  rw [IsDiscrete.smul_eq Ï€ x, liftDiscrete, hf]

/-!
# Binary coproducts
-/

theorem Sum.inl_equivariant {Î± Î² : Type*} [MulAction (Finperm ğ”¸) Î±] [MulAction (Finperm ğ”¸) Î²] :
    Equivariant ğ”¸ (Sum.inl : Î± â†’ Î± âŠ• Î²) :=
  Î» _ _ â†¦ rfl

theorem Sum.inr_equivariant {Î± Î² : Type*} [MulAction (Finperm ğ”¸) Î±] [MulAction (Finperm ğ”¸) Î²] :
    Equivariant ğ”¸ (Sum.inr : Î² â†’ Î± âŠ• Î²) :=
  Î» _ _ â†¦ rfl

instance {Î± Î² : Type*} [Nominal ğ”¸ Î±] [Nominal ğ”¸ Î²] : Nominal ğ”¸ (Î± âŠ• Î²) where
  supported := by
    rintro (x | x)
    Â· exact (Nominal.supported x).map Sum.inl Sum.inl_equivariant
    Â· exact (Nominal.supported x).map Sum.inr Sum.inr_equivariant

/-- This proves that `Î± âŠ• Î²` is the coproduct of `Î±` and `Î²` in the category of nominal sets. -/
def Sum.elim_equivariant {Î± Î² Î³ : Type*}
    [MulAction (Finperm ğ”¸) Î±] [MulAction (Finperm ğ”¸) Î²] [MulAction (Finperm ğ”¸) Î³]
    (f : Î± â†’ Î³) (g : Î² â†’ Î³) (hf : Equivariant ğ”¸ f) (hg : Equivariant ğ”¸ g) :
    Equivariant ğ”¸ (Sum.elim f g) := by
  rintro Ï€ (x | x)
  Â· rw [elim_inl, smul_inl, hf, elim_inl]
  Â· rw [elim_inr, smul_inr, hg, elim_inr]

@[simp]
theorem Sum.supp_inl {Î± Î² : Type*} [Nominal ğ”¸ Î±] [Nominal ğ”¸ Î²] (x : Î±) :
    supp ğ”¸ (Sum.inl x : Î± âŠ• Î²) = supp ğ”¸ x :=
  supp_apply_eq_of_injective (Sum.inl : Î± â†’ Î± âŠ• Î²) Sum.inl_injective Sum.inl_equivariant x

@[simp]
theorem Sum.supp_inr {Î± Î² : Type*} [Nominal ğ”¸ Î±] [Nominal ğ”¸ Î²] (x : Î²) :
    supp ğ”¸ (Sum.inr x : Î± âŠ• Î²) = supp ğ”¸ x :=
  supp_apply_eq_of_injective (Sum.inr : Î² â†’ Î± âŠ• Î²) Sum.inr_injective Sum.inr_equivariant x

/-!
# Binary products
-/

theorem Prod.fst_equivariant {Î± Î² : Type*} [MulAction (Finperm ğ”¸) Î±] [MulAction (Finperm ğ”¸) Î²] :
    Equivariant ğ”¸ (Prod.fst : Î± Ã— Î² â†’ Î±) :=
  Î» _ _ â†¦ rfl

theorem Prod.snd_equivariant {Î± Î² : Type*} [MulAction (Finperm ğ”¸) Î±] [MulAction (Finperm ğ”¸) Î²] :
    Equivariant ğ”¸ (Prod.snd : Î± Ã— Î² â†’ Î²) :=
  Î» _ _ â†¦ rfl

theorem MulAction.Supports.pair {Î± Î² : Type*} [MulAction (Finperm ğ”¸) Î±] [MulAction (Finperm ğ”¸) Î²]
    {s t : Finset ğ”¸} {x : Î±} {y : Î²}
    (hs : Supports (Finperm ğ”¸) (s : Set ğ”¸) x) (ht : Supports (Finperm ğ”¸) (t : Set ğ”¸) y) :
    Supports (Finperm ğ”¸) ((s âˆª t : Finset ğ”¸) : Set ğ”¸) (x, y) := by
  intro Ï€ hÏ€
  aesop

theorem FinitelySupported.pair {Î± Î² : Type*} [MulAction (Finperm ğ”¸) Î±] [MulAction (Finperm ğ”¸) Î²]
    {x : Î±} {y : Î²} (hx : FinitelySupported ğ”¸ x) (hy : FinitelySupported ğ”¸ y) :
    FinitelySupported ğ”¸ (x, y) := by
  obtain âŸ¨s, hsâŸ© := hx
  obtain âŸ¨t, htâŸ© := hy
  exact âŸ¨s âˆª t, hs.pair htâŸ©

instance {Î± Î² : Type*} [Nominal ğ”¸ Î±] [Nominal ğ”¸ Î²] : Nominal ğ”¸ (Î± Ã— Î²) where
  supported x := (Nominal.supported x.1).pair (Nominal.supported x.2)

/-- This proves that `Î± Ã— Î²` is the product of `Î±` and `Î²` in the category of nominal sets. -/
theorem Prod.pair_equivariant {Î± Î² Î³ : Type*}
    [MulAction (Finperm ğ”¸) Î±] [MulAction (Finperm ğ”¸) Î²] [MulAction (Finperm ğ”¸) Î³]
    (f : Î³ â†’ Î±) (g : Î³ â†’ Î²) (hf : Equivariant ğ”¸ f) (hg : Equivariant ğ”¸ g) :
    Equivariant ğ”¸ (Î» x â†¦ (f x, g x)) := by
  intro Ï€ x
  rw [smul_mk, hf, hg]

@[simp]
theorem Prod.supp_mk [Infinite ğ”¸] {Î± Î² : Type*} [Nominal ğ”¸ Î±] [Nominal ğ”¸ Î²] (x : Î±) (y : Î²) :
    supp ğ”¸ (x, y) = supp ğ”¸ x âˆª supp ğ”¸ y := by
  apply subset_antisymm
  Â· apply supp_minimal
    apply Supports.pair
    Â· exact Nominal.supp_supports ğ”¸ x
    Â· exact Nominal.supp_supports ğ”¸ y
  Â· apply Finset.union_subset
    Â· exact supp_apply_subset fst fst_equivariant (x, y)
    Â· exact supp_apply_subset snd snd_equivariant (x, y)

/-!
# Initial and terminal object
-/

instance {Î± : Type*} [Subsingleton Î±] : MulAction (Finperm ğ”¸) Î± where
  smul _ := id
  one_smul _ := rfl
  mul_smul _ _ _ := rfl

instance {Î± : Type*} [Subsingleton Î±] : Nominal ğ”¸ Î± where
  supported _ := âŸ¨âˆ…, Î» _ _ â†¦ rflâŸ©

instance {Î± : Type*} [Subsingleton Î±] : IsDiscrete ğ”¸ Î± where
  smul_eq _ _ := rfl

theorem equivariant_of_isEmpty {Î± Î² : Type*} [IsEmpty Î±] [MulAction (Finperm ğ”¸) Î²] (f : Î± â†’ Î²) :
    Equivariant ğ”¸ f := by
  intro Ï€ x
  cases IsEmpty.false x

theorem equivariant_of_subsingleton {Î± Î² : Type*} [MulAction (Finperm ğ”¸) Î±] [Subsingleton Î²] (f : Î± â†’ Î²) :
    Equivariant ğ”¸ f := by
  intro Ï€ x
  apply Subsingleton.allEq

/-!
# Coreflection

We show that the category of nominal sets is coreflective in the category of `Finperm ğ”¸`-sets.
-/

/-- A finitely supported element of `Î±`. -/
def FS (ğ”¸ : Type*) [DecidableEq ğ”¸] (Î± : Type*) [MulAction (Finperm ğ”¸) Î±] :=
  {x : Î± // FinitelySupported ğ”¸ x}

def FS.val {Î± : Type*} [MulAction (Finperm ğ”¸) Î±] (x : FS ğ”¸ Î±) : Î± :=
  Subtype.val x

attribute [coe] FS.val

instance {Î± : Type*} [MulAction (Finperm ğ”¸) Î±] : CoeOut (FS ğ”¸ Î±) Î± where
  coe := FS.val

@[ext]
theorem FS.ext {Î± : Type*} [MulAction (Finperm ğ”¸) Î±] {x y : FS ğ”¸ Î±} (h : (x : Î±) = y) : x = y :=
  Subtype.ext h

theorem FS.val_injective {Î± : Type*} [MulAction (Finperm ğ”¸) Î±] :
    Function.Injective (FS.val : FS ğ”¸ Î± â†’ Î±) :=
  Subtype.val_injective

instance {Î± : Type*} [MulAction (Finperm ğ”¸) Î±] : SMul (Finperm ğ”¸) (FS ğ”¸ Î±) where
  smul Ï€ x := âŸ¨Ï€ â€¢ (x : Î±), x.prop.smul Ï€âŸ©

@[simp]
theorem FS.smul_coe {Î± : Type*} [MulAction (Finperm ğ”¸) Î±] (x : FS ğ”¸ Î±) (Ï€ : Finperm ğ”¸) :
    ((Ï€ â€¢ x : FS ğ”¸ Î±) : Î±) = Ï€ â€¢ x :=
  rfl

instance {Î± : Type*} [MulAction (Finperm ğ”¸) Î±] : MulAction (Finperm ğ”¸) (FS ğ”¸ Î±) where
  one_smul _ := FS.ext (by rw [FS.smul_coe, one_smul])
  mul_smul _ _ _ := FS.ext (by simp only [FS.smul_coe, mul_smul])

theorem FS.val_equivariant {Î± : Type*} [MulAction (Finperm ğ”¸) Î±] :
    Equivariant ğ”¸ (FS.val (ğ”¸ := ğ”¸) (Î± := Î±)) :=
  Î» _ _ â†¦ rfl

instance {Î± : Type*} [MulAction (Finperm ğ”¸) Î±] : Nominal ğ”¸ (FS ğ”¸ Î±) where
  supported x := x.prop.of_map FS.val_injective FS.val_equivariant

@[simp]
theorem FS.supports_iff {Î± : Type*} [MulAction (Finperm ğ”¸) Î±] (x : FS ğ”¸ Î±) (s : Finset ğ”¸) :
    Supports (Finperm ğ”¸) (s : Set ğ”¸) x â†” Supports (Finperm ğ”¸) (s : Set ğ”¸) (x : Î±) :=
  âŸ¨Î» h â†¦ h.map val val_equivariant, Î» h â†¦ h.of_map val_injective val_equivariantâŸ©

/-- The factorisation of an equivariant function from a nominal set through the finitely supported
elements of its codomain. -/
def Equivariant.toFS {Î± Î² : Type*} [Nominal ğ”¸ Î±] [MulAction (Finperm ğ”¸) Î²]
    {f : Î± â†’ Î²} (hf : Equivariant ğ”¸ f) (x : Î±) : FS ğ”¸ Î² :=
  âŸ¨f x, (Nominal.supported x).map f hfâŸ©

/-!
# Function types

As the `SMul` instance we want to define is incompatible with the usual one, we use a structure.
-/

structure FinpermMap (Î± Î² : Type*) where
  protected toFun : Î± â†’ Î²

infixr:25 " â†’á¶ áµ– " => FinpermMap

instance FinpermMap.funLike {Î± Î² : Type*} : FunLike (Î± â†’á¶ áµ– Î²) Î± Î² where
  coe := FinpermMap.toFun
  coe_injective' f g h := by cases f; congr

instance {Î± Î² : Type*} [MulAction (Finperm ğ”¸) Î±] [MulAction (Finperm ğ”¸) Î²] :
    SMul (Finperm ğ”¸) (Î± â†’á¶ áµ– Î²) where
  smul Ï€ f := âŸ¨Î» x â†¦ Ï€ â€¢ f (Ï€â»Â¹ â€¢ x)âŸ©

theorem FinpermMap.smul_def {Î± Î² : Type*} [MulAction (Finperm ğ”¸) Î±] [MulAction (Finperm ğ”¸) Î²]
    (f : Î± â†’á¶ áµ– Î²) (x : Î±) (Ï€ : Finperm ğ”¸) :
    (Ï€ â€¢ f) x = Ï€ â€¢ f (Ï€â»Â¹ â€¢ x) :=
  rfl

@[simp]
theorem FinpermMap.smul_apply_smul {Î± Î² : Type*} [MulAction (Finperm ğ”¸) Î±] [MulAction (Finperm ğ”¸) Î²]
    (f : Î± â†’á¶ áµ– Î²) (x : Î±) (Ï€ : Finperm ğ”¸) :
    (Ï€ â€¢ f) (Ï€ â€¢ x) = Ï€ â€¢ f x := by
  rw [smul_def, inv_smul_smul]

theorem FinpermMap.smul_eq_iff {Î± Î² : Type*} [MulAction (Finperm ğ”¸) Î±] [MulAction (Finperm ğ”¸) Î²]
    (f : Î± â†’á¶ áµ– Î²) (Ï€ : Finperm ğ”¸) :
    Ï€ â€¢ f = f â†” âˆ€ x, Ï€ â€¢ f x = f (Ï€ â€¢ x) := by
  constructor
  Â· intro h x
    rw [â† smul_apply_smul, h]
  Â· intro h
    apply DFunLike.ext
    intro x
    rw [smul_def, h, smul_inv_smul]

instance {Î± Î² : Type*} [MulAction (Finperm ğ”¸) Î±] [MulAction (Finperm ğ”¸) Î²] :
    MulAction (Finperm ğ”¸) (FinpermMap Î± Î²) where
  one_smul _ := by
    apply DFunLike.ext
    simp only [FinpermMap.smul_def, inv_one, one_smul, implies_true]
  mul_smul _ _ _ := by
    apply DFunLike.ext
    simp only [FinpermMap.smul_def, mul_inv_rev, mul_smul, implies_true]

theorem FinpermMap.supports_iff {Î± Î² : Type*} [MulAction (Finperm ğ”¸) Î±] [MulAction (Finperm ğ”¸) Î²]
    (f : FinpermMap Î± Î²) (s : Finset ğ”¸) :
    Supports (Finperm ğ”¸) (s : Set ğ”¸) f â†”
    âˆ€ Ï€ : Finperm ğ”¸, (âˆ€ a âˆˆ s, Ï€ a = a) â†’ âˆ€ x, Ï€ â€¢ f x = f (Ï€ â€¢ x) := by
  simp_rw [â† smul_eq_iff]
  rfl

/-!
# Finitely supported functions
-/

/-- A finitely supported map from `Î±` to `Î²`. -/
structure NominalMap (ğ”¸ Î± Î² : Type*) [DecidableEq ğ”¸]
    [MulAction (Finperm ğ”¸) Î±] [MulAction (Finperm ğ”¸) Î²]
    extends Î± â†’á¶ áµ– Î² where
  supported' : FinitelySupported ğ”¸ toFinpermMap

notation:25 Î± " â†’â‚™[" ğ”¸:25 "] " Î²:0 => NominalMap ğ”¸ Î± Î²
