import Mathlib.Algebra.Group.Action.Sum
import Mathlib.Data.Part
import Nominal.Fresh

open Finperm

variable {ğ”¸ : Type*} [DecidableEq ğ”¸]

/-!
# Discrete structures
-/

set_option linter.unusedVariables false in
/-- A type alias to endow a type `Î±` with its discrete nominal structure. -/
def Discrete (ğ”¸ : Type*) (Î± : Sort*) :=
  Î±

instance {Î± : Sort*} : MulPerm ğ”¸ (Discrete ğ”¸ Î±) where
  perm _ := id
  one_perm _ := rfl
  mul_perm _ _ _ := rfl

instance {Î± : Sort*} : Nominal ğ”¸ (Discrete ğ”¸ Î±) where
  supported _ := âŸ¨âˆ…, Î» _ _ â†¦ rflâŸ©

/-- Typeclass for discrete nominal structures. -/
class IsDiscrete (ğ”¸ : Type*) [DecidableEq ğ”¸] (Î± : Sort*) [HasPerm ğ”¸ Î±] : Prop where
  perm_eq' : âˆ€ Ï€ : Finperm ğ”¸, âˆ€ x : Î±, Ï€ â¬ x = x

@[simp]
theorem IsDiscrete.perm_eq {Î± : Sort*} [HasPerm ğ”¸ Î±] [IsDiscrete ğ”¸ Î±] :
    âˆ€ Ï€ : Finperm ğ”¸, âˆ€ x : Î±, Ï€ â¬ x = x :=
  IsDiscrete.perm_eq'

instance {Î± : Sort*} : IsDiscrete ğ”¸ (Discrete ğ”¸ Î±) where
  perm_eq' _ _ := rfl

instance {Î± : Sort*} [Subsingleton Î±] [HasPerm ğ”¸ Î±] : IsDiscrete ğ”¸ Î± where
  perm_eq' _ _ := Subsingleton.elim _ _

instance : IsDiscrete ğ”¸ Prop where
  perm_eq' _ _ := rfl

theorem equivariant_of_isDiscrete {Î± : Sort*} [HasPerm ğ”¸ Î±] [IsDiscrete ğ”¸ Î±] (x : Î±) :
    Equivariant ğ”¸ x := by
  intro Ï€
  rw [IsDiscrete.perm_eq]

theorem finitelySupported_of_isDiscrete {Î± : Sort*} [HasPerm ğ”¸ Î±] [IsDiscrete ğ”¸ Î±] (x : Î±) :
    FinitelySupported ğ”¸ x :=
  (equivariant_of_isDiscrete x).finitelySupported

instance {Î± Î² : Sort*} [HasPerm ğ”¸ Î±] [IsDiscrete ğ”¸ Î±] [HasPerm ğ”¸ Î²] [IsDiscrete ğ”¸ Î²] :
    IsDiscrete ğ”¸ (Î± â†’ Î²) := by
  constructor
  intro Ï€ f
  ext x
  rw [Function.perm_def, IsDiscrete.perm_eq, IsDiscrete.perm_eq]

theorem Equivariant.not {Î± : Sort*} [MulPerm ğ”¸ Î±] {p : Î± â†’ Prop}
    (h : Equivariant ğ”¸ p) :
    Equivariant ğ”¸ (Î» x â†¦ Â¬p x) :=
  (equivariant_of_isDiscrete (Â¬ Â·)).comp h

theorem Equivariant.notâ‚‚ {Î± Î² : Sort*} [MulPerm ğ”¸ Î±] [MulPerm ğ”¸ Î²] {p : Î± â†’ Î² â†’ Prop}
    (h : Equivariant ğ”¸ p) :
    Equivariant ğ”¸ (Î» x y â†¦ Â¬p x y) :=
  (equivariant_of_isDiscrete (Â¬ Â·)).compâ‚‚ h

theorem FinitelySupported.not {Î± : Sort*} [MulPerm ğ”¸ Î±] {p : Î± â†’ Prop}
    (h : FinitelySupported ğ”¸ p) :
    FinitelySupported ğ”¸ (Î» x â†¦ Â¬p x) :=
  (finitelySupported_of_isDiscrete (Â¬ Â·)).comp h

theorem equivariant_iff_supp_eq_empty [Infinite ğ”¸] {Î± : Type*} [Nominal ğ”¸ Î±] (x : Î±) :
    Equivariant ğ”¸ x â†” supp ğ”¸ x = âˆ… := by
  constructor
  Â· intro h
    rw [Finset.eq_empty_iff_forall_not_mem]
    intro a ha
    rw [Nominal.mem_supp_iff] at ha
    have := ha âˆ… (Î» Ï€ _ â†¦ h Ï€)
    cases this
  Â· intro h Ï€
    have := Nominal.supp_supports ğ”¸ x
    rw [h] at this
    exact this Ï€ (Î» _ h â†¦ by cases h)

theorem Equivariant.map {Î± : Type*} [MulPerm ğ”¸ Î±] {x : Î±} (h : Equivariant ğ”¸ x)
    {Î² : Type*} [MulPerm ğ”¸ Î²]
    {f : Î± â†’ Î²} (hf : Equivariant ğ”¸ f) :
    Equivariant ğ”¸ (f x) := by
  intro Ï€
  rw [apply_perm_eq hf, h]

theorem Equivariant.apply_equivariant_of_isDiscrete
    {Î± Î² : Type*} [MulPerm ğ”¸ Î±] [MulPerm ğ”¸ Î²] [IsDiscrete ğ”¸ Î±]
    {f : Î± â†’ Î²} (hf : Equivariant ğ”¸ f) (x : Î±) :
    Equivariant ğ”¸ (f x) :=
  (equivariant_of_isDiscrete x).map hf

/-- One part of the adjunction between the discrete and global sections functors. -/
def liftDiscrete {Î± Î² : Type*} [MulPerm ğ”¸ Î²] (f : Î± â†’ Î²)
    (_hf : âˆ€ x, Equivariant ğ”¸ (f x)) :
    Discrete ğ”¸ Î± â†’ Î² :=
  f

theorem liftDiscrete_equivariant {Î± Î² : Type*} [MulPerm ğ”¸ Î²] (f : Î± â†’ Î²)
    (hf : âˆ€ x, Equivariant ğ”¸ (f x)) :
    Equivariant ğ”¸ (liftDiscrete f hf) := by
  intro Ï€
  ext x
  rw [Function.perm_def, IsDiscrete.perm_eq Ï€â»Â¹ x, liftDiscrete, hf]

/-!
# Option and Part
-/

instance {Î± : Type*} [HasPerm ğ”¸ Î±] : HasPerm ğ”¸ (Part Î±) where
  perm Ï€ x := x.map (Ï€ â¬ Â·)

omit [DecidableEq ğ”¸] in
theorem Part.perm_def {Î± : Type*} [HasPerm ğ”¸ Î±] (Ï€ : Finperm ğ”¸) (x : Part Î±) :
    Ï€ â¬ x = x.map (Ï€ â¬ Â·) :=
  rfl

@[simp]
theorem Part.mem_perm_iff {Î± : Type*} [MulPerm ğ”¸ Î±] (Ï€ : Finperm ğ”¸) (x : Part Î±) (y : Î±) :
    y âˆˆ Ï€ â¬ x â†” Ï€â»Â¹ â¬ y âˆˆ x := by
  rw [Part.perm_def, Part.mem_map_iff]
  constructor
  Â· rintro âŸ¨a, ha, rflâŸ©
    rwa [inv_perm_perm]
  Â· intro h
    use Ï€â»Â¹ â¬ y
    rw [perm_inv_perm]
    exact âŸ¨h, rflâŸ©

instance {Î± : Type*} [MulPerm ğ”¸ Î±] : MulPerm ğ”¸ (Part Î±) where
  one_perm := by
    intro x
    ext y
    simp only [Part.mem_perm_iff, inv_one, one_perm]
  mul_perm := by
    intro Ï€â‚ Ï€â‚‚ x
    ext y
    simp only [Part.mem_perm_iff, inv_one, mul_inv_rev, mul_perm]

theorem Part.supports_iff_of_dom {Î± : Type*} [MulPerm ğ”¸ Î±]
    {x : Part Î±} (hx : x.Dom) (s : Finset ğ”¸) :
    Supports s x â†” Supports s (x.get hx) := by
  constructor
  Â· intro h Ï€ hÏ€
    have := congr_arg (x.get hx âˆˆ Â·) (h Ï€ hÏ€)
    simp only [mem_perm_iff, eq_iff_iff] at this
    have := this.mpr (Part.get_mem hx)
    have := Part.get_eq_of_mem this hx
    rwa [perm_eq_iff_eq_inv_perm]
  Â· intro h Ï€ hÏ€
    have := h Ï€ hÏ€
    ext y
    rw [mem_perm_iff]
    constructor
    Â· intro h'
      rw [Part.get_eq_of_mem h' hx] at this
      rw [perm_inv_perm] at this
      rwa [â† this] at h'
    Â· intro h'
      rw [Part.get_eq_of_mem h' hx, perm_eq_iff_eq_inv_perm] at this
      rwa [this] at h'

theorem Part.supports_of_not_dom {Î± : Type*} [MulPerm ğ”¸ Î±]
    {x : Part Î±} (hx : Â¬x.Dom) (s : Finset ğ”¸) :
    Supports s x := by
  intro Ï€ hÏ€
  ext y
  rw [Part.eq_none_iff'.mpr hx]
  simp only [mem_perm_iff, Part.not_mem_none]

instance {Î± : Type*} [Nominal ğ”¸ Î±] : Nominal ğ”¸ (Part Î±) where
  supported := by
    rintro âŸ¨p, xâŸ©
    by_cases hp : p
    Â· obtain âŸ¨s, hsâŸ© := Nominal.supported (ğ”¸ := ğ”¸) (x hp)
      use s
      rwa [Part.supports_iff_of_dom]
    Â· use âˆ…
      exact Part.supports_of_not_dom hp âˆ…

theorem Part.supp_eq_of_dom {Î± : Type*} [Nominal ğ”¸ Î±] {x : Part Î±} (hx : x.Dom) :
    supp ğ”¸ x = supp ğ”¸ (x.get hx) := by
  ext a
  simp only [Nominal.mem_supp_iff, supports_iff_of_dom hx]

theorem Part.supp_eq_of_not_dom {Î± : Type*} [Nominal ğ”¸ Î±] {x : Part Î±} (hx : Â¬x.Dom) :
    supp ğ”¸ x = âˆ… := by
  ext a
  simp only [Nominal.mem_supp_iff, supports_of_not_dom hx, forall_const, Finset.not_mem_empty,
    iff_false, not_forall]
  use âˆ…
  exact Finset.not_mem_empty a

theorem Part.fresh_iff_of_dom {Î± Î² : Type*} [Nominal ğ”¸ Î±] [MulPerm ğ”¸ Î²]
    {x : Part Î±} (hx : x.Dom) (y : Î²) :
    y #[ğ”¸] x â†” y #[ğ”¸] x.get hx := by
  rw [fresh_def, fresh_def, supp_eq_of_dom hx]

theorem Part.fresh_of_not_dom {Î± Î² : Type*} [Nominal ğ”¸ Î±]
    [MulPerm ğ”¸ Î²] {x : Part Î±} (hx : Â¬x.Dom) (y : Î²) :
    y #[ğ”¸] x := by
  rw [fresh_def, supp_eq_of_not_dom hx]
  exact Finset.disjoint_empty_right _

/-!
# Binary coproducts
-/

instance {Î± Î² : Type*} [HasPerm ğ”¸ Î±] [HasPerm ğ”¸ Î²] : HasPerm ğ”¸ (Î± âŠ• Î²) where
  perm Ï€ x := x.elim (Î» x â†¦ .inl (Ï€ â¬ x)) (Î» x â†¦ .inr (Ï€ â¬ x))

omit [DecidableEq ğ”¸] in
@[simp]
theorem Sum.perm_inl {Î± Î² : Type*} [HasPerm ğ”¸ Î±] [HasPerm ğ”¸ Î²] (Ï€ : Finperm ğ”¸) (x : Î±) :
    (Ï€ â¬ .inl x : Î± âŠ• Î²) = .inl (Ï€ â¬ x) :=
  rfl

omit [DecidableEq ğ”¸] in
@[simp]
theorem Sum.perm_inr {Î± Î² : Type*} [HasPerm ğ”¸ Î±] [HasPerm ğ”¸ Î²] (Ï€ : Finperm ğ”¸) (x : Î²) :
    (Ï€ â¬ .inr x : Î± âŠ• Î²) = .inr (Ï€ â¬ x) :=
  rfl

instance {Î± Î² : Type*} [MulPerm ğ”¸ Î±] [MulPerm ğ”¸ Î²] : MulPerm ğ”¸ (Î± âŠ• Î²) where
  one_perm x := by
    cases x <;> simp only [Sum.perm_inl, Sum.perm_inr, one_perm]
  mul_perm Ï€â‚ Ï€â‚‚ x := by
    cases x <;> simp only [Sum.perm_inl, Sum.perm_inr, mul_perm]

theorem Sum.inl_equivariant {Î± Î² : Type*} [MulPerm ğ”¸ Î±] [MulPerm ğ”¸ Î²] :
    Equivariant ğ”¸ (Sum.inl : Î± â†’ Î± âŠ• Î²) := by
  simp only [Function.equivariant_iff, perm_inl, implies_true]

theorem Sum.inr_equivariant {Î± Î² : Type*} [MulPerm ğ”¸ Î±] [MulPerm ğ”¸ Î²] :
    Equivariant ğ”¸ (Sum.inr : Î² â†’ Î± âŠ• Î²) := by
  simp only [Function.equivariant_iff, perm_inr, implies_true]

instance {Î± Î² : Type*} [Nominal ğ”¸ Î±] [Nominal ğ”¸ Î²] : Nominal ğ”¸ (Î± âŠ• Î²) where
  supported := by
    rintro (x | x)
    Â· exact (Nominal.supported x).map Sum.inl Sum.inl_equivariant
    Â· exact (Nominal.supported x).map Sum.inr Sum.inr_equivariant

/-- This proves that `Î± âŠ• Î²` is the coproduct of `Î±` and `Î²` in the category of nominal sets. -/
def Sum.elim_equivariant {Î± Î² Î³ : Type*}
    [MulPerm ğ”¸ Î±] [MulPerm ğ”¸ Î²] [MulPerm ğ”¸ Î³]
    (f : Î± â†’ Î³) (g : Î² â†’ Î³) (hf : Equivariant ğ”¸ f) (hg : Equivariant ğ”¸ g) :
    Equivariant ğ”¸ (Sum.elim f g) := by
  rw [Function.equivariant_iff]
  rintro Ï€ (x | x)
  Â· rw [elim_inl, perm_inl, apply_perm_eq hf, elim_inl]
  Â· rw [elim_inr, perm_inr, apply_perm_eq hg, elim_inr]

@[simp]
theorem Sum.supp_inl {Î± Î² : Type*} [Nominal ğ”¸ Î±] [Nominal ğ”¸ Î²] (x : Î±) :
    supp ğ”¸ (Sum.inl x : Î± âŠ• Î²) = supp ğ”¸ x :=
  supp_apply_eq_of_injective (Sum.inl : Î± â†’ Î± âŠ• Î²) Sum.inl_injective Sum.inl_equivariant x

@[simp]
theorem Sum.supp_inr {Î± Î² : Type*} [Nominal ğ”¸ Î±] [Nominal ğ”¸ Î²] (x : Î²) :
    supp ğ”¸ (Sum.inr x : Î± âŠ• Î²) = supp ğ”¸ x :=
  supp_apply_eq_of_injective (Sum.inr : Î² â†’ Î± âŠ• Î²) Sum.inr_injective Sum.inr_equivariant x

/-!
# Binary products
-/

instance {Î± Î² : Type*} [HasPerm ğ”¸ Î±] [HasPerm ğ”¸ Î²] : HasPerm ğ”¸ (Î± Ã— Î²) where
  perm Ï€ x := (Ï€ â¬ x.1, Ï€ â¬ x.2)

omit [DecidableEq ğ”¸] in
theorem Prod.perm_def {Î± Î² : Type*} [HasPerm ğ”¸ Î±] [HasPerm ğ”¸ Î²] (Ï€ : Finperm ğ”¸) (x : Î± Ã— Î²) :
    Ï€ â¬ x = (Ï€ â¬ x.1, Ï€ â¬ x.2) :=
  rfl

omit [DecidableEq ğ”¸] in
@[simp]
theorem Prod.perm_mk {Î± Î² : Type*} [HasPerm ğ”¸ Î±] [HasPerm ğ”¸ Î²] (Ï€ : Finperm ğ”¸) (x : Î±) (y : Î²) :
    Ï€ â¬ (x, y) = (Ï€ â¬ x, Ï€ â¬ y) :=
  rfl

instance {Î± Î² : Type*} [MulPerm ğ”¸ Î±] [MulPerm ğ”¸ Î²] : MulPerm ğ”¸ (Î± Ã— Î²) where
  one_perm x := by rw [Prod.perm_def, one_perm, one_perm]
  mul_perm Ï€â‚ Ï€â‚‚ x := by rw [Prod.perm_def, mul_perm, mul_perm]; rfl

theorem Prod.fst_equivariant {Î± Î² : Type*} [MulPerm ğ”¸ Î±] [MulPerm ğ”¸ Î²] :
    Equivariant ğ”¸ (Prod.fst : Î± Ã— Î² â†’ Î±) := by
  simp only [Function.equivariant_iff, Prod.forall, perm_mk, implies_true]

theorem Prod.snd_equivariant {Î± Î² : Type*} [MulPerm ğ”¸ Î±] [MulPerm ğ”¸ Î²] :
    Equivariant ğ”¸ (Prod.snd : Î± Ã— Î² â†’ Î²) := by
  simp only [Function.equivariant_iff, Prod.forall, perm_mk, implies_true]

theorem Supports.pair {Î± Î² : Type*} [MulPerm ğ”¸ Î±] [MulPerm ğ”¸ Î²]
    {s t : Finset ğ”¸} {x : Î±} {y : Î²}
    (hs : Supports s x) (ht : Supports t y) :
    Supports (s âˆª t) (x, y) := by
  intro Ï€ hÏ€
  aesop

theorem FinitelySupported.pair {Î± Î² : Type*} [MulPerm ğ”¸ Î±] [MulPerm ğ”¸ Î²]
    {x : Î±} {y : Î²} (hx : FinitelySupported ğ”¸ x) (hy : FinitelySupported ğ”¸ y) :
    FinitelySupported ğ”¸ (x, y) := by
  obtain âŸ¨s, hsâŸ© := hx
  obtain âŸ¨t, htâŸ© := hy
  exact âŸ¨s âˆª t, hs.pair htâŸ©

instance {Î± Î² : Type*} [Nominal ğ”¸ Î±] [Nominal ğ”¸ Î²] : Nominal ğ”¸ (Î± Ã— Î²) where
  supported x := (Nominal.supported x.1).pair (Nominal.supported x.2)

/-- This proves that `Î± Ã— Î²` is the product of `Î±` and `Î²` in the category of nominal sets. -/
theorem Prod.pair_equivariant {Î± Î² Î³ : Type*}
    [MulPerm ğ”¸ Î±] [MulPerm ğ”¸ Î²] [MulPerm ğ”¸ Î³]
    (f : Î³ â†’ Î±) (g : Î³ â†’ Î²) (hf : Equivariant ğ”¸ f) (hg : Equivariant ğ”¸ g) :
    Equivariant ğ”¸ (Î» x â†¦ (f x, g x)) := by
  rw [Function.equivariant_iff]
  intro Ï€ x
  rw [perm_mk, apply_perm_eq hf, apply_perm_eq hg]

@[simp]
theorem Prod.supp_mk [Infinite ğ”¸] {Î± Î² : Type*} [Nominal ğ”¸ Î±] [Nominal ğ”¸ Î²] (x : Î±) (y : Î²) :
    supp ğ”¸ (x, y) = supp ğ”¸ x âˆª supp ğ”¸ y := by
  apply subset_antisymm
  Â· apply supp_minimal
    apply Supports.pair
    Â· exact Nominal.supp_supports ğ”¸ x
    Â· exact Nominal.supp_supports ğ”¸ y
  Â· apply Finset.union_subset
    Â· exact supp_apply_subset fst fst_equivariant (x, y)
    Â· exact supp_apply_subset snd snd_equivariant (x, y)

@[simp]
theorem Prod.fresh_iff [Infinite ğ”¸] {Î± Î² Î³ : Type*} [Nominal ğ”¸ Î±] [Nominal ğ”¸ Î²] [Nominal ğ”¸ Î³]
    (x : Î±) (y : Î²) (z : Î³) :
    z #[ğ”¸] (x, y) â†” z #[ğ”¸] x âˆ§ z #[ğ”¸] y := by
  rw [fresh_def, fresh_def, fresh_def, supp_mk, Finset.disjoint_union_right]

theorem Equivariant.uncurry {Î± Î² : Type*} {Î³ : Sort*} [MulPerm ğ”¸ Î±] [MulPerm ğ”¸ Î²] [MulPerm ğ”¸ Î³]
    {f : Î± â†’ Î² â†’ Î³} (h : Equivariant ğ”¸ f) : Equivariant ğ”¸ (Function.uncurry f) := by
  simp only [Function.equivariant_iff, funext_iff, Function.perm_def, Prod.forall,
    Function.uncurry_apply_pair, Prod.perm_mk] at h âŠ¢
  intro Ï€ x y
  rw [â† h Ï€ x (Ï€ â¬ y), inv_perm_perm]

theorem Equivariant.uncurryâ‚‚ {Î± Î² Î³ : Type*} {Î´ : Sort*} [MulPerm ğ”¸ Î±] [MulPerm ğ”¸ Î²]
    [MulPerm ğ”¸ Î³] [MulPerm ğ”¸ Î´] {f : Î± â†’ Î² â†’ Î³ â†’ Î´} (h : Equivariant ğ”¸ f) :
    Equivariant ğ”¸ (Î» x (y : Î² Ã— Î³) â†¦ f x y.1 y.2) := by
  simp only [Function.equivariant_iff, funext_iff, Function.perm_def, Prod.forall,
    Function.uncurry_apply_pair, Prod.perm_mk] at h âŠ¢
  intro Ï€ x y z
  rw [â† h Ï€ x y z]

/-!
# Equalisers
-/

def Nominal.Equaliser {Î± Î² : Type*} [MulPerm ğ”¸ Î±] [MulPerm ğ”¸ Î²]
    (f g : Î± â†’ Î²) (_hf : Equivariant ğ”¸ f) (_hg : Equivariant ğ”¸ g) :=
  {x : Î± // f x = g x}

namespace Nominal.Equaliser

variable {Î± Î² : Type*} [MulPerm ğ”¸ Î±] [MulPerm ğ”¸ Î²]
    {f g : Î± â†’ Î²} {hf : Equivariant ğ”¸ f} {hg : Equivariant ğ”¸ g}

protected def val (x : Equaliser f g hf hg) : Î± :=
  Subtype.val x

attribute [coe] Equaliser.val

instance : CoeOut (Equaliser f g hf hg) Î± where
  coe := Equaliser.val

protected theorem prop (x : Equaliser f g hf hg) :
    f (x : Î±) = g (x : Î±) :=
  Subtype.prop x

@[ext]
protected theorem ext {x y : Equaliser f g hf hg} (h : (x : Î±) = y) : x = y :=
  Subtype.ext h

theorem val_injective :
    Function.Injective (Equaliser.val : Equaliser f g hf hg â†’ Î±) :=
  Subtype.val_injective

instance : HasPerm ğ”¸ (Equaliser f g hf hg) where
  perm Ï€ x := âŸ¨Ï€ â¬ (x : Î±), by rw [â† apply_perm_eq hf, â† apply_perm_eq hg, x.prop]âŸ©

@[simp]
theorem perm_coe (Ï€ : Finperm ğ”¸) (x : Equaliser f g hf hg) :
    ((Ï€ â¬ x : Equaliser f g hf hg) : Î±) = Ï€ â¬ (x : Î±) :=
  rfl

instance : MulPerm ğ”¸ (Equaliser f g hf hg) where
  one_perm _ := by
    ext
    rw [perm_coe, one_perm]
  mul_perm _ _ _ := by
    ext
    rw [perm_coe, perm_coe, perm_coe, mul_perm]

instance {Î± Î² : Type*} [Nominal ğ”¸ Î±] [MulPerm ğ”¸ Î²]
    {f g : Î± â†’ Î²} {hf : Equivariant ğ”¸ f} {hg : Equivariant ğ”¸ g}
    [Infinite ğ”¸] : Nominal ğ”¸ (Equaliser f g hf hg) where
  supported x := by
    use supp ğ”¸ (x : Î±)
    intro Ï€ hÏ€
    ext
    exact supp_supports ğ”¸ (x : Î±) Ï€ hÏ€

theorem val_equivariant : Equivariant ğ”¸ (Equaliser.val : Equaliser f g hf hg â†’ Î±) := by
  simp only [Function.equivariant_iff, perm_coe, implies_true]

def factor (f g : Î± â†’ Î²) (hf : Equivariant ğ”¸ f) (hg : Equivariant ğ”¸ g)
    {Î³ : Type*} [MulPerm ğ”¸ Î³] (h : Î³ â†’ Î±) (hfh : âˆ€ x, f (h x) = g (h x))
    (x : Î³) : Equaliser f g hf hg :=
  âŸ¨h x, hfh xâŸ©

theorem factor_equivariant {f g : Î± â†’ Î²} {hf : Equivariant ğ”¸ f} {hg : Equivariant ğ”¸ g}
    {Î³ : Type*} [MulPerm ğ”¸ Î³] {h : Î³ â†’ Î±} {hfh : âˆ€ x, f (h x) = g (h x)}
    (hh : Equivariant ğ”¸ h) :
    Equivariant ğ”¸ (factor f g hf hg h hfh) := by
  rw [Function.equivariant_iff]
  intro Ï€ hÏ€
  ext
  exact apply_perm_eq hh Ï€ hÏ€

end Nominal.Equaliser

/-!
# Initial and terminal object
-/

instance {Î± : Type*} [Subsingleton Î±] : MulPerm ğ”¸ Î± where
  perm _ := id
  one_perm _ := rfl
  mul_perm _ _ _ := rfl

instance {Î± : Type*} [Subsingleton Î±] : Nominal ğ”¸ Î± where
  supported _ := âŸ¨âˆ…, Î» _ _ â†¦ rflâŸ©

theorem equivariant_of_isEmpty {Î± Î² : Type*} [IsEmpty Î±] [MulPerm ğ”¸ Î²] (f : Î± â†’ Î²) :
    Equivariant ğ”¸ f := by
  intro Ï€
  ext x
  cases IsEmpty.false x

theorem equivariant_of_subsingleton {Î± Î² : Type*} [MulPerm ğ”¸ Î±] [Subsingleton Î²] (f : Î± â†’ Î²) :
    Equivariant ğ”¸ f := by
  intro Ï€
  ext x
  apply Subsingleton.allEq

/-!
# Coreflection

We show that the category of nominal sets is coreflective in the category of `Finperm ğ”¸`-sets.
-/

/-- A finitely supported element of `Î±`. -/
def FS (ğ”¸ : Type*) [DecidableEq ğ”¸] (Î± : Type*) [MulPerm ğ”¸ Î±] :=
  {x : Î± // FinitelySupported ğ”¸ x}

def FS.val {Î± : Type*} [MulPerm ğ”¸ Î±] (x : FS ğ”¸ Î±) : Î± :=
  Subtype.val x

attribute [coe] FS.val

instance {Î± : Type*} [MulPerm ğ”¸ Î±] : CoeOut (FS ğ”¸ Î±) Î± where
  coe := FS.val

theorem FS.prop {Î± : Type*} [MulPerm ğ”¸ Î±] (x : FS ğ”¸ Î±) : FinitelySupported ğ”¸ (x : Î±) :=
  Subtype.prop x

@[ext]
theorem FS.ext {Î± : Type*} [MulPerm ğ”¸ Î±] {x y : FS ğ”¸ Î±} (h : (x : Î±) = y) : x = y :=
  Subtype.ext h

theorem FS.val_injective {Î± : Type*} [MulPerm ğ”¸ Î±] :
    Function.Injective (FS.val : FS ğ”¸ Î± â†’ Î±) :=
  Subtype.val_injective

instance {Î± : Type*} [MulPerm ğ”¸ Î±] : HasPerm ğ”¸ (FS ğ”¸ Î±) where
  perm Ï€ x := âŸ¨Ï€ â¬ (x : Î±), x.prop.perm Ï€âŸ©

@[simp]
theorem FS.perm_coe {Î± : Type*} [MulPerm ğ”¸ Î±] (x : FS ğ”¸ Î±) (Ï€ : Finperm ğ”¸) :
    ((Ï€ â¬ x : FS ğ”¸ Î±) : Î±) = Ï€ â¬ x :=
  rfl

instance {Î± : Type*} [MulPerm ğ”¸ Î±] : MulPerm ğ”¸ (FS ğ”¸ Î±) where
  one_perm _ := FS.ext (by rw [FS.perm_coe, one_perm])
  mul_perm _ _ _ := FS.ext (by simp only [FS.perm_coe, mul_perm])

theorem FS.val_equivariant {Î± : Type*} [MulPerm ğ”¸ Î±] :
    Equivariant ğ”¸ (FS.val (ğ”¸ := ğ”¸) (Î± := Î±)) := by
  rw [Function.equivariant_iff]
  intro Ï€ x
  rfl

instance {Î± : Type*} [MulPerm ğ”¸ Î±] : Nominal ğ”¸ (FS ğ”¸ Î±) where
  supported x := x.prop.of_map FS.val_injective FS.val_equivariant

@[simp]
theorem FS.supports_iff {Î± : Type*} [MulPerm ğ”¸ Î±] (x : FS ğ”¸ Î±) (s : Finset ğ”¸) :
    Supports s x â†” Supports s (x : Î±) :=
  âŸ¨Î» h â†¦ h.map val val_equivariant, Î» h â†¦ h.of_map val_injective val_equivariantâŸ©

/-- The factorisation of an equivariant function from a nominal set through the finitely supported
elements of its codomain. -/
def Equivariant.toFS {Î± Î² : Type*} [Nominal ğ”¸ Î±] [MulPerm ğ”¸ Î²]
    {f : Î± â†’ Î²} (hf : Equivariant ğ”¸ f) (x : Î±) : FS ğ”¸ Î² :=
  âŸ¨f x, (Nominal.supported x).map f hfâŸ©

@[simp]
theorem FS.supp_eq {Î± : Type*} [MulPerm ğ”¸ Î±] (x : FS ğ”¸ Î±) :
    supp ğ”¸ x = supp ğ”¸ x.val := by
  ext a
  simp only [Nominal.mem_supp_iff, supports_iff, mem_supp_iff' _ x.prop]

@[simp]
theorem FS.fresh_iff {Î± Î² : Type*} [MulPerm ğ”¸ Î±] [MulPerm ğ”¸ Î²] (x : FS ğ”¸ Î±) (y : Î²) :
    y #[ğ”¸] x â†” y #[ğ”¸] x.val := by
  rw [fresh_def, fresh_def, supp_eq]

/-!
# Function types

As the `perm` instance we want to define is incompatible with the usual one, we use a structure.
We will never put a `Nominal` instance on a general `Î ` type.
However, we can put various instances on relation types, because this doesn't conflict with
`Pi.perm` (we don't define a `Nominal` instance for `Prop`).
-/

structure FinpermMap (Î± Î² : Type*) where
  protected toFun : Î± â†’ Î²

infixr:25 " â†’á¶ áµ– " => FinpermMap

instance FinpermMap.funLike {Î± Î² : Type*} : FunLike (Î± â†’á¶ áµ– Î²) Î± Î² where
  coe := FinpermMap.toFun
  coe_injective' f g h := by cases f; congr

@[ext]
theorem ext {Î± Î² : Type*} {f g : Î± â†’á¶ áµ– Î²} (h : âˆ€ x, f x = g x) : f = g := by
  cases f
  cases g
  rw [FinpermMap.mk.injEq]
  ext x
  exact h x

@[simp]
theorem FinpermMap.mk_apply {Î± Î² : Type*} (f : Î± â†’ Î²) (x : Î±) :
    (âŸ¨fâŸ© : Î± â†’á¶ áµ– Î²) x = f x :=
  rfl

instance {Î± Î² : Type*} [MulPerm ğ”¸ Î±] [MulPerm ğ”¸ Î²] :
    HasPerm ğ”¸ (Î± â†’á¶ áµ– Î²) where
  perm Ï€ f := âŸ¨Î» x â†¦ Ï€ â¬ f (Ï€â»Â¹ â¬ x)âŸ©

@[simp]
theorem FinpermMap.perm_def {Î± Î² : Type*} [MulPerm ğ”¸ Î±] [MulPerm ğ”¸ Î²]
    (f : Î± â†’á¶ áµ– Î²) (x : Î±) (Ï€ : Finperm ğ”¸) :
    (Ï€ â¬ f) x = Ï€ â¬ f (Ï€â»Â¹ â¬ x) :=
  rfl

@[simp]
theorem FinpermMap.perm_apply_perm {Î± Î² : Type*} [MulPerm ğ”¸ Î±] [MulPerm ğ”¸ Î²]
    (f : Î± â†’á¶ áµ– Î²) (x : Î±) (Ï€ : Finperm ğ”¸) :
    (Ï€ â¬ f) (Ï€ â¬ x) = Ï€ â¬ f x := by
  rw [perm_def, inv_perm_perm]

theorem FinpermMap.perm_eq_iff {Î± Î² : Type*} [MulPerm ğ”¸ Î±] [MulPerm ğ”¸ Î²]
    (f : Î± â†’á¶ áµ– Î²) (Ï€ : Finperm ğ”¸) :
    Ï€ â¬ f = f â†” âˆ€ x, Ï€ â¬ f x = f (Ï€ â¬ x) := by
  constructor
  Â· intro h x
    rw [â† perm_apply_perm, h]
  Â· intro h
    apply DFunLike.ext
    intro x
    rw [perm_def, h, perm_inv_perm]

instance {Î± Î² : Type*} [MulPerm ğ”¸ Î±] [MulPerm ğ”¸ Î²] :
    MulPerm ğ”¸ (FinpermMap Î± Î²) where
  one_perm _ := by
    apply DFunLike.ext
    simp only [FinpermMap.perm_def, inv_one, one_perm, implies_true]
  mul_perm _ _ _ := by
    apply DFunLike.ext
    simp only [FinpermMap.perm_def, mul_inv_rev, mul_perm, implies_true]

theorem FinpermMap.supports_iff {Î± Î² : Type*} [MulPerm ğ”¸ Î±] [MulPerm ğ”¸ Î²]
    (f : FinpermMap Î± Î²) (s : Finset ğ”¸) :
    Supports s f â†”
    âˆ€ Ï€ : Finperm ğ”¸, (âˆ€ a âˆˆ s, Ï€ a = a) â†’ âˆ€ x, Ï€ â¬ f x = f (Ï€ â¬ x) := by
  simp_rw [â† perm_eq_iff]
  rfl

theorem FinitelySupported.graph {Î± Î² : Sort*} [MulPerm ğ”¸ Î±] [MulPerm ğ”¸ Î²] {f : Î± â†’ Î²}
    (h : FinitelySupported ğ”¸ f) :
    FinitelySupported ğ”¸ (Î» x y â†¦ f x = y) := by
  simp only [Function.finitelySupported_iff, funext_iff, Function.perm_def, IsDiscrete.perm_eq,
    eq_iff_iff] at h âŠ¢
  obtain âŸ¨s, hsâŸ© := h
  use s
  intro Ï€ hÏ€ x y
  rw [â† hs Ï€ hÏ€, perm_eq_iff_eq_inv_perm]

/-!
# Finitely supported functions
-/

/-- A finitely supported map from `Î±` to `Î²`. -/
structure NominalMap (ğ”¸ Î± Î² : Type*) [DecidableEq ğ”¸]
    [MulPerm ğ”¸ Î±] [MulPerm ğ”¸ Î²]
    extends Î± â†’á¶ áµ– Î² where
  supported' : FinitelySupported ğ”¸ toFinpermMap

notation:25 Î± " â†’â‚™[" ğ”¸:25 "] " Î²:0 => NominalMap ğ”¸ Î± Î²

instance NominalMap.funLike {Î± Î² : Type*} [MulPerm ğ”¸ Î±] [MulPerm ğ”¸ Î²] :
    FunLike (Î± â†’â‚™[ğ”¸] Î²) Î± Î² where
  coe f := f.toFun
  coe_injective' f g h := by
    cases f
    congr
    apply DFunLike.coe_injective
    exact h
