import Mathlib.CategoryTheory.Limits.Shapes.Pullback.Cospan
import Mathlib.Data.Finite.Sum
import Mathlib.Logic.Embedding.Basic
import Nominal.Category.Defs

/-!
# The category of finite sets and injections

This small category is not filtered, but has a cocone for every span.
Fortunately, this is enough to make the usual construction of filtered colimits work.
-/

open CategoryTheory Function Functor Limits Finset

@[ext]
structure FinInj (ğ”¸ : Type*) where
  val : Finset ğ”¸

open FinInj

attribute [coe] FinInj.val

instance {ğ”¸ : Type*} : Category (FinInj ğ”¸) where
  Hom s t := s.val â†ª t.val
  id s := Embedding.refl s.val
  comp := Embedding.trans

instance {ğ”¸ : Type*} {s t : FinInj ğ”¸} :
    FunLike (s âŸ¶ t) s.val t.val :=
  inferInstanceAs (FunLike (s.val â†ª t.val) s.val t.val)

@[simp]
theorem FinInj.id_coe {ğ”¸ : Type*} (s : FinInj ğ”¸) :
    ((ğŸ™ s : s âŸ¶ s) : s.val â†’ s.val) = id :=
  rfl

@[simp]
theorem FinInj.comp_coe {ğ”¸ : Type*} {s t u : FinInj ğ”¸} (f : s âŸ¶ t) (g : t âŸ¶ u) :
    ((f â‰« g) : s.val â†’ u.val) = g âˆ˜ f :=
  rfl

@[simp]
theorem FinInj.apply_eq_iff_eq {ğ”¸ : Type*} {s t : FinInj ğ”¸} {f : s âŸ¶ t} (x y : s.val) :
    f x = f y â†” x = y :=
  f.apply_eq_iff_eq x y

@[simp]
theorem FinInj.mk_coe_eq {ğ”¸ : Type*} {s t : FinInj ğ”¸}
    (f : s.val â†’ t.val) (h : Function.Injective f) :
    ((âŸ¨f, hâŸ© : s âŸ¶ t) : s.val â†’ t.val) = f :=
  rfl

@[simp]
theorem FinInj.mk_apply_eq {ğ”¸ : Type*} {s t : FinInj ğ”¸}
    (f : s.val â†’ t.val) (h : Function.Injective f) (x : s.val) :
    (âŸ¨f, hâŸ© : s âŸ¶ t) x = f x :=
  rfl

instance {ğ”¸ : Type*} : Nonempty (FinInj ğ”¸) :=
  âŸ¨âŸ¨âˆ…âŸ©âŸ©

/-!
## Pullbacks
-/

def FinInj.pullbackConeApex {ğ”¸ : Type*} [DecidableEq ğ”¸] [Infinite ğ”¸]
    (F : WalkingCospan â¥¤ FinInj ğ”¸) :
    FinInj ğ”¸ :=
  âŸ¨((F.obj (some .left)).val.attach.filter
    (Î» x â†¦ âˆƒ y, F.map (.term .left) x = F.map (.term .right) y)).map
    (Embedding.subtype _)âŸ©

theorem FinInj.mem_pullbackConeApex_iff {ğ”¸ : Type*} [DecidableEq ğ”¸] [Infinite ğ”¸]
    (F : WalkingCospan â¥¤ FinInj ğ”¸) (x : ğ”¸) :
    x âˆˆ (pullbackConeApex F).val â†”
      âˆƒ (h : x âˆˆ (F.obj (some .left)).val),
      âˆƒ y, F.map (.term .left) âŸ¨x, hâŸ© = F.map (.term .right) y := by
  simp only [pullbackConeApex, Subtype.exists, mem_map, mem_filter, mem_attach, true_and,
    Embedding.coe_subtype, exists_and_right, exists_eq_right]

def FinInj.pullbackConeMapNone {ğ”¸ : Type*} [DecidableEq ğ”¸] [Infinite ğ”¸]
    (F : WalkingCospan â¥¤ FinInj ğ”¸) :
    pullbackConeApex F âŸ¶ F.obj none where
  toFun x := âŸ¨F.map (.term .left) âŸ¨x,
    by
      have := x.prop
      rw [mem_pullbackConeApex_iff] at this
      obtain âŸ¨hx, y, hâŸ© := this
      exact hxâŸ©,
    Finset.coe_mem _âŸ©
  inj' := by
    rintro âŸ¨y, hyâŸ© âŸ¨z, hzâŸ© h
    rw [mem_pullbackConeApex_iff] at hy hz
    simp only [Subtype.mk.injEq] at h
    obtain âŸ¨hy, a, haâŸ© := hy
    obtain âŸ¨hz, b, hbâŸ© := hz
    rw [ha, hb] at h
    have := (F.map (.term .right)).injective (Subtype.coe_injective h)
    rw [Subtype.mk.injEq] at this
    cases Subtype.coe_injective this
    rw [â† hb] at ha
    have := (F.map (.term .left)).injective ha
    rw [Subtype.mk.injEq] at this
    cases this
    rfl

def FinInj.pullbackConeMapLeft {ğ”¸ : Type*} [DecidableEq ğ”¸] [Infinite ğ”¸]
    (F : WalkingCospan â¥¤ FinInj ğ”¸) :
    pullbackConeApex F âŸ¶ F.obj (some .left) where
  toFun x := âŸ¨x,
    by
      have := x.prop
      rw [mem_pullbackConeApex_iff] at this
      obtain âŸ¨hx, y, hyâŸ© := this
      exact hxâŸ©
  inj' := by
    rintro âŸ¨x, hxâŸ© âŸ¨y, hyâŸ© h
    rw [mem_pullbackConeApex_iff] at hx hy
    simp only [Subtype.mk.injEq] at h
    cases h
    rfl

def FinInj.pullbackConeMapRight' {ğ”¸ : Type*} [DecidableEq ğ”¸] [Infinite ğ”¸]
    (F : WalkingCospan â¥¤ FinInj ğ”¸) (x : (pullbackConeApex F).val) :
    (F.obj (some .right)).val :=
  (F.obj (some .right)).val.attach.choose
  (Î» y â†¦ F.map (.term .left) (pullbackConeMapLeft F x) = F.map (.term .right) y) <| by
    have := x.prop
    rw [mem_pullbackConeApex_iff] at this
    obtain âŸ¨hx, y, hyâŸ© := this
    refine âŸ¨y, âŸ¨mem_attach _ _, hyâŸ©, ?_âŸ©
    rintro âŸ¨z, hzâŸ© âŸ¨hz', h'âŸ©
    exact (F.map (.term .right)).injective (h'.symm.trans hy)

theorem FinInj.pullbackConeMapRight'_property {ğ”¸ : Type*} [DecidableEq ğ”¸] [Infinite ğ”¸]
    (F : WalkingCospan â¥¤ FinInj ğ”¸) (x : (pullbackConeApex F).val) :
    F.map (.term .left) (pullbackConeMapLeft F x) =
    F.map (.term .right) (pullbackConeMapRight' F x) :=
  (F.obj (some .right)).val.attach.choose_property
    (Î» y â†¦ F.map (.term .left) (pullbackConeMapLeft F x) = F.map (.term .right) y) _

def FinInj.pullbackConeMapRight {ğ”¸ : Type*} [DecidableEq ğ”¸] [Infinite ğ”¸]
    (F : WalkingCospan â¥¤ FinInj ğ”¸) :
    pullbackConeApex F âŸ¶ F.obj (some .right) where
  toFun := pullbackConeMapRight' F
  inj' := by
    intro x y h
    apply (pullbackConeMapLeft F).injective
    apply (F.map (.term .left)).injective
    apply (pullbackConeMapRight'_property F x).trans
    exact (h â–¸ pullbackConeMapRight'_property F y).symm

theorem FinInj.pullbackConeMapRight_comp {ğ”¸ : Type*} [DecidableEq ğ”¸] [Infinite ğ”¸]
    (F : WalkingCospan â¥¤ FinInj ğ”¸) :
    pullbackConeMapRight F â‰« F.map (.term WalkingPair.right) = pullbackConeMapNone F := by
  apply DFunLike.coe_injective
  ext x : 1
  exact (pullbackConeMapRight'_property F x).symm

def FinInj.pullbackCone {ğ”¸ : Type*} [DecidableEq ğ”¸] [Infinite ğ”¸]
    (F : WalkingCospan â¥¤ FinInj ğ”¸) :
    Cone F where
  pt := pullbackConeApex F
  Ï€ := {
    app x := match x with
      | none => pullbackConeMapNone F
      | some .left => pullbackConeMapLeft F
      | some .right => pullbackConeMapRight F
    naturality {s t} f := by
      cases f
      case id =>
        simp only [const_obj_obj, WidePullbackShape.hom_id, const_obj_map, Category.id_comp,
          CategoryTheory.Functor.map_id, Category.comp_id]
      case term i =>
        cases i
        case left => rfl
        case right =>
          simp only [const_obj_obj, const_obj_map, Category.id_comp, pullbackConeMapRight_comp]
  }

def FinInj.pullbackConeLift {ğ”¸ : Type*} [DecidableEq ğ”¸] [Infinite ğ”¸]
    (F : WalkingCospan â¥¤ FinInj ğ”¸) (s : Cone F) :
    s.pt âŸ¶ pullbackConeApex F :=
  âŸ¨Î» x â†¦ âŸ¨s.Ï€.app (some .left) x,
    by
      rw [mem_pullbackConeApex_iff]
      refine âŸ¨?_, s.Ï€.app (some .right) x, ?_âŸ©
      Â· simp only [const_obj_obj, coe_mem]
      Â· exact (congr_arg (Â· x) (s.w (.term .left))).trans (congr_arg (Â· x) (s.w (.term .right))).symmâŸ©,
    by
      intro x y h
      simp only [const_obj_obj, Subtype.mk.injEq] at h
      exact (s.Ï€.app (some .left)).injective (Subtype.coe_injective h)âŸ©

def FinInj.pullbackCone_isLimit {ğ”¸ : Type*} [DecidableEq ğ”¸] [Infinite ğ”¸]
    (F : WalkingCospan â¥¤ FinInj ğ”¸) :
    IsLimit (pullbackCone F) where
  lift := pullbackConeLift F
  fac s j := by
    apply DFunLike.coe_injective
    ext x : 1
    cases j with
    | none => exact congr_arg (Â· x) (s.w (.term .left))
    | some j =>
      cases j with
      | left => rfl
      | right =>
        apply (F.map (.term .right)).injective
        have hâ‚ := congr_arg (Â· (pullbackConeLift F s x)) ((pullbackCone F).w (.term .right))
        have hâ‚‚ := congr_arg (Â· x) (s.w (.term .right))
        have hâ‚ƒ := congr_arg (Â· x) (s.w (.term .left))
        exact (hâ‚.trans hâ‚ƒ).trans hâ‚‚.symm
  uniq s m h := by
    apply DFunLike.coe_injective
    ext x : 2
    simp only [pullbackConeLift, const_obj_obj]
    change _ = (s.Ï€.app (some .left) x).val
    rw [â† h]
    rfl

instance FinInj.hasPullbacks {ğ”¸ : Type*} [DecidableEq ğ”¸] [Infinite ğ”¸] :
    HasLimitsOfShape WalkingCospan (FinInj ğ”¸) :=
  âŸ¨Î» F â†¦ âŸ¨_, pullbackCone_isLimit FâŸ©âŸ©

/-!
## Pushout cocones
-/

inductive FinInj.SpanCoconeApex {ğ”¸ : Type*} [DecidableEq ğ”¸]
    (F : WalkingSpan â¥¤ FinInj ğ”¸) where
  | inl : (F.obj (some .left)).val â†’ FinInj.SpanCoconeApex F
  | inr' : (y : (F.obj (some .right)).val) â†’ (âˆ€ x, y â‰  F.map (.init .right) x) â†’
      FinInj.SpanCoconeApex F

noncomputable def FinInj.SpanCoconeApex.inr {ğ”¸ : Type*} [DecidableEq ğ”¸]
    {F : WalkingSpan â¥¤ FinInj ğ”¸} (y : (F.obj (some .right)).val) : SpanCoconeApex F :=
  if h : âˆƒ x, y = F.map (.init .right) x then
    .inl (F.map (.init .left) h.choose)
  else
    .inr' y (by push_neg at h; exact h)

theorem FinInj.SpanCoconeApex.inr_injective {ğ”¸ : Type*} [DecidableEq ğ”¸]
    {F : WalkingSpan â¥¤ FinInj ğ”¸} :
    Function.Injective (.inr : (F.obj (some .right)).val â†’ SpanCoconeApex F) := by
  intro x y h
  rw [inr, inr] at h
  split_ifs at h with hâ‚ hâ‚‚
  case pos =>
    rw [inl.injEq, apply_eq_iff_eq] at h
    rw [hâ‚.choose_spec, hâ‚‚.choose_spec, h]
  case neg =>
    cases h
    rfl

theorem FinInj.SpanCoconeApex.inl_eq_inr {ğ”¸ : Type*} [DecidableEq ğ”¸]
    {F : WalkingSpan â¥¤ FinInj ğ”¸} (x : (F.obj none).val) :
    SpanCoconeApex.inl (F.map (.init .left) x) =
      SpanCoconeApex.inr (F.map (.init .right) x) := by
  have : âˆƒ y, F.map (.init .right) x = F.map (.init .right) y := âŸ¨x, rflâŸ©
  rw [inr, dif_pos this, inl.injEq, apply_eq_iff_eq]
  exact (F.map (.init .right)).injective this.choose_spec

instance {ğ”¸ : Type*} [DecidableEq ğ”¸] (F : WalkingSpan â¥¤ FinInj ğ”¸) :
    Finite (SpanCoconeApex F) := by
  refine Finite.of_injective (Î² := (F.obj (some .left)).val âŠ• (F.obj (some .right)).val)
    (Î» x â†¦ match x with
      | .inl x => .inl x
      | .inr' x _ => .inr x) ?_
  intro x y h
  aesop

theorem Finset.exists_equiv_of_finite (ğ”¸ : Type*) [Infinite ğ”¸] (Î² : Type*) [inst : Finite Î²] :
    Nonempty ((s : Finset ğ”¸) Ã— (Î² â‰ƒ s)) := by
  rw [finite_iff_exists_equiv_fin] at inst
  obtain âŸ¨n, âŸ¨eâŸ©âŸ© := inst
  obtain âŸ¨s, hâŸ© := Infinite.exists_subset_card_eq ğ”¸ n
  exact âŸ¨âŸ¨s, e.trans (s.equivFinOfCardEq h).symmâŸ©âŸ©

noncomputable def Finset.chosenOfFinite (ğ”¸ : Type*) [Infinite ğ”¸] (Î² : Type*) [Finite Î²] :
    Finset ğ”¸ :=
  (exists_equiv_of_finite ğ”¸ Î²).some.fst

noncomputable def Finset.equivOfFinite {ğ”¸ : Type*} [Infinite ğ”¸] {Î² : Type*} [Finite Î²] :
    Î² â‰ƒ chosenOfFinite ğ”¸ Î² :=
  (exists_equiv_of_finite ğ”¸ Î²).some.snd

/--
This is not a colimiting cocone. Indeed, consider the other cocone `c` given by
```
âˆ…   â†’ {x}
â†“      â†“
{x} â†’ {x}
```
The map of apexes from this cocone to `c` is not injective, so this cocone is not colimiting.
-/
noncomputable def FinInj.pushoutCocone {ğ”¸ : Type*} [DecidableEq ğ”¸] [Infinite ğ”¸]
    (F : WalkingSpan â¥¤ FinInj ğ”¸) :
    Cocone F where
  pt := âŸ¨chosenOfFinite ğ”¸ (SpanCoconeApex F)âŸ©
  Î¹ := {
    app x := match x with
      | none => âŸ¨Î» x â†¦ equivOfFinite (.inl (F.map (.init .left) x)), by
          intro x y h
          simp only [const_obj_obj, EmbeddingLike.apply_eq_iff_eq, SpanCoconeApex.inl.injEq] at h
          exact (F.map (.init .left)).injective hâŸ©
      | some .left => âŸ¨Î» x â†¦ equivOfFinite (.inl x), by
          intro x y h
          simp only [const_obj_obj, EmbeddingLike.apply_eq_iff_eq, SpanCoconeApex.inl.injEq] at h
          exact hâŸ©
      | some .right => âŸ¨Î» x â†¦ equivOfFinite (.inr x), by
          intro x y h
          simp only [const_obj_obj, EmbeddingLike.apply_eq_iff_eq,
            SpanCoconeApex.inr_injective.eq_iff] at h
          exact hâŸ©
    naturality x y f := by
      apply DFunLike.coe_injective
      cases f
      case a.id =>
        simp only [const_obj_obj, WidePushoutShape.hom_id, CategoryTheory.Functor.map_id,
          Category.id_comp, const_obj_map, Category.comp_id]
      case a.init i =>
        cases i
        case left => rfl
        case right =>
          ext x
          simp only [const_obj_obj, CategoryStruct.comp, SpanCoconeApex.inl_eq_inr, const_obj_map,
            CategoryStruct.id]
          rfl
  }

/-!
## Permutation actions
-/

instance {ğ”¸ : Type*} [DecidableEq ğ”¸] : Nominal ğ”¸ (FinInj ğ”¸) where
  perm Ï€ s := âŸ¨Ï€ â¬ s.valâŸ©
  one_perm s := FinInj.ext (one_perm s.val)
  mul_perm Ï€â‚ Ï€â‚‚ s := FinInj.ext (mul_perm Ï€â‚ Ï€â‚‚ s.val)
  supported s := by
    refine âŸ¨s.val, ?_âŸ©
    intro Ï€ h
    ext a : 2
    simp only [mem_perm, Finperm.perm_name_eq]
    constructor
    Â· intro ha
      have := h _ ha
      rw [Finperm.apply_inv_self] at this
      rwa [â† this] at ha
    Â· intro ha
      rwa [â† h _ ha, Finperm.inv_apply_self]

@[simp]
theorem FinInj.perm_val {ğ”¸ : Type*} [DecidableEq ğ”¸] (Ï€ : Finperm ğ”¸) (s : FinInj ğ”¸) :
    (Ï€ â¬ s).val = Ï€ â¬ s.val :=
  rfl

def FinInj.isoOfEq {ğ”¸ : Type*} {s t : FinInj ğ”¸} (h : s = t) :
    s â‰… t where
  hom := âŸ¨Î» x â†¦ âŸ¨x, by rw [â† h]; exact x.propâŸ©, Î» x y h â†¦ by
      apply Subtype.coe_injective
      have := congr_arg Subtype.val h
      exact thisâŸ©
  inv := âŸ¨Î» x â†¦ âŸ¨x, by rw [h]; exact x.propâŸ©, Î» x y h â†¦ by
      apply Subtype.coe_injective
      have := congr_arg Subtype.val h
      exact thisâŸ©

def FinInj.permIso {ğ”¸ : Type*} [DecidableEq ğ”¸] (Ï€ : Finperm ğ”¸) (s : FinInj ğ”¸) :
    Ï€ â¬ s â‰… s where
  hom := âŸ¨Î» x â†¦ âŸ¨Ï€â»Â¹ x, by have := x.prop; simp only [perm_val, Finset.mem_perm] at this; exact thisâŸ©,
    Î» x y h â†¦ Subtype.coe_injective (EmbeddingLike.injective Ï€â»Â¹ (congr_arg (Î» x â†¦ x.val) h))âŸ©
  inv := âŸ¨Î» x â†¦ âŸ¨Ï€ x, by rw [â† Finperm.perm_name_eq, â† inv_inv Ï€, â† Finset.mem_perm,
      inv_inv, perm_val, inv_perm_perm]; exact x.propâŸ©,
    Î» x y h â†¦ Subtype.coe_injective (EmbeddingLike.injective Ï€ (congr_arg (Î» x â†¦ x.val) h))âŸ©
  hom_inv_id := by
    apply DFunLike.coe_injective
    ext x
    exact perm_inv_perm Ï€ _
  inv_hom_id := by
    apply DFunLike.coe_injective
    ext x
    exact inv_perm_perm Ï€ _

def finInjPermFunctor {ğ”¸ : Type*} [DecidableEq ğ”¸] (Ï€ : Finperm ğ”¸) :
    FinInj ğ”¸ â¥¤ FinInj ğ”¸ where
  obj s := Ï€ â¬ s
  map {s t} f := (permIso Ï€ s).hom â‰« f â‰« (permIso Ï€ t).inv

/-- The action of a finite permutation of atoms induces an equivalence of categories. -/
def finInjPerm {ğ”¸ : Type*} [DecidableEq ğ”¸] (Ï€ : Finperm ğ”¸) :
    FinInj ğ”¸ â‰Œ FinInj ğ”¸ where
  functor := finInjPermFunctor Ï€
  inverse := finInjPermFunctor Ï€â»Â¹
  unitIso := {
    hom := {
      app s := (isoOfEq (inv_perm_perm Ï€ s)).inv
      naturality {s t} f := by
        apply DFunLike.coe_injective
        ext x
        change (f _).val = Ï€â»Â¹ â¬ Ï€ â¬ f _
        rw [inv_perm_perm]
        congr
        simp only [perm_val, permIso, finInjPermFunctor, inv_inv, id_obj, comp_obj, isoOfEq,
          Embedding.coeFn_mk, Finperm.inv_apply_self, Subtype.coe_eta]
    }
    inv := {
      app s := (isoOfEq (inv_perm_perm Ï€ s)).hom
      naturality {s t} f := by
        apply DFunLike.coe_injective
        ext x
        change Ï€â»Â¹ â¬ Ï€ â¬ f _ = (f _).val
        rw [inv_perm_perm]
        congr 2
        simp only [perm_val, permIso, finInjPermFunctor, inv_inv, Embedding.coeFn_mk,
          Finperm.inv_apply_self, comp_obj, id_obj, isoOfEq]
    }
  }
  counitIso := {
    hom := {
      app s := (isoOfEq (perm_inv_perm Ï€ s)).hom
      naturality {s t} f := by
        apply DFunLike.coe_injective
        ext x
        change Ï€ â¬ Ï€â»Â¹ â¬ f _ = (f _).val
        rw [perm_inv_perm]
        congr 2
        simp only [perm_val, permIso, inv_inv, finInjPermFunctor, Embedding.coeFn_mk,
          Finperm.apply_inv_self, comp_obj, id_obj, isoOfEq]
    }
    inv := {
      app s := (isoOfEq (perm_inv_perm Ï€ s)).inv
      naturality {s t} f := by
        apply DFunLike.coe_injective
        ext x
        change (f _).val = Ï€ â¬ Ï€â»Â¹ â¬ f _
        rw [perm_inv_perm]
        congr
        simp only [perm_val, permIso, inv_inv, finInjPermFunctor, id_obj, comp_obj, isoOfEq,
          Embedding.coeFn_mk, Finperm.apply_inv_self, Subtype.coe_eta]
    }
  }
  functor_unitIso_comp s := by
    apply DFunLike.coe_injective
    ext x : 2
    simp only [finInjPermFunctor, permIso, perm_val, id_obj, comp_obj, isoOfEq, Category.assoc]
    exact perm_inv_perm Ï€ _
