import Mathlib.CategoryTheory.Generator.Basic
import Nominal.Category.Coreflection
import Nominal.Category.Forget

/-!
# Separators and coseparators

In this file, we exhibit separating and coseparating sets for the category of nominal sets.

Note that Nom has no single separating object. Suppse that `X` were such a separator.
Let `n` be the size of the smallest support of an object in `X`; note that `X` must be nonempty
otherwise it obviously fails to be a separator. Then let `Y` be the nominal set of finite sets
of atoms of size greater than `n`. Now, `Nom(X, Y)` is empty, because if `f : X â†’ Y` were
equivariant, it would map `x` with support of size `n` to `f x` with support of size at most `n`,
so `|f x| <= n`, a contradiction. So `X` cannot separate maps with domain `Y`.

However, Nom does have a single coseparator, namely, `FS ð”¸ (Power ð”¸ (ULift Prop))`.
This follows from the general fact that given an adjunction `F : C â¥¤ D âŠ£ G : D â¥¤ C`
where the left adjoint is faithful, any coseparating set in `D` can be converted into a
coseparating set in `C`.
-/

open CategoryTheory Functor

universe u

variable {ð”¸ : Type u} [DecidableEq ð”¸]

/-- Given an adjunction `F : C â¥¤ D âŠ£ G : D â¥¤ C` where the left adjoint is faithful,
any coseparating set in `D` can be converted into a coseparating set in `C`. -/
theorem CategoryTheory.Adjunction.isCoseparating_of_faithful
    {C D : Type*} [Category C] [Category D] {F : C â¥¤ D} {G : D â¥¤ C}
    (adj : F âŠ£ G) (hf : F.Faithful) {s : Set D} (hs : IsCoseparating s) :
    IsCoseparating (G.obj '' s) := by
  intro X Y f g h
  apply hf.map_injective
  apply hs
  intro Z hZ p
  have := h _ âŸ¨Z, hZ, rflâŸ© (adj.homEquiv Y Z p)
  rw [â† adj.homEquiv_naturality_left, â† adj.homEquiv_naturality_left,
    EquivLike.apply_eq_iff_eq] at this
  exact this

/-- If `F âŠ£ G` where the left adjoint is faithful, then any coseparator in `D`
can be converted to a coseparator in `C`. -/
theorem CategoryTheory.Adjunction.isCoseparator_of_faithful
    {C D : Type*} [Category C] [Category D] {F : C â¥¤ D} {G : D â¥¤ C}
    (adj : F âŠ£ G) (hf : F.Faithful) {X : D} (hs : IsCoseparator X) :
    IsCoseparator (G.obj X) := by
  have := adj.isCoseparating_of_faithful hf hs
  rwa [Set.image_singleton] at this

theorem CategoryTheory.Types.prop_isCoseparator.{v} :
    IsCoseparator (ULift.{v} Prop) := by
  intro Î± Î² f g h
  ext x
  have := congr_arg (Â· x) (h _ rfl (Î» y â†¦ .up (f x = y)))
  simp only [types_comp_apply, ULift.up.injEq, eq_iff_iff, true_iff] at this
  exact this

theorem Nominal.prop_isCoseparator.{v} :
    IsCoseparator ((MulPerm.power ð”¸ â‹™ nominalCoreflection ð”¸).obj (ULift.{max u v} Prop)) :=
  (forgetAdj ð”¸).isCoseparator_of_faithful (HasForget.forget_faithful) (Types.prop_isCoseparator)

instance Nominal.hasCoseparator.{v} : HasCoseparator (Bundled.{max u v} (Nominal ð”¸)) :=
  âŸ¨_, Nominal.prop_isCoseparatorâŸ©
