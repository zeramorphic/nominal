import Mathlib.CategoryTheory.ConcreteCategory.Basic
import Mathlib.CategoryTheory.Limits.Shapes.Pullback.PullbackCone
import Nominal.Category.FinInj

/-!
# Colimits of shape `FinInj`

In this file, we construct the colimit in `Type` for a diagram of shape `FinInj`.
Our main objective is to construct the functor
`finInjToNominal : (FinInj ğ”¸ â¥¤ Type) â¥¤ Bundled (Nominal ğ”¸)`.
-/

open CategoryTheory Functor Limits

def finInjRel {ğ”¸ : Type*} (F : FinInj ğ”¸ â¥¤ Type*) (x y : (s : FinInj ğ”¸) Ã— F.obj s) : Prop :=
  âˆƒ (t : FinInj ğ”¸) (f : x.1 âŸ¶ t) (g : y.1 âŸ¶ t), F.map f x.2 = F.map g y.2

instance finInjRel_refl {ğ”¸ : Type*} (F : FinInj ğ”¸ â¥¤ Type*) :
    IsRefl _ (finInjRel F) := by
  constructor
  intro x
  exact âŸ¨x.1, ğŸ™ x.1, ğŸ™ x.1, rflâŸ©

instance finInjRel_symm {ğ”¸ : Type*} (F : FinInj ğ”¸ â¥¤ Type*) :
    IsSymm _ (finInjRel F) := by
  constructor
  rintro x y âŸ¨t, f, g, hâŸ©
  exact âŸ¨t, g, f, h.symmâŸ©

instance : Fintype WalkingPair where
  elems := {.left, .right}
  complete x := by cases x <;> decide

instance finInjRel_trans {ğ”¸ : Type*} [Infinite ğ”¸] (F : FinInj ğ”¸ â¥¤ Type*) :
    IsTrans _ (finInjRel F) := by
  have : DecidableEq ğ”¸ := Classical.typeDecidableEq ğ”¸
  constructor
  intro x y z âŸ¨tâ‚, fâ‚, gâ‚, hâ‚âŸ© âŸ¨tâ‚‚, fâ‚‚, gâ‚‚, hâ‚‚âŸ©
  have := FinInj.pushoutCocone (span gâ‚ fâ‚‚)
  refine âŸ¨(FinInj.pushoutCocone (span gâ‚ fâ‚‚)).pt,
    fâ‚ â‰« PushoutCocone.inl (FinInj.pushoutCocone (span gâ‚ fâ‚‚)),
    gâ‚‚ â‰« PushoutCocone.inr (FinInj.pushoutCocone (span gâ‚ fâ‚‚)), ?_âŸ©
  rw [FunctorToTypes.map_comp_apply, FunctorToTypes.map_comp_apply, hâ‚, â† hâ‚‚,
    â† FunctorToTypes.map_comp_apply, â† FunctorToTypes.map_comp_apply,
    PushoutCocone.condition]

theorem finInjRel_equivalence {ğ”¸ : Type*} [Infinite ğ”¸] (F : FinInj ğ”¸ â¥¤ Type*) :
    Equivalence (finInjRel F) :=
  âŸ¨(finInjRel_refl F).refl, (finInjRel_symm F).symm _ _, (finInjRel_trans F).trans _ _ _âŸ©

def finInjSetoid {ğ”¸ : Type*} [Infinite ğ”¸] (F : FinInj ğ”¸ â¥¤ Type*) :
    Setoid ((s : FinInj ğ”¸) Ã— F.obj s) where
  r := finInjRel F
  iseqv := finInjRel_equivalence F

def finInjColimitApex {ğ”¸ : Type*} [Infinite ğ”¸] (F : FinInj ğ”¸ â¥¤ Type*) :=
  Quotient (finInjSetoid F)

def finInjCocone.{u, v} {ğ”¸ : Type u} [Infinite ğ”¸] (F : FinInj ğ”¸ â¥¤ Type (max u v)) :
    Cocone F where
  pt := finInjColimitApex F
  Î¹ := {
    app s x := Quotient.mk (finInjSetoid F) âŸ¨s, xâŸ©
    naturality s t f := by
      ext x
      simp only [const_obj_obj, types_comp_apply, const_obj_map, types_id_apply]
      apply Quotient.sound
      refine âŸ¨t, ğŸ™ t, f, ?_âŸ©
      simp only [FunctorToTypes.map_id_apply]
  }

def finInjCocone_isColimit.{u, v} {ğ”¸ : Type u} [Infinite ğ”¸] (F : FinInj ğ”¸ â¥¤ Type (max u v)) :
    IsColimit (finInjCocone F) where
  desc c := Quotient.lift (Î» x â†¦ c.Î¹.app x.1 x.2) <| by
    rintro âŸ¨s, xâŸ© âŸ¨t, yâŸ© âŸ¨u, f, g, hâŸ©
    dsimp only
    rw [â† c.w f, â† c.w g, types_comp_apply, h]
    rfl
  fac c s := by
    ext x
    simp only [finInjCocone, const_obj_obj, types_comp_apply, Quotient.lift_mk]
  uniq c m h := by
    ext x
    induction x using Quotient.inductionOn
    case h x =>
    simp only [Quotient.lift_mk]
    rw [â† h x.1]
    rfl

def finInjColimit.{u, v} (ğ”¸ : Type u) [Infinite ğ”¸] :
    (FinInj ğ”¸ â¥¤ Type (max u v)) â¥¤ Type (max u v) where
  obj := finInjColimitApex
  map {F G} f := (finInjCocone_isColimit F).desc ((Cocones.precompose f).obj (finInjCocone G))
  map_id F := by
    dsimp only
    rw [â† (finInjCocone_isColimit F).uniq]
    intro
    rfl
  map_comp {F G H} f g := by
    dsimp only
    rw [â† (finInjCocone_isColimit F).uniq]
    intro
    rfl

theorem finInjColimit_map.{u, v} (ğ”¸ : Type u) [Infinite ğ”¸] {F G : FinInj ğ”¸ â¥¤ Type (max u v)}
    (f : F âŸ¶ G) :
    (finInjColimit ğ”¸).map f =
    (finInjCocone_isColimit F).desc ((Cocones.precompose f).obj (finInjCocone G)) :=
  rfl

def finInjLeg.{u, v} {ğ”¸ : Type u} [Infinite ğ”¸] {F : FinInj ğ”¸ â¥¤ Type (max u v)}
    (s : FinInj ğ”¸) (x : F.obj s) :
    (finInjColimit ğ”¸).obj F :=
  (finInjCocone F).Î¹.app s x

@[simp]
theorem finInjLeg_w.{u, v} {ğ”¸ : Type u} [Infinite ğ”¸] (F : FinInj ğ”¸ â¥¤ Type (max u v))
    {s t : FinInj ğ”¸} (f : s âŸ¶ t) :
    F.map f â‰« finInjLeg t = finInjLeg s :=
  (finInjCocone F).w f

@[elab_as_elim]
theorem finInjLeg_inductionOn.{u, v} {ğ”¸ : Type u} [Infinite ğ”¸] {F : FinInj ğ”¸ â¥¤ Type (max u v)}
    {motive : (finInjColimit ğ”¸).obj F â†’ Prop}
    (q : (finInjColimit ğ”¸).obj F)
    (h : (s : FinInj ğ”¸) â†’ (x : F.obj s) â†’ motive (finInjLeg s x)) : motive q :=
  Quot.inductionOn q (Î» x â†¦ h x.1 x.2)

def finInjPermCocone {ğ”¸ : Type*} [DecidableEq ğ”¸] [Infinite ğ”¸] (F : FinInj ğ”¸ â¥¤ Type _)
    (Ï€ : Finperm ğ”¸) :
    Cocone F where
  pt := (finInjColimit ğ”¸).obj F
  Î¹ := {
    app s := F.map ((finInjPermIso Ï€).inv.app s) â‰« (finInjCocone F).Î¹.app (Ï€ â¬ s)
    naturality {s t} f := by
      have := (isoWhiskerRight (finInjPermIso Ï€) F).inv.naturality f
      simp only [comp_obj, id_obj, Functor.comp_map, Functor.id_map, isoWhiskerRight_inv,
        whiskerRight_app] at this
      simp only [const_obj_obj, const_obj_map, Category.comp_id]
      rw [â† Category.assoc, this, Category.assoc]
      erw [(finInjCocone F).w ((finInjPermFunctor Ï€).map f)]
      rfl
  }

theorem finInjPermCocone_Î¹_app_one {ğ”¸ : Type*} [DecidableEq ğ”¸] [Infinite ğ”¸] (F : FinInj ğ”¸ â¥¤ Type _)
    (s : FinInj ğ”¸) :
    (finInjPermCocone F 1).Î¹.app s = (finInjCocone F).Î¹.app s :=
  (finInjCocone F).w ((finInjPermIso 1).inv.app s)

theorem finInjPermCocone_Î¹_app_mul {ğ”¸ : Type*} [DecidableEq ğ”¸] [Infinite ğ”¸] (F : FinInj ğ”¸ â¥¤ Type _)
    (Ï€â‚ Ï€â‚‚ : Finperm ğ”¸) (s : FinInj ğ”¸) :
    (finInjPermCocone F (Ï€â‚ * Ï€â‚‚)).Î¹.app s =
      (finInjPermCocone F Ï€â‚‚).Î¹.app s â‰«
      (finInjCocone_isColimit F).desc (finInjPermCocone F Ï€â‚) := by
  simp only [finInjPermCocone, const_obj_obj, Category.assoc, IsColimit.fac]
  erw [(finInjCocone F).w ((finInjPermIso Ï€â‚).inv.app (Ï€â‚‚ â¬ s)),
    (finInjCocone F).w ((finInjPermIso (Ï€â‚ * Ï€â‚‚)).inv.app s),
    (finInjCocone F).w ((finInjPermIso Ï€â‚‚).inv.app s)]

/-- We define the permutation action of `Finperm ğ”¸` on the colimit of a functor `FinInj ğ”¸ â¥¤ Type _`
by the universal property of the colimit. This avoids dealing with any heterogeneous equalities
or transports. -/
instance {ğ”¸ : Type*} [DecidableEq ğ”¸] [Infinite ğ”¸] (F : FinInj ğ”¸ â¥¤ Type _) :
    HasPerm ğ”¸ ((finInjColimit ğ”¸).obj F) where
  perm Ï€ := (finInjCocone_isColimit F).desc (finInjPermCocone F Ï€)

theorem finInjColimit_perm_def {ğ”¸ : Type*} [DecidableEq ğ”¸] [Infinite ğ”¸]
    (F : FinInj ğ”¸ â¥¤ Type _) (x : (finInjColimit ğ”¸).obj F) (Ï€ : Finperm ğ”¸) :
    Ï€ â¬ x = (finInjCocone_isColimit F).desc (finInjPermCocone F Ï€) x :=
  rfl

theorem finInjColimit_mk_perm {ğ”¸ : Type*} [DecidableEq ğ”¸] [Infinite ğ”¸]
    (F : FinInj ğ”¸ â¥¤ Type _) (s : FinInj ğ”¸) (x : F.obj s) (Ï€ : Finperm ğ”¸) :
    Ï€ â¬ finInjLeg s x = finInjLeg (Ï€ â¬ s) (F.map ((finInjPermIso Ï€).inv.app s) x) :=
  rfl

instance {ğ”¸ : Type*} [DecidableEq ğ”¸] [Infinite ğ”¸] (F : FinInj ğ”¸ â¥¤ Type _) :
    MulPerm ğ”¸ ((finInjColimit ğ”¸).obj F) where
  one_perm x := by
    rw [finInjColimit_perm_def, â† (finInjCocone_isColimit F).uniq]
    intro s
    change (finInjCocone F).Î¹.app s = _
    rw [finInjPermCocone_Î¹_app_one]
  mul_perm Ï€â‚ Ï€â‚‚ x := by
    simp only [finInjColimit_perm_def]
    rw [â† (finInjCocone_isColimit F).uniq]
    intro s
    rw [finInjPermCocone_Î¹_app_mul]
    rfl

theorem supports_finInjLeg {ğ”¸ : Type*} [DecidableEq ğ”¸] [Infinite ğ”¸] {F : FinInj ğ”¸ â¥¤ Type _}
    (s : FinInj ğ”¸) (x : F.obj s) :
    Supports s.val (finInjLeg s x) := by
  intro Ï€ hÏ€
  rw [finInjColimit_perm_def, finInjLeg,
    â† (finInjCocone_isColimit F).uniq (finInjPermCocone F Ï€) (ğŸ™ ((finInjCocone F).pt)),
    types_id_apply]
  intro j
  exact ((finInjCocone F).w ((finInjPermIso Ï€).inv.app j)).symm

instance {ğ”¸ : Type*} [DecidableEq ğ”¸] [Infinite ğ”¸] (F : FinInj ğ”¸ â¥¤ Type _) :
    Nominal ğ”¸ ((finInjColimit ğ”¸).obj F) where
  supported x := by
    induction x using finInjLeg_inductionOn
    case h s x =>
    exact âŸ¨_, supports_finInjLeg s xâŸ©

theorem finInjColimit_map_equivariant' {ğ”¸ : Type*} [DecidableEq ğ”¸] [Infinite ğ”¸]
    {F G : FinInj ğ”¸ â¥¤ Type _} (f : F âŸ¶ G) (Ï€ : Finperm ğ”¸) :
    (finInjCocone_isColimit F).desc (finInjPermCocone F Ï€) â‰«
      (finInjCocone_isColimit F).desc ((Cocones.precompose f).obj (finInjCocone G)) =
    (finInjCocone_isColimit F).desc ((Cocones.precompose f).obj (finInjCocone G)) â‰«
        (finInjCocone_isColimit G).desc (finInjPermCocone G Ï€) := by
  trans (finInjCocone_isColimit F).desc ((Cocones.precompose f).obj (finInjPermCocone G Ï€))
  Â· apply (finInjCocone_isColimit F).uniq
      ((Cocones.precompose f).obj (finInjPermCocone G Ï€))
    intro s
    have := f.naturality ((finInjPermIso Ï€).inv.app s)
    simp only [finInjPermCocone, const_obj_obj, Cocones.precompose_obj_pt, IsColimit.fac_assoc,
      Category.assoc, IsColimit.fac, Cocones.precompose_obj_Î¹, NatTrans.comp_app]
    simp only [id_obj, finInjPermFunctor] at this
    rw [â† Category.assoc, this]
    rfl
  Â· symm
    apply (finInjCocone_isColimit F).uniq
      ((Cocones.precompose f).obj (finInjPermCocone G Ï€))
    intro s
    simp only [Cocones.precompose_obj_pt, const_obj_obj, IsColimit.fac_assoc,
      Cocones.precompose_obj_Î¹, NatTrans.comp_app, Category.assoc, IsColimit.fac]

theorem finInjColimit_map_equivariant {ğ”¸ : Type*} [DecidableEq ğ”¸] [Infinite ğ”¸]
    {F G : FinInj ğ”¸ â¥¤ Type _} (f : F âŸ¶ G) :
    Equivariant ğ”¸ (Î± := (finInjColimit ğ”¸).obj F â†’ (finInjColimit ğ”¸).obj G)
      ((finInjColimit ğ”¸).map f) := by
  intro Ï€
  ext x
  rw [Function.perm_def, perm_eq_iff_eq_inv_perm, finInjColimit_map, finInjColimit_perm_def,
    finInjColimit_perm_def]
  rw [â† types_comp_apply _ ((finInjCocone_isColimit F).desc _),
    â† types_comp_apply _ ((finInjCocone_isColimit G).desc _),
    finInjColimit_map_equivariant']

def finInjToNominal.{u, v} (ğ”¸ : Type u) [DecidableEq ğ”¸] [Infinite ğ”¸] :
    (FinInj ğ”¸ â¥¤ Type (max u v)) â¥¤ Bundled.{max u v} (Nominal ğ”¸) where
  obj F := âŸ¨(finInjColimit ğ”¸).obj F, inferInstanceâŸ©
  map {F G} f := âŸ¨(finInjColimit ğ”¸).map f, finInjColimit_map_equivariant fâŸ©
  map_id F := by
    apply EQ.ext
    exact (finInjColimit ğ”¸).map_id F
  map_comp f g := by
    apply EQ.ext
    exact (finInjColimit ğ”¸).map_comp f g
