import Nominal.Instances
import Nominal.Quantifier

open Finperm

variable {ğ”¸ Î± Î² Î³ : Type*} [DecidableEq ğ”¸] [Infinite ğ”¸]

structure Abstract' (ğ”¸ Î± Î² : Type*) where
  param : Î±
  val : Î²

namespace Abstract'

variable [MulPerm ğ”¸ Î±] [MulPerm ğ”¸ Î²]

instance : HasPerm ğ”¸ (Abstract' ğ”¸ Î± Î²) where
  perm Ï€ x := âŸ¨Ï€ â¬ x.param, Ï€ â¬ x.valâŸ©

omit [Infinite ğ”¸] in
theorem perm_def (Ï€ : Finperm ğ”¸) (x : Abstract' ğ”¸ Î± Î²) :
    Ï€ â¬ x = âŸ¨Ï€ â¬ x.param, Ï€ â¬ x.valâŸ© :=
  rfl

instance : MulPerm ğ”¸ (Abstract' ğ”¸ Î± Î²) where
  one_perm := by
    rintro âŸ¨p, vâŸ©
    rw [perm_def, one_perm, one_perm]
  mul_perm := by
    rintro Ï€â‚ Ï€â‚‚ âŸ¨p, vâŸ©
    rw [perm_def, mul_perm, mul_perm]
    rfl

def Rel (x y : Abstract' ğ”¸ Î± Î²) : Prop :=
  âˆƒ Ï€ : Finperm ğ”¸, Ï€ â¬ x = y âˆ§ Ï€ #[ğ”¸] supp ğ”¸ x.val \ supp ğ”¸ x.param

omit [Infinite ğ”¸] in
theorem perm_supp_diff_eq (Ï€ : Finperm ğ”¸) (x : Abstract' ğ”¸ Î± Î²) :
    Ï€ â¬ (supp ğ”¸ x.val \ supp ğ”¸ x.param) = supp ğ”¸ (Ï€ â¬ x).val \ supp ğ”¸ (Ï€ â¬ x).param := by
  ext a
  rw [Finset.perm_def]
  simp only [perm_def, supp_perm_eq, Finset.mem_sdiff, Finset.mem_perm, perm_name_eq,
    Finset.mem_map, Function.Embedding.coeFn_mk]
  aesop

theorem perm_supp_diff_eq' {Ï€ : Finperm ğ”¸} {x : Abstract' ğ”¸ Î± Î²}
    (h : Ï€ #[ğ”¸] (supp ğ”¸ x.val \ supp ğ”¸ x.param)) :
    supp ğ”¸ (Ï€ â¬ x).val \ supp ğ”¸ (Ï€ â¬ x).param = supp ğ”¸ x.val \ supp ğ”¸ x.param := by
  rw [â† perm_supp_diff_eq, perm_eq_of_fresh h]

theorem rel_refl (x : Abstract' ğ”¸ Î± Î²) :
    Rel x x := by
  use 1
  simp only [one_perm, fresh_iff, coe_one, id_eq, implies_true, and_self]

theorem rel_symm {x y : Abstract' ğ”¸ Î± Î²} (h : Rel x y) :
    Rel y x := by
  obtain âŸ¨Ï€, rfl, hÏ€âŸ© := h
  refine âŸ¨Ï€â»Â¹, inv_perm_perm Ï€ x, ?_âŸ©
  rwa [perm_supp_diff_eq' hÏ€, fresh_def, Finperm.supp_eq, inv_support,
    â† Finperm.supp_eq, â† fresh_def]

theorem rel_trans {x y z : Abstract' ğ”¸ Î± Î²} (hâ‚ : Rel x y) (hâ‚‚ : Rel y z) :
    Rel x z := by
  obtain âŸ¨Ï€â‚, rfl, hÏ€â‚âŸ© := hâ‚
  obtain âŸ¨Ï€â‚‚, rfl, hÏ€â‚‚âŸ© := hâ‚‚
  refine âŸ¨Ï€â‚‚ * Ï€â‚, mul_perm Ï€â‚‚ Ï€â‚ x, ?_âŸ©
  rw [perm_supp_diff_eq' hÏ€â‚] at hÏ€â‚‚
  rw [fresh_def, Finperm.supp_eq, Finset.disjoint_iff_ne] at hÏ€â‚ hÏ€â‚‚ âŠ¢
  intro a ha
  have := mul_support_subset Ï€â‚‚ Ï€â‚ ha
  rw [Finset.mem_union] at this
  obtain (h | h) := this
  Â· exact hÏ€â‚‚ a h
  Â· exact hÏ€â‚ a h

theorem rel_equivalence :
    Equivalence (Rel : Abstract' ğ”¸ Î± Î² â†’ Abstract' ğ”¸ Î± Î² â†’ Prop) :=
  âŸ¨rel_refl, rel_symm, rel_transâŸ©

theorem rel_perm {x y : Abstract' ğ”¸ Î± Î²} (h : Rel x y) (Ï€ : Finperm ğ”¸) :
    Rel (Ï€ â¬ x) (Ï€ â¬ y) := by
  obtain âŸ¨Ï€', rfl, hÏ€'âŸ© := h
  refine âŸ¨Ï€ â¬ Ï€', ?_, ?_âŸ©
  Â· simp only [Finperm.perm_def, mul_perm, inv_perm_perm]
  rw [â† perm_supp_diff_eq]
  exact hÏ€'.perm Ï€

instance setoid : Setoid (Abstract' ğ”¸ Î± Î²) where
  r := Rel (ğ”¸ := ğ”¸)
  iseqv := rel_equivalence

end Abstract'

theorem Abstract'.rel_iff_aux [Nominal ğ”¸ Î±] [Nominal ğ”¸ Î²]
    {x y : Abstract' ğ”¸ Î± Î²} (Ï€ : Finperm ğ”¸)
    (hÏ€â‚ : Ï€ â¬ x = y) (hÏ€â‚‚ : Ï€ #[ğ”¸] supp ğ”¸ x.val \ supp ğ”¸ x.param) :
    âˆƒ Ï€ : Finperm ğ”¸, Ï€ â¬ x = y âˆ§
      Ï€ #[ğ”¸] supp ğ”¸ x.val \ supp ğ”¸ x.param âˆ§
      Ï€.support âŠ† supp ğ”¸ x.param âˆª supp ğ”¸ y.param := by
  cases hÏ€â‚
  have hÏ€ : (supp ğ”¸ x.param).image Ï€ = supp ğ”¸ (Ï€ â¬ x).param := by
    ext a
    simp only [Finset.mem_image, perm_def, supp_perm_eq, Finset.mem_perm, perm_name_eq]
    aesop
  obtain âŸ¨Ï€', hÏ€'â‚, hÏ€'â‚‚âŸ© := exists_extension
    (Ï€.toPerm (supp ğ”¸ x.param) (supp ğ”¸ (Ï€ â¬ x).param) hÏ€)
  refine âŸ¨Ï€', ?_, ?_, hÏ€'â‚‚âŸ©
  Â· have := Nominal.supp_supports ğ”¸ (x.param, x.val) (Ï€'â»Â¹ * Ï€) ?_
    Â· simp only [mul_perm, Prod.perm_mk, Prod.mk.injEq, inv_perm_eq_iff] at this
      cases x
      rw [perm_def, perm_def, mk.injEq, this.1, this.2]
      exact âŸ¨rfl, rflâŸ©
    intro a ha
    simp only [coe_mul, Function.comp_apply, inv_apply_eq_iff_eq]
    simp only [Prod.supp_mk, Finset.mem_union] at ha
    by_cases ha' : a âˆˆ supp ğ”¸ x.param
    Â· have := hÏ€'â‚ a ha'
      simp only [toPerm_apply] at this
      rw [this]
    Â· simp only [ha', false_or] at ha
      simp only [fresh_iff, Finset.names_supp_eq, Finset.mem_sdiff, and_imp] at hÏ€â‚‚
      rw [hÏ€â‚‚ a ha ha', eq_comm]
      by_contra ha''
      rw [â† ne_eq, â† Finperm.mem_support_iff] at ha''
      have := hÏ€'â‚‚ ha''
      simp only [perm_def, supp_perm_eq, Finset.mem_union, ha', Finset.mem_perm, perm_name_eq,
        false_or] at this
      rw [â† hÏ€â‚‚ a ha ha', inv_apply_self] at this
      contradiction
  Â· simp only [fresh_iff, Finset.names_supp_eq, Finset.mem_sdiff, and_imp]
    intro a haâ‚ haâ‚‚
    by_contra haâ‚ƒ
    rw [â† ne_eq, â† Finperm.mem_support_iff] at haâ‚ƒ
    have := hÏ€'â‚‚ haâ‚ƒ
    simp only [Finset.mem_union, haâ‚‚, false_or] at this
    have h := hÏ€'â‚ (Ï€â»Â¹ a) ?_
    Â· simp only [toPerm_apply, apply_inv_self] at h
      simp only [fresh_iff, Finset.names_supp_eq, Finset.mem_sdiff, and_imp] at hÏ€â‚‚
      have := hÏ€â‚‚ a
      simp only [haâ‚, haâ‚‚, not_false_eq_true, forall_const] at this
      rw [eq_comm, â† inv_apply_eq_iff_eq] at this
      rw [this] at h
      rw [mem_support_iff] at haâ‚ƒ
      contradiction
    Â· simp only [perm_def, supp_perm_eq, Finset.mem_perm, perm_name_eq] at this
      exact this

/-- A more powerful, but equivalent, definition of the relation. -/
theorem Abstract'.rel_iff [Nominal ğ”¸ Î±] [Nominal ğ”¸ Î²] (x y : Abstract' ğ”¸ Î± Î²) :
    Rel x y â†” âˆƒ Ï€ : Finperm ğ”¸, Ï€ â¬ x = y âˆ§
      Ï€ #[ğ”¸] supp ğ”¸ x.val \ supp ğ”¸ x.param âˆ§
      Ï€.support âŠ† supp ğ”¸ x.param âˆª supp ğ”¸ y.param := by
  constructor
  Â· rintro âŸ¨Ï€, hÏ€â‚, hÏ€â‚‚âŸ©
    exact rel_iff_aux Ï€ hÏ€â‚ hÏ€â‚‚
  Â· rintro âŸ¨Ï€, rfl, hÏ€âŸ©
    exact âŸ¨Ï€, rfl, hÏ€.1âŸ©

def Abstract (ğ”¸ Î± Î² : Type*) [DecidableEq ğ”¸] [Infinite ğ”¸] [MulPerm ğ”¸ Î±] [MulPerm ğ”¸ Î²] :=
  Quotient (Abstract'.setoid (ğ”¸ := ğ”¸) (Î± := Î±) (Î² := Î²))

namespace Abstract
open Abstract'

section Basic

variable [MulPerm ğ”¸ Î±] [MulPerm ğ”¸ Î²]

notation:max "["Î±"|"ğ”¸"]" Î²:max => Abstract ğ”¸ Î± Î²

def mk (param : Î±) (val : Î²) : [Î±|ğ”¸]Î² :=
  Quotient.mk Abstract'.setoid âŸ¨param, valâŸ©

notation "âŸ¨"param"âŸ©" val:max => Abstract.mk param val

theorem sound {xâ‚ xâ‚‚ : Î±} {yâ‚ yâ‚‚ : Î²} (h : Rel (ğ”¸ := ğ”¸) âŸ¨xâ‚, yâ‚âŸ© âŸ¨xâ‚‚, yâ‚‚âŸ©) :
    (âŸ¨xâ‚âŸ©yâ‚ : [Î±|ğ”¸]Î²) = âŸ¨xâ‚‚âŸ©yâ‚‚ :=
  Quotient.sound h

def lift {Î³ : Sort*} (f : Î± â†’ Î² â†’ Î³)
    (hf : âˆ€ (xâ‚ xâ‚‚ : Î±) (yâ‚ yâ‚‚ : Î²), Rel (ğ”¸ := ğ”¸) âŸ¨xâ‚, yâ‚âŸ© âŸ¨xâ‚‚, yâ‚‚âŸ© â†’ f xâ‚ yâ‚ = f xâ‚‚ yâ‚‚) :
    [Î±|ğ”¸]Î² â†’ Î³ :=
  Quotient.lift (Î» x â†¦ f x.param x.val) (Î» x y â†¦ hf x.param y.param x.val y.val)

theorem ind {motive : [Î±|ğ”¸]Î² â†’ Prop} (mk : âˆ€ x y, motive (âŸ¨xâŸ©y)) :
    âˆ€ x, motive x :=
  Quotient.ind (Î» x â†¦ mk x.param x.val)

theorem exact {xâ‚ xâ‚‚ : Î±} {yâ‚ yâ‚‚ : Î²} (h : (âŸ¨xâ‚âŸ©yâ‚ : [Î±|ğ”¸]Î²) = âŸ¨xâ‚‚âŸ©yâ‚‚) :
    Rel (ğ”¸ := ğ”¸) âŸ¨xâ‚, yâ‚âŸ© âŸ¨xâ‚‚, yâ‚‚âŸ© :=
  Quotient.exact h

theorem mk_eq_iff {xâ‚ xâ‚‚ : Î±} {yâ‚ yâ‚‚ : Î²} :
    (âŸ¨xâ‚âŸ©yâ‚ : [Î±|ğ”¸]Î²) = âŸ¨xâ‚‚âŸ©yâ‚‚ â†” Rel (ğ”¸ := ğ”¸) âŸ¨xâ‚, yâ‚âŸ© âŸ¨xâ‚‚, yâ‚‚âŸ© :=
  âŸ¨exact, soundâŸ©

/-!
# Nominal structure
-/

theorem perm_aux (Ï€ : Finperm ğ”¸) (xâ‚ xâ‚‚ : Î±) (yâ‚ yâ‚‚ : Î²) (h : Rel (ğ”¸ := ğ”¸) âŸ¨xâ‚, yâ‚âŸ© âŸ¨xâ‚‚, yâ‚‚âŸ©) :
    (âŸ¨Ï€ â¬ xâ‚âŸ©(Ï€ â¬ yâ‚) : [Î±|ğ”¸]Î²) = âŸ¨Ï€ â¬ xâ‚‚âŸ©(Ï€ â¬ yâ‚‚) := by
  rw [mk_eq_iff]
  exact rel_perm h Ï€

instance : HasPerm ğ”¸ [Î±|ğ”¸]Î² where
  perm Ï€ := lift (Î» x y â†¦ âŸ¨Ï€ â¬ xâŸ©(Ï€ â¬ y)) (perm_aux Ï€)

@[simp]
theorem perm_mk (Ï€ : Finperm ğ”¸) (x : Î±) (y : Î²) :
    Ï€ â¬ (âŸ¨xâŸ©y : [Î±|ğ”¸]Î²) = âŸ¨Ï€ â¬ xâŸ©(Ï€ â¬ y) :=
  rfl

instance : MulPerm ğ”¸ [Î±|ğ”¸]Î² where
  one_perm x := by
    induction x using ind
    case mk x y => simp only [perm_mk, one_perm]
  mul_perm Ï€â‚ Ï€â‚‚ x := by
    induction x using ind
    case mk x y => simp only [perm_mk, mul_perm]

end Basic

theorem supports_mk [Nominal ğ”¸ Î±] [Nominal ğ”¸ Î²] (x : Î±) (y : Î²) :
    Supports (supp ğ”¸ y \ supp ğ”¸ x) (âŸ¨xâŸ©y : [Î±|ğ”¸]Î²) := by
  intro Ï€ hÏ€
  rw [perm_mk, mk_eq_iff]
  rw [â† Abstract'.perm_def Ï€ âŸ¨x, yâŸ©]
  refine âŸ¨Ï€â»Â¹, ?_, ?_âŸ©
  Â· simp only [inv_perm_perm]
  Â· rw [â† perm_supp_diff_eq, fresh_perm_iff_inv_perm_fresh, Finperm.fresh_iff]
    intro b hb
    rw [Finset.names_supp_eq] at hb
    rw [Finperm.perm_def, inv_inv, inv_mul_cancel_right, inv_apply_eq_iff_eq, hÏ€ b hb]

theorem subset_of_supports [Nominal ğ”¸ Î±] [Nominal ğ”¸ Î²] {x : Î±} {y : Î²} {s : Finset ğ”¸}
    (hs : Supports s (âŸ¨xâŸ©y : [Î±|ğ”¸]Î²)) :
    supp ğ”¸ y \ supp ğ”¸ x âŠ† s := by
  intro a ha
  rw [Finset.mem_sdiff] at ha
  by_contra ha'
  obtain âŸ¨b, hbâ‚, hbâ‚‚, hbâ‚ƒ,hbâ‚„âŸ© := ((newNames_not_mem (supp ğ”¸ y)).and
    ((newNames_not_mem (supp ğ”¸ x)).and ((newNames_not_mem s).and (newNames_not_mem {a})))).exists
  rw [Finset.mem_singleton] at hbâ‚„
  have := hs (swap a b) ?_
  Â· rw [perm_mk, mk_eq_iff] at this
    obtain âŸ¨Ï€, hÏ€â‚, hÏ€â‚‚âŸ© := this
    simp only [Abstract'.perm_def, Abstract'.mk.injEq] at hÏ€â‚
    simp only [supp_perm_eq, fresh_iff, Finset.names_supp_eq, Finset.mem_sdiff, and_imp] at hÏ€â‚‚
    have := congr_arg (a âˆˆ supp ğ”¸ Â·) hÏ€â‚.2
    simp only [supp_perm_eq, Finset.mem_perm, perm_name_eq, swap_inv, ha.1, eq_iff_iff,
      iff_true] at this
    have ha'' := hÏ€â‚‚ (Ï€â»Â¹ a) ?_ ?_
    Â· simp only [apply_inv_self] at ha''
      rw [â† ha'', swap_apply_left] at this
      contradiction
    Â· rwa [Finset.mem_perm, swap_inv, perm_name_eq]
    Â· rw [â† perm_name_eq, â† Finset.mem_perm, â† supp_perm_eq, â† supp_perm_eq, hÏ€â‚.1]
      exact ha.2
  Â· intro c hc
    rw [swap_apply_of_ne_of_ne] <;>
    Â· rintro rfl; contradiction

instance [Nominal ğ”¸ Î±] [Nominal ğ”¸ Î²] : Nominal ğ”¸ [Î±|ğ”¸]Î² where
  supported x := by
    induction x using ind
    case mk x y =>
    exact âŸ¨_, supports_mk _ _âŸ©

@[simp]
protected theorem supp_eq [Nominal ğ”¸ Î±] [Nominal ğ”¸ Î²] (x : Î±) (y : Î²) :
    supp ğ”¸ (âŸ¨xâŸ©y : [Î±|ğ”¸]Î²) = supp ğ”¸ y \ supp ğ”¸ x := by
  apply subset_antisymm
  Â· apply supp_minimal
    exact supports_mk x y
  Â· apply subset_of_supports
    exact Nominal.supp_supports ğ”¸ (âŸ¨xâŸ©y)

/-- An induction principle for `[Î±|ğ”¸]Î²`. -/
theorem fresh_induction [MulPerm ğ”¸ Î±] [MulPerm ğ”¸ Î²] [MulPerm ğ”¸ Î³] {motive : [Î±|ğ”¸]Î² â†’ Prop}
    (t : Î³) (h : âˆ€ x y, x #[ğ”¸] t â†’ motive (âŸ¨xâŸ©y)) (x : [Î±|ğ”¸]Î²) : motive x := by
  induction x using ind
  case mk x y =>
  obtain âŸ¨Ï€, hÏ€â‚, hÏ€â‚‚âŸ© := Finperm.exists_fresh (supp ğ”¸ x) (supp ğ”¸ t âˆª supp ğ”¸ y)
  have := h (Ï€ â¬ x) (Ï€ â¬ y) ?_
  Â· convert this using 1
    rw [mk_eq_iff]
    refine âŸ¨Ï€, rfl, ?_âŸ©
    aesop
  Â· rw [fresh_def, supp_perm_eq, Finset.disjoint_iff_ne]
    rintro a haâ‚ _ haâ‚‚ rfl
    rw [Finset.mem_perm, perm_name_eq] at haâ‚
    have := hÏ€â‚ (Ï€â»Â¹ a) haâ‚
    aesop

/-!
# Functoriality
-/

theorem exists_map [Nominal ğ”¸ Î±] [Nominal ğ”¸ Î²] [Nominal ğ”¸ Î³]
    (f : Î² â†’ Î³) (hf : FinitelySupported ğ”¸ f) (x : [Î±|ğ”¸]Î²) :
    âˆƒ y : [Î±|ğ”¸]Î³, âˆ€ a b, a #[ğ”¸] f â†’ x = âŸ¨aâŸ©b â†’ y = âŸ¨aâŸ©(f b) := by
  induction x using fresh_induction f
  infer_instance
  case h x y hxy =>
  use âŸ¨xâŸ©(f y)
  intro x' y' hxy' h
  rw [mk_eq_iff] at h âŠ¢
  rw [Abstract'.rel_iff] at h
  obtain âŸ¨Ï€, hÏ€â‚, hÏ€â‚‚, hÏ€â‚ƒâŸ© := h
  cases hÏ€â‚
  dsimp only at *
  have hfy := congr_fun (supp_supports' f hf Ï€ ?_) (Ï€ â¬ y)
  swap
  Â· intro a ha
    by_contra ha'
    rw [â† ne_eq, â† mem_support_iff] at ha'
    have := hÏ€â‚ƒ ha'
    simp only [fresh_def, supp_perm_eq, Finset.disjoint_iff_ne] at hxy hxy'
    aesop
  simp only [Function.perm_def, inv_perm_perm] at hfy
  refine âŸ¨Ï€, ?_, ?_âŸ©
  Â· rw [Abstract'.perm_def, Abstract'.mk.injEq]
    simp only [hfy, and_self]
  Â· simp only [fresh_iff, Finset.names_supp_eq, Finset.mem_sdiff, and_imp]
    intro a ha ha'
    by_contra ha''
    simp only [fresh_iff, Finset.names_supp_eq, Finset.mem_sdiff, and_imp] at hÏ€â‚‚
    have hay := hÏ€â‚‚ a
    simp only [ha', not_false_eq_true, ha'', imp_false, not_true_eq_false] at hay
    rw [â† ne_eq, â† mem_support_iff] at ha''
    have hax := hÏ€â‚ƒ ha''
    simp only [Finset.mem_union, ha', false_or] at hax
    rw [fresh_def, Finset.disjoint_iff_ne] at hxy'
    have := supp_apply_subset' f hf y ha
    simp only [Finset.mem_union] at this
    tauto

noncomputable def map [Nominal ğ”¸ Î±] [Nominal ğ”¸ Î²] [Nominal ğ”¸ Î³]
    (f : Î² â†’ Î³) (hf : FinitelySupported ğ”¸ f) (x : [Î±|ğ”¸]Î²) : [Î±|ğ”¸]Î³ :=
  (exists_map f hf x).choose

theorem map_mk [Nominal ğ”¸ Î±] [Nominal ğ”¸ Î²] [Nominal ğ”¸ Î³]
    {f : Î² â†’ Î³} {hf : FinitelySupported ğ”¸ f} {a : Î±} {b : Î²} (hab : a #[ğ”¸] f) :
    map f hf (âŸ¨aâŸ©b) = âŸ¨aâŸ©(f b) :=
  (exists_map f hf (âŸ¨aâŸ©b)).choose_spec a b hab rfl

end Abstract
