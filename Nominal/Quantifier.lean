import Mathlib.Order.Filter.Cofinite
import Nominal.Instances
import Nominal.Rel

open Filter Finperm

variable {ğ”¸ : Type*}

def NewNames (p : ğ”¸ â†’ Prop) : Prop :=
  âˆ€á¶  a in cofinite, p a

notation3 "Î½ "(...)", "r:(scoped p => NewNames p) => r

theorem newNames_def (p : ğ”¸ â†’ Prop) :
    (Î½ a, p a) â†” âˆ€á¶  a in cofinite, p a :=
  Iff.rfl

theorem newNames_def' (p : ğ”¸ â†’ Prop) :
    (Î½ a, p a) â†” {a | p a}á¶œ.Finite :=
  Iff.rfl

theorem NewNames.exists [Infinite ğ”¸] {p : ğ”¸ â†’ Prop} (h : Î½ a, p a) :
    âˆƒ a, p a :=
  Eventually.exists h

@[simp]
theorem newNames_true :
    Î½ _ : ğ”¸, True :=
  eventually_true _

theorem newNames_of_forall {p : ğ”¸ â†’ Prop} :
    (âˆ€ a, p a) â†’ Î½ a, p a :=
  Eventually.of_forall

@[simp]
theorem not_newNames_false [Infinite ğ”¸] :
    Â¬Î½ _ : ğ”¸, False :=
  eventually_false_iff_eq_bot.mp.mt NeBot.ne'

@[simp]
theorem newNames_const [Infinite ğ”¸] {p : Prop} :
    (Î½ _ : ğ”¸, p) â†” p :=
  eventually_const

theorem newNames_mp {p q : ğ”¸ â†’ Prop} (hp : Î½ a, p a) (hq : Î½ a, p a â†’ q a) :
    Î½ a, q a :=
  Eventually.mp hp hq

theorem newNames_mono {p q : ğ”¸ â†’ Prop} (hp : Î½ a, p a) (hq : âˆ€ a, p a â†’ q a) :
    Î½ a, q a :=
  Eventually.mono hp hq

theorem NewNames.mono {p q : ğ”¸ â†’ Prop} (h : Î½ a, p a) (h' : âˆ€ a, p a â†’ q a) :
    Î½ a, q a :=
  Eventually.mono h h'

theorem forall_newNames_of_newNames_forall {Î± : Type*} {p : ğ”¸ â†’ Î± â†’ Prop}
    (h : Î½ a, âˆ€ x, p a x) : âˆ€ x, Î½ a, p a x :=
  forall_eventually_of_eventually_forall h

@[simp]
theorem newNames_and {p q : ğ”¸ â†’ Prop} :
    (Î½ a, p a âˆ§ q a) â†” (Î½ a, p a) âˆ§ (Î½ a, q a) :=
  eventually_and

theorem NewNames.and {p q : ğ”¸ â†’ Prop} (h : Î½ a, p a) (h' : Î½ a, q a) :
    Î½ a, p a âˆ§ q a :=
  Eventually.and h h'

theorem NewNames.congr {p q : ğ”¸ â†’ Prop} (h : Î½ a, p a â†” q a) :
    (Î½ a, p a) â†” (Î½ a, q a) :=
  eventually_congr h

@[simp]
theorem newNames_all {Î¹ : Sort*} [Finite Î¹] {p : Î¹ â†’ ğ”¸ â†’ Prop} :
    (Î½ a, âˆ€ i, p i a) â†” âˆ€ i, Î½ a, p i a :=
  eventually_all

@[simp]
theorem newNames_all_finite {Î¹ : Type*} {I : Set Î¹} (hI : I.Finite) {p : Î¹ â†’ ğ”¸ â†’ Prop} :
    (Î½ a, âˆ€ i âˆˆ I, p i a) â†” âˆ€ i âˆˆ I, Î½ a, p i a :=
  eventually_all_finite hI

@[simp]
theorem newNames_all_finset {Î¹ : Type*} (I : Finset Î¹) {p : Î¹ â†’ ğ”¸ â†’ Prop} :
    (Î½ a, âˆ€ i âˆˆ I, p i a) â†” âˆ€ i âˆˆ I, Î½ a, p i a :=
  eventually_all_finset I

@[simp]
theorem newNames_or_distrib_left {p : Prop} {q : ğ”¸ â†’ Prop} :
    (Î½ a, p âˆ¨ q a) â†” p âˆ¨ Î½ a, q a :=
  eventually_or_distrib_left

@[simp]
theorem newNames_or_distrib_right {p : ğ”¸ â†’ Prop} {q : Prop} :
    (Î½ a, p a âˆ¨ q) â†” (Î½ a, p a) âˆ¨ q :=
  eventually_or_distrib_right

@[simp]
theorem newNames_imp_distrib_left {p : Prop} {q : ğ”¸ â†’ Prop} :
    (Î½ a, p â†’ q a) â†” p â†’ Î½ a, q a :=
  eventually_imp_distrib_left

theorem NewNames.not [Infinite ğ”¸] {p : ğ”¸ â†’ Prop} :
    (Î½ a, Â¬p a) â†’ Â¬Î½ a, p a := by
  intro hâ‚ hâ‚‚
  have := hâ‚.and hâ‚‚
  simp only [not_and_self, not_newNames_false] at this

/-- The law of the excluded middle for finitely supported predicates. -/
theorem newNames_em [DecidableEq ğ”¸] [Infinite ğ”¸] (p : ğ”¸ â†’ Prop)
    (hp : FinitelySupported ğ”¸ p) :
    (Î½ a, p a) âˆ¨ (Î½ a, Â¬p a) := by
  obtain h | h := hp.finite_or_finite
  Â· right
    apply h.subset
    simp only [Set.compl_setOf, not_not, subset_rfl]
  Â· left
    exact h

theorem NewNames.of_not [DecidableEq ğ”¸] [Infinite ğ”¸] (p : ğ”¸ â†’ Prop)
    (hp : FinitelySupported ğ”¸ p) :
    (Â¬Î½ a, p a) â†’ Î½ a, Â¬p a := by
  have := newNames_em p hp
  tauto

theorem newNames_not [DecidableEq ğ”¸] [Infinite ğ”¸] (p : ğ”¸ â†’ Prop)
    (hp : FinitelySupported ğ”¸ p) :
    (Î½ a, Â¬p a) â†” (Â¬Î½ a, p a) :=
  âŸ¨NewNames.not, NewNames.of_not p hpâŸ©

theorem NewNames.or_left {p : ğ”¸ â†’ Prop} (h : Î½ a, p a) (q : ğ”¸ â†’ Prop) :
    Î½ a, p a âˆ¨ q a := by
  apply h.mono
  exact Î» _ â†¦ Or.inl

theorem NewNames.or_right {p : ğ”¸ â†’ Prop} (h : Î½ a, p a) (q : ğ”¸ â†’ Prop) :
    Î½ a, q a âˆ¨ p a := by
  apply h.mono
  exact Î» _ â†¦ Or.inr

theorem newNames_or_left [DecidableEq ğ”¸] [Infinite ğ”¸] (p q : ğ”¸ â†’ Prop)
    (hp : FinitelySupported ğ”¸ p) :
    (Î½ a, p a âˆ¨ q a) â†” (Î½ a, p a) âˆ¨ (Î½ a, q a) := by
  constructor
  Â· intro h
    obtain h' | h' := newNames_em p hp
    Â· left
      exact h'
    Â· right
      apply (h.and h').mono
      tauto
  Â· rintro (h | h)
    Â· exact h.or_left q
    Â· exact h.or_right p

theorem newNames_or_right [DecidableEq ğ”¸] [Infinite ğ”¸] (p q : ğ”¸ â†’ Prop)
    (hq : FinitelySupported ğ”¸ q) :
    (Î½ a, p a âˆ¨ q a) â†” (Î½ a, p a) âˆ¨ (Î½ a, q a) := by
  have := newNames_or_left q p hq
  simp only [or_comm (a := q _), or_comm (a := Î½ a, q a)] at this
  exact this

theorem newNames_imp_left [DecidableEq ğ”¸] [Infinite ğ”¸] (p q : ğ”¸ â†’ Prop)
    (hp : FinitelySupported ğ”¸ p) :
    (Î½ a, p a â†’ q a) â†” (Î½ a, p a) â†’ (Î½ a, q a) := by
  simp only [imp_iff_not_or]
  rw [newNames_or_left _ _ hp.not, newNames_not p hp]

theorem newNames_iff [DecidableEq ğ”¸] [Infinite ğ”¸] (p q : ğ”¸ â†’ Prop)
    (hp : FinitelySupported ğ”¸ p) (hq : FinitelySupported ğ”¸ q) :
    (Î½ a, p a â†” q a) â†” ((Î½ a, p a) â†” (Î½ a, q a)) := by
  conv => lhs; simp only [iff_iff_implies_and_implies]
  rw [newNames_and, newNames_imp_left p q hp, newNames_imp_left q p hq]
  tauto

theorem NewNames.perm [DecidableEq ğ”¸] {p : ğ”¸ â†’ Prop} (h : Î½ a, p a) (Ï€ : Finperm ğ”¸) :
    Î½ a, p (Ï€ a) := by
  rw [newNames_def'] at h âŠ¢
  apply (h.image (Ï€â»Â¹ Â·)).subset
  intro a ha
  simp only [Set.mem_image, Set.mem_compl_iff, Set.mem_setOf_eq]
  exact âŸ¨_, ha, Finperm.inv_apply_self Ï€ aâŸ©

theorem NewNames.of_perm [DecidableEq ğ”¸] {p : ğ”¸ â†’ Prop} {Ï€ : Finperm ğ”¸} (h : Î½ a, p (Ï€ a)) :
    Î½ a, p a := by
  have := h.perm Ï€â»Â¹
  simp only [Finperm.apply_inv_self] at this
  exact this

theorem newNames_perm [DecidableEq ğ”¸] {p : ğ”¸ â†’ Prop} (Ï€ : Finperm ğ”¸) :
    (Î½ a, p a) â†” (Î½ a, p (Ï€ a)) :=
  âŸ¨Î» h â†¦ h.perm Ï€, Î» h â†¦ h.of_permâŸ©

theorem newNames_notMem [DecidableEq ğ”¸] (s : Finset ğ”¸) :
    Î½ a, a âˆ‰ s := by
  simp only [newNames_def', Set.compl_setOf, Decidable.not_not, Finset.setOf_mem,
    Finset.finite_toSet]

theorem newNames_fresh [DecidableEq ğ”¸] [Infinite ğ”¸] {Î± : Type*} [Nominal ğ”¸ Î±] (x : Î±) :
    Î½ a : ğ”¸, a #[ğ”¸] x := by
  simp only [name_fresh_iff, newNames_def', Set.compl_setOf, Decidable.not_not, Finset.setOf_mem,
    Finset.finite_toSet]

theorem FinitelySupported.new [DecidableEq ğ”¸] {Î± Î² : Type*}
    [MulPerm ğ”¸ Î±] [MulPerm ğ”¸ Î²] {f : Î± â†’ Î²} (hf : FinitelySupported ğ”¸ f) :
    Î½ (a : ğ”¸), Î½ (b : ğ”¸), âˆ€ x, swap a b â¬ f x = f (swap a b â¬ x) := by
  rw [Function.finitelySupported_iff] at hf
  obtain âŸ¨s, hsâŸ© := hf
  have := newNames_notMem s
  apply this.mono
  intro a ha
  apply this.mono
  intro b hb x
  rw [hs]
  intro c hc
  rw [swap_apply_of_ne_of_ne]
  Â· rintro rfl
    contradiction
  Â· rintro rfl
    contradiction

theorem finitelySupported_iff [DecidableEq ğ”¸] [Infinite ğ”¸] {Î± : Sort*} [MulPerm ğ”¸ Î±] (x : Î±) :
    FinitelySupported ğ”¸ x â†” Î½ (a : ğ”¸) (b : ğ”¸), swap a b â¬ x = x := by
  constructor
  Â· rintro âŸ¨s, hsâŸ©
    rw [supports_iff'] at hs
    apply (newNames_notMem s).mono
    intro a ha
    apply (newNames_notMem s).mono
    intro b hb
    exact hs a b ha hb
  Â· intro h
    simp only [FinitelySupported, supports_iff, ne_eq]
    rw [newNames_def'] at h
    use h.toFinset
    intro a b ha hb hab
    simp only [Set.Finite.mem_toFinset, Set.mem_compl_iff, Set.mem_setOf_eq, not_not] at ha hb
    obtain âŸ¨c, hac, hbc, hcbâŸ© := (ha.and (hb.and (newNames_notMem {b}))).exists
    simp only [Finset.mem_singleton] at hcb
    rw [swap_triple a b c hab (Ne.symm hcb), mul_perm, mul_perm, hac, hbc, hac]

/-!
## The some/any theorem
-/

variable [DecidableEq ğ”¸] [Infinite ğ”¸] {Î± Î² : Type*} [Nominal ğ”¸ Î±] [Nominal ğ”¸ Î²]

theorem exists_of_newNames (p : ğ”¸ â†’ Î± â†’ Prop) (x : Î±) :
    (Î½ a, p a x) â†’ âˆƒ a #[ğ”¸] x, p a x := by
  intro h
  apply NewNames.exists
  simp only [newNames_and, newNames_fresh, h, and_self]

theorem newNames_of_exists (p : ğ”¸ â†’ Î± â†’ Prop) (hp : Equivariant ğ”¸ p) (x : Î±) :
    (âˆƒ a #[ğ”¸] x, p a x) â†’ Î½ a, p a x := by
  rintro âŸ¨a, hâ‚, hâ‚‚âŸ©
  apply (supp ğ”¸ x).finite_toSet.subset
  intro b hb
  contrapose hb
  simp only [Finset.mem_coe, â† name_fresh_iff] at hb
  simp only [Set.mem_compl_iff, Set.mem_setOf_eq, not_not]
  rwa [hp.rename_of_fresh b a x hb hâ‚]

theorem forall_fresh_of_newNames (p : ğ”¸ â†’ Î± â†’ Prop) (hp : Equivariant ğ”¸ p) (x : Î±) :
    (Î½ a, p a x) â†’ âˆ€ a #[ğ”¸] x, p a x := by
  intro h
  by_contra! h'
  have := newNames_of_exists (Î» a x â†¦ Â¬p a x) hp.notâ‚‚ x h'
  have := h.and this
  simp only [and_not_self, not_newNames_false] at this

theorem newNames_of_forall_fresh (p : ğ”¸ â†’ Î± â†’ Prop) (x : Î±) :
    (âˆ€ a #[ğ”¸] x, p a x) â†’ Î½ a, p a x := by
  intro h
  apply (supp ğ”¸ x).finite_toSet.subset
  intro b hb
  contrapose hb
  simp only [Finset.mem_coe, â† name_fresh_iff] at hb
  simp only [Set.mem_compl_iff, Set.mem_setOf_eq, not_not]
  exact h b hb

theorem newNames_iff_exists (p : ğ”¸ â†’ Î± â†’ Prop) (hp : Equivariant ğ”¸ p) (x : Î±) :
    (Î½ a, p a x) â†” âˆƒ a #[ğ”¸] x, p a x :=
  âŸ¨exists_of_newNames p x, newNames_of_exists p hp xâŸ©

theorem newNames_iff_forall (p : ğ”¸ â†’ Î± â†’ Prop) (hp : Equivariant ğ”¸ p) (x : Î±) :
    (Î½ a, p a x) â†” âˆ€ a #[ğ”¸] x, p a x :=
  âŸ¨forall_fresh_of_newNames p hp x, newNames_of_forall_fresh p xâŸ©

theorem Equivariant.exists_iff_forall (p : ğ”¸ â†’ Î± â†’ Prop) (hp : Equivariant ğ”¸ p) (x : Î±) :
    (âˆƒ a #[ğ”¸] x, p a x) â†” (âˆ€ a #[ğ”¸] x, p a x) := by
  rw [â† newNames_iff_exists p hp, newNames_iff_forall p hp]

theorem newNames_iff_existsâ‚‚ (p : ğ”¸ â†’ Î± â†’ Î² â†’ Prop) (hp : Equivariant ğ”¸ p) (x : Î±) (y : Î²) :
    (Î½ a, p a x y) â†” âˆƒ a #[ğ”¸] x, a #[ğ”¸] y âˆ§ p a x y := by
  rw [newNames_iff_exists _ hp.uncurryâ‚‚ âŸ¨x, yâŸ©]
  simp only [Prod.fresh_iff, and_assoc]

theorem newNames_iff_forallâ‚‚ (p : ğ”¸ â†’ Î± â†’ Î² â†’ Prop) (hp : Equivariant ğ”¸ p) (x : Î±) (y : Î²) :
    (Î½ a, p a x y) â†” âˆ€ a #[ğ”¸] x, a #[ğ”¸] y â†’ p a x y := by
  rw [newNames_iff_forall _ hp.uncurryâ‚‚ âŸ¨x, yâŸ©]
  simp only [Prod.fresh_iff, and_imp]

omit [Infinite ğ”¸] in
theorem app_equivariant : Equivariant ğ”¸ (Î» a (p : FS ğ”¸ (ğ”¸ â†’ Î± â†’ Prop)) x â†¦ p.val a x) := by
  intro Ï€
  ext p y
  simp only [Function.perm_def, perm_name_eq, FS.perm_coe, inv_inv, apply_inv_self,
    IsDiscrete.perm_eq, perm_inv_perm]

theorem newNames_iff_exists_fresh (p : ğ”¸ â†’ Î± â†’ Prop) (hp : FinitelySupported ğ”¸ p) (x : Î±) :
    (Î½ a, p a x) â†” âˆƒ a #[ğ”¸] p, a #[ğ”¸] x âˆ§ p a x := by
  have := newNames_iff_existsâ‚‚ (ğ”¸ := ğ”¸) (Î± := FS ğ”¸ (ğ”¸ â†’ Î± â†’ Prop)) (Î² := Î±)
    (Î» a p x â†¦ p.val a x) app_equivariant âŸ¨p, hpâŸ© x
  simp only [FS.fresh_iff] at this
  exact this

theorem newNames_iff_forall_fresh (p : ğ”¸ â†’ Î± â†’ Prop) (hp : FinitelySupported ğ”¸ p) (x : Î±) :
    (Î½ a, p a x) â†” âˆ€ a #[ğ”¸] p, a #[ğ”¸] x â†’ p a x := by
  have := newNames_iff_forallâ‚‚ (ğ”¸ := ğ”¸) (Î± := FS ğ”¸ (ğ”¸ â†’ Î± â†’ Prop)) (Î² := Î±)
    (Î» a p x â†¦ p.val a x) app_equivariant âŸ¨p, hpâŸ© x
  simp only [FS.fresh_iff] at this
  exact this

/-!
## The freshness theorem
-/

theorem fresh_of_coinjective {r : Î± â†’ Î² â†’ Prop} (hâ‚ : Rel.Coinjective r)
    (hâ‚‚ : FinitelySupported ğ”¸ r)
    {x : Î±} {y : Î²} (h : r x y) {a : ğ”¸} (hr : a #[ğ”¸] r) (hx : a #[ğ”¸] x) : a #[ğ”¸] y := by
  have := supp_supports' r hâ‚‚
  simp only [Function.supports_iff, funext_iff, Function.perm_def, IsDiscrete.perm_eq,
    eq_iff_iff] at this
  rw [fresh_iff_exists_swap_perm_eq]
  obtain âŸ¨b, hbx, hby, hbrâŸ© := ((newNames_fresh (ğ”¸ := ğ”¸) x).and
    ((newNames_fresh y).and (newNames_notMem (supp ğ”¸ r)))).exists
  refine âŸ¨b, hby, ?_âŸ©
  have := this (swap a b) ?_ x y
  Â· rw [swap_perm_eq_of_fresh a b x hx hbx, swap_inv] at this
    have := hâ‚.coinjective h (this.mpr h)
    rw [â† this]
  Â· intro c hc
    rw [swap_apply_of_ne_of_ne]
    Â· rintro rfl
      rw [name_fresh_iff] at hr
      contradiction
    Â· rintro rfl
      contradiction

/-- The `fresh` relation from the freshness theorem. -/
def freshRel (r : FS ğ”¸ (ğ”¸ â†’ Î± â†’ Prop)) (x : Î±) : Prop :=
  Rel.Coinjective r.val âˆ§ Î½ a, r.val a x

theorem freshRel_coinjective : Rel.Coinjective (freshRel (ğ”¸ := ğ”¸) (Î± := Î±)) := by
  constructor
  rintro x y r âŸ¨hr, hrxâŸ© âŸ¨-, hryâŸ©
  obtain âŸ¨a, hax, hayâŸ© := (hrx.and hry).exists
  exact hr.coinjective hax hay

theorem mem_freshRel_dom (r : FS ğ”¸ (ğ”¸ â†’ Î± â†’ Prop))
    (h : Rel.Coinjective r.val) (h' : Î½ a, âˆƒ x, a #[ğ”¸] x âˆ§ r.val a x) :
    r âˆˆ Rel.dom freshRel := by
  obtain âŸ¨a, âŸ¨x, hax, hraxâŸ©, harâŸ© := (h'.and (newNames_fresh r)).exists
  refine âŸ¨x, h, ?_âŸ©
  rw [newNames_iff_exists_fresh _ r.prop]
  rw [FS.fresh_iff] at har
  exact âŸ¨a, har, hax, hraxâŸ©

/-- **The freshness theorem** for relations. -/
theorem exists_of_newNames_fresh (r : ğ”¸ â†’ Î± â†’ Prop)
    (hâ‚ : Rel.Coinjective r) (hâ‚‚ : FinitelySupported ğ”¸ r)
    (hâ‚ƒ : Î½ a, âˆƒ x, a #[ğ”¸] x âˆ§ r a x) :
    âˆƒ! x, Î½ a, r a x := by
  obtain âŸ¨x, hxâŸ© := mem_freshRel_dom âŸ¨r, hâ‚‚âŸ© hâ‚ hâ‚ƒ
  refine âŸ¨x, hx.2, ?_âŸ©
  intro y hy
  obtain âŸ¨a, hax, hayâŸ© := (hx.2.and hy).exists
  exact hâ‚.coinjective hay hax

noncomputable def freshName {Î± : Sort*} [Nonempty Î±] (f : ğ”¸ â†’ Î±) : Î± :=
  Classical.epsilon (Î» x â†¦ Î½ a, f a = x)

/-- The `fresh` syntax for creating terms that need, but do not depend on, a choice of name. -/
notation3 "fresh "(...)", "r:(scoped p => freshName p) => r

/-- **The freshness theorem** for functions. -/
theorem fresh_spec [Nonempty Î±] (f : ğ”¸ â†’ Î±) (hfâ‚ : Î½ a, a #[ğ”¸] f a) (hfâ‚‚ : FinitelySupported ğ”¸ f) :
    Î½ a, (fresh b, f b) = f a := by
  have := exists_of_newNames_fresh _ ?_ hfâ‚‚.graph ?_
  Â· obtain âŸ¨x, hx, -âŸ© := this
    rw [freshName]
    have := Classical.epsilon_spec (p := Î» x â†¦ Î½ a, f a = x) âŸ¨x, hxâŸ©
    apply (hx.and this).mono
    rintro a âŸ¨haâ‚, haâ‚‚âŸ©
    exact haâ‚‚.symm
  Â· constructor
    rintro x y a rfl rfl
    rfl
  Â· apply hfâ‚.mono
    simp only [exists_eq_right', imp_self, implies_true]
