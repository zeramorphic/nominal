import Mathlib.CategoryTheory.Adjunction.AdjointFunctorTheorems
import Mathlib.CategoryTheory.Functor.FullyFaithful
import Mathlib.CategoryTheory.SingleObj
import Mathlib.CategoryTheory.Monad.Limits
import Mathlib.CategoryTheory.Limits.Constructions.LimitsOfProductsAndEqualizers
import Mathlib.CategoryTheory.Whiskering
import Nominal.Instances

/-!
# The category of nominal sets

In this file, we translate our nominal definitions into the language of mathlib's
category theory library. This allows us to appeal to results such as the adjoint
functor theorem.

It is difficult to use these results directly because they are stated in bundled form
(objects are types-with-structure not types together with typeclass-inferred structure),
whereas the rest of this nominal sets library uses unbundled form.
-/

open CategoryTheory Functor Limits

variable {ğ”¸ : Type*} [DecidableEq ğ”¸]

protected def MulPerm.of (Î± : Type*) [str : MulPerm ğ”¸ Î±] : Bundled (MulPerm ğ”¸) :=
  âŸ¨Î±, strâŸ©

instance MulPerm.coeSort : CoeSort (Bundled (MulPerm ğ”¸)) (Type _) :=
  âŸ¨Bundled.Î±âŸ©

theorem MulPerm.coe_mk (Î±) (str) : (@Bundled.mk (MulPerm ğ”¸) Î± str : Type _) = Î± :=
  rfl

instance {Î± : Bundled (MulPerm ğ”¸)} : MulPerm ğ”¸ Î± :=
  Î±.str

protected def Nominal.of (Î± : Type*) [str : Nominal ğ”¸ Î±] : Bundled (Nominal ğ”¸) :=
  âŸ¨Î±, strâŸ©

instance Nominal.coeSort : CoeSort (Bundled (Nominal ğ”¸)) (Type _) :=
  âŸ¨Bundled.Î±âŸ©

theorem Nominal.coe_mk (Î±) (str) : (@Bundled.mk (Nominal ğ”¸) Î± str : Type _) = Î± :=
  rfl

instance {Î± : Bundled (Nominal ğ”¸)} : Nominal ğ”¸ Î± :=
  Î±.str

instance {Î± Î² : Type*} [HasPerm ğ”¸ Î±] [HasPerm ğ”¸ Î²] :
    FunLike {f : Î± â†’ Î² // Equivariant ğ”¸ f} Î± Î² where
  coe := Subtype.val
  coe_injective' := Subtype.val_injective

instance : Category (Bundled (MulPerm ğ”¸)) where
  Hom Î± Î² := {f : Î± â†’ Î² // Equivariant ğ”¸ f}
  id _ := âŸ¨id, id_equivariantâŸ©
  comp f g := âŸ¨g.val âˆ˜ f.val, g.prop.comp f.propâŸ©

instance : ConcreteCategory (Bundled (MulPerm ğ”¸)) (Î» Î± Î² â†¦ {f : Î± â†’ Î² // Equivariant ğ”¸ f}) where
  hom := id
  ofHom := id

instance : Category (Bundled (Nominal ğ”¸)) where
  Hom Î± Î² := {f : Î± â†’ Î² // Equivariant ğ”¸ f}
  id _ := âŸ¨id, id_equivariantâŸ©
  comp f g := âŸ¨g.val âˆ˜ f.val, g.prop.comp f.propâŸ©

instance : ConcreteCategory (Bundled (Nominal ğ”¸)) (Î» Î± Î² â†¦ {f : Î± â†’ Î² // Equivariant ğ”¸ f}) where
  hom := id
  ofHom := id

def nominalInclusion.{u} (ğ”¸ : Type*) [DecidableEq ğ”¸] :
    Bundled.{u} (Nominal ğ”¸) â¥¤ Bundled.{u} (MulPerm ğ”¸) where
  obj := Bundled.map (Î» x â†¦ x.toMulPerm)
  map := id

def nominalCoreflection.{u} (ğ”¸ : Type*) [DecidableEq ğ”¸] :
    Bundled.{u} (MulPerm ğ”¸) â¥¤ Bundled.{u} (Nominal ğ”¸) where
  obj Î± := Nominal.of (FS ğ”¸ Î±)
  map {X Y} f := âŸ¨(f.prop.comp FS.val_equivariant).toFS,
    (f.prop.comp FS.val_equivariant).toFS_equivariantâŸ©

def nominalInclusionFullyFaithful : FullyFaithful (nominalInclusion ğ”¸) where
  preimage := id

instance : Faithful (nominalInclusion ğ”¸) := nominalInclusionFullyFaithful.faithful

instance : Full (nominalInclusion ğ”¸) := nominalInclusionFullyFaithful.full

def nominalInclusionAdj (ğ”¸ : Type*) [DecidableEq ğ”¸] :
    nominalInclusion ğ”¸ âŠ£ nominalCoreflection ğ”¸ where
  unit := {
    app Î± := âŸ¨id_equivariant.toFS, id_equivariant.toFS_equivariantâŸ©
  }
  counit := {
    app Î± := âŸ¨FS.val, FS.val_equivariantâŸ©
  }

instance nominalCoreflective : Coreflective (nominalInclusion ğ”¸) where
  R := nominalCoreflection ğ”¸
  adj := nominalInclusionAdj ğ”¸

def nominalInclusion_nominalCoreflection.{v} (ğ”¸ : Type*) [DecidableEq ğ”¸] :
    nominalInclusion.{v} ğ”¸ â‹™ nominalCoreflection ğ”¸ â‰… ğŸ­ (Bundled (Nominal ğ”¸)) where
  hom := {
    app Î± := âŸ¨FS.val, FS.val_equivariantâŸ©
  }
  inv := {
    app Î± := âŸ¨Î» x â†¦ âŸ¨x, Nominal.supported xâŸ©, by
      intro Ï€
      ext x
      apply FS.val_injective
      exact perm_inv_perm Ï€ xâŸ©
  }

/-! We can identify the `Finperm ğ”¸`-types with functors from
the delooping of `Finperm ğ”¸` into `Type u`. -/

instance {F : SingleObj (Finperm ğ”¸) â¥¤ Type*} :
    MulPerm ğ”¸ (F.obj (SingleObj.star (Finperm ğ”¸))) where
  perm := F.map
  one_perm x := congr_fun (F.map_id (SingleObj.star (Finperm ğ”¸))) x
  mul_perm Ï€â‚ Ï€â‚‚ x := congr_fun (F.map_comp Ï€â‚‚ Ï€â‚) x

def mulPermMap.{u} (ğ”¸ : Type*) [DecidableEq ğ”¸] :
    Bundled (MulPerm.{u + 1} ğ”¸) â¥¤ SingleObj (Finperm ğ”¸) â¥¤ Type u where
  obj Î± := {
    obj _ := Î±
    map Ï€ x := Ï€ â¬ x
    map_id _ := by ext; exact one_perm _
    map_comp _ _ := by ext; exact mul_perm _ _ _
  }
  map f := {
    app _ := f
    naturality _ _ Ï€ := by ext a; exact (apply_perm_eq f.prop Ï€ a).symm
  }

def mulPermMap'.{u} (ğ”¸ : Type*) [DecidableEq ğ”¸] :
    (SingleObj (Finperm ğ”¸) â¥¤ Type u) â¥¤ Bundled (MulPerm.{u + 1} ğ”¸) where
  obj F := MulPerm.of (F.obj (SingleObj.star (Finperm ğ”¸)))
  map f := {
    val := f.app (SingleObj.star (Finperm ğ”¸))
    property := by
      rw [Function.equivariant_iff]
      intro Ï€ x
      exact (congr_fun (f.naturality (X := SingleObj.star _) (Y := SingleObj.star _) Ï€) x).symm
  }

def mulPermEquiv.{u} (ğ”¸ : Type*) [DecidableEq ğ”¸] :
    Bundled (MulPerm.{u + 1} ğ”¸) â‰Œ (SingleObj (Finperm ğ”¸) â¥¤ Type u) where
  functor := mulPermMap ğ”¸
  inverse := mulPermMap' ğ”¸
  unitIso := Iso.refl _
  counitIso := Iso.refl _

/-!
# Limits and colimits
-/

noncomputable instance mulPermEquiv_createsLimits (ğ”¸ : Type*) [DecidableEq ğ”¸] :
    CreatesLimits (mulPermEquiv ğ”¸).functor :=
  createsLimitsOfIsEquivalence _

noncomputable instance mulPermEquiv_inverse_preservesLimits (ğ”¸ : Type*) [DecidableEq ğ”¸] :
    CreatesLimits (mulPermEquiv ğ”¸).inverse :=
  createsLimitsOfIsEquivalence _

noncomputable instance mulPermEquiv_createsColimits (ğ”¸ : Type*) [DecidableEq ğ”¸] :
    CreatesColimits (mulPermEquiv ğ”¸).functor :=
  createsColimitsOfIsEquivalence _

noncomputable instance mulPermEquiv_inverse_createsColimits (ğ”¸ : Type*) [DecidableEq ğ”¸] :
    CreatesColimits (mulPermEquiv ğ”¸).inverse :=
  createsColimitsOfIsEquivalence _

instance MulPerm.hasLimits.{vâ‚, uâ‚, v} (ğ”¸ : Type*) [DecidableEq ğ”¸] [UnivLE.{uâ‚, v}] :
    HasLimitsOfSize.{vâ‚, uâ‚} (Bundled.{v} (MulPerm ğ”¸)) :=
  hasLimits_of_hasLimits_createsLimits (mulPermEquiv ğ”¸).functor

instance MulPerm.hasColimits.{vâ‚, uâ‚, v} (ğ”¸ : Type*) [DecidableEq ğ”¸] [UnivLE.{uâ‚, v}] :
    HasColimitsOfSize.{vâ‚, uâ‚} (Bundled.{v} (MulPerm ğ”¸)) :=
  hasColimits_of_hasColimits_createsColimits (mulPermEquiv ğ”¸).functor

/-! We show that the coreflection of nominal sets in `Finperm ğ”¸`-sets is a geometric morphism. -/

instance nominalCoreflection_preservesLimits.{u, v} (ğ”¸ : Type u) [DecidableEq ğ”¸] :
    PreservesLimitsOfSize (nominalCoreflection.{v} ğ”¸) :=
  (nominalInclusionAdj ğ”¸).rightAdjoint_preservesLimits

instance nominalInclusion_preservesColimits.{u, v} (ğ”¸ : Type u) [DecidableEq ğ”¸] :
    PreservesColimitsOfSize (nominalInclusion.{v} ğ”¸) :=
  (nominalInclusionAdj ğ”¸).leftAdjoint_preservesColimits

def MulPerm.coproductCocone {J : Type*} (K : Discrete J â¥¤ Bundled (MulPerm ğ”¸)) :
    Cocone K where
  pt := âŸ¨(j : Discrete J) Ã— K.obj j, inferInstanceâŸ©
  Î¹ := {
    app j := show {f // Equivariant ğ”¸ f} from âŸ¨Î» x â†¦ âŸ¨j, xâŸ©,
      by intro Ï€; ext x; rw [Function.perm_def, Sigma.perm_mk, perm_inv_perm]âŸ©
    naturality j k h := by cases Discrete.ext (Discrete.eq_of_hom h); simp
  }

def MulPerm.coproductCocone_isColimit {J : Type*} (K : Discrete J â¥¤ Bundled (MulPerm ğ”¸)) :
    IsColimit (coproductCocone K) where
  desc s := âŸ¨Î» x â†¦ s.Î¹.app x.fst x.snd, by
    intro Ï€
    ext x
    apply (apply_perm_eq (s.Î¹.app (Ï€â»Â¹ â¬ x).fst).prop Ï€ (Ï€â»Â¹ â¬ x).snd).trans
    rw [Sigma.perm_snd, perm_inv_perm]
    rflâŸ©
  uniq := by
    intro s m h
    ext x
    exact congr_arg (Â· x.snd) (h x.fst)

def MulPerm.nominalCoreflection_coproductCocone_isColimit
    {J : Type*} (K : Discrete J â¥¤ Bundled (MulPerm ğ”¸)) :
    IsColimit ((nominalCoreflection ğ”¸).mapCocone (coproductCocone K)) where
  desc s := âŸ¨Î» x â†¦ s.Î¹.app x.val.fst âŸ¨x.val.snd, Sigma.snd_finitelySupported x.propâŸ©, by
    intro Ï€
    ext x
    apply (apply_perm_eq (s.Î¹.app (Ï€â»Â¹ â¬ x).val.fst).prop Ï€ _).trans
    apply congr_arg (s.Î¹.app _)
    exact perm_inv_perm Ï€ (show FS ğ”¸ _ from âŸ¨x.val.snd, Sigma.snd_finitelySupported x.propâŸ©)âŸ©
  uniq := by
    intro s m h
    ext x
    simp only [mapCocone_pt, ConcreteCategory.hom, id_eq, comp_obj, const_obj_obj]
    rw [â† h x.val.fst]
    rfl

/-- The coreflector preserves coproducts, but it does not preserve coequalisers.
Hence, it has no right adjoint. -/
instance nominalCoreflection_preservesCoproducts.{u, v} (ğ”¸ : Type u) [DecidableEq ğ”¸]
    {J : Type v} :
    PreservesColimitsOfShape (Discrete J) (nominalCoreflection.{v} ğ”¸) := by
  refine âŸ¨Î» {K} â†¦ ?_âŸ©
  apply preservesColimit_of_preserves_colimit_cocone
  Â· exact MulPerm.coproductCocone_isColimit K
  Â· exact MulPerm.nominalCoreflection_coproductCocone_isColimit K

def Nominal.coproductCocone [Infinite ğ”¸] {J : Type*} (K : Discrete J â¥¤ Bundled (Nominal ğ”¸)) :
    Cocone K where
  pt := âŸ¨(j : Discrete J) Ã— K.obj j, inferInstanceâŸ©
  Î¹ := {
    app j := show {f // Equivariant ğ”¸ f} from âŸ¨Î» x â†¦ âŸ¨j, xâŸ©,
      by intro Ï€; ext x; rw [Function.perm_def, Sigma.perm_mk, perm_inv_perm]âŸ©
    naturality j k h := by cases Discrete.ext (Discrete.eq_of_hom h); simp
  }

def Nominal.coproductCocone_isColimit [Infinite ğ”¸] {J : Type*}
    (K : Discrete J â¥¤ Bundled (Nominal ğ”¸)) :
    IsColimit (coproductCocone K) where
  desc s := âŸ¨Î» x â†¦ s.Î¹.app x.fst x.snd, by
    intro Ï€
    ext x
    apply (apply_perm_eq (s.Î¹.app (Ï€â»Â¹ â¬ x).fst).prop Ï€ (Ï€â»Â¹ â¬ x).snd).trans
    rw [Sigma.perm_snd, perm_inv_perm]
    rflâŸ©
  uniq := by
    intro s m h
    ext x
    exact congr_arg (Â· x.snd) (h x.fst)

instance Nominal.hasCoproducts.{v} [Infinite ğ”¸] :
    HasCoproducts.{v} (Bundled.{v} (Nominal ğ”¸)) :=
  Î» _ â†¦ âŸ¨Î» K â†¦ âŸ¨Nominal.coproductCocone K, Nominal.coproductCocone_isColimit KâŸ©âŸ©

def Nominal.coequaliserCocone [Infinite ğ”¸] (K : WalkingParallelPair â¥¤ Bundled (Nominal ğ”¸)) :
    Cocone K where
  pt := âŸ¨Coequaliser (K.map .left) (K.map .right) (K.map .left).prop (K.map .right).prop,
    inferInstanceâŸ©
  Î¹ := {
    app j := match j with
      | .zero => âŸ¨Coequaliser.mk (hf := (K.map .left).prop) (hg := (K.map .right).prop) âˆ˜
          K.map .right,
        Coequaliser.mk_equivariant.comp (K.map .right).propâŸ©
      | .one => âŸ¨Coequaliser.mk (hf := (K.map .left).prop) (hg := (K.map .right).prop),
        Coequaliser.mk_equivariantâŸ©
    naturality j k h := by
      ext x
      cases h
      case left =>
        simp only [ConcreteCategory.hom, id_eq, const_obj_obj, const_obj_map, Category.comp_id,
          Function.comp_apply]
        apply Coequaliser.condition
      case right => rfl
      case id =>
        simp only [ConcreteCategory.hom, id_eq, const_obj_obj, walkingParallelPairHom_id,
          CategoryTheory.Functor.map_id, Category.id_comp, const_obj_map, Category.comp_id]
  }

def Nominal.coequaliserCocone_isColimit [Infinite ğ”¸]
    (K : WalkingParallelPair â¥¤ Bundled (Nominal ğ”¸)) :
    IsColimit (coequaliserCocone K) where
  desc s := âŸ¨Coequaliser.factor
    (K.map .left) (K.map .right) (K.map .left).prop (K.map .right).prop
    (s.Î¹.app _)
    Î» x â†¦ (congr_arg (Â· x) (s.Î¹.naturality .left)).trans
      (congr_arg (Â· x) (s.Î¹.naturality .right)).symm,
    Coequaliser.factor_equivariant (s.Î¹.app .one).propâŸ©
  fac s j := by
    ext x
    cases j
    case zero => exact congr_arg (Â· x) (s.Î¹.naturality .right)
    case one => rfl
  uniq s m h := by
    ext x
    obtain âŸ¨x, rflâŸ© := Coequaliser.mk_surjective (hf := (K.map .left).prop) (hg := (K.map .right).prop) x
    exact congr_arg (Â· x) (h .one)

instance Nominal.hasCoequalisers [Infinite ğ”¸] :
    HasCoequalizers (Bundled (Nominal ğ”¸)) :=
  âŸ¨Î» F â†¦ âŸ¨Nominal.coequaliserCocone F, Nominal.coequaliserCocone_isColimit FâŸ©âŸ©

instance Nominal.hasLimits.{vâ‚, uâ‚, u, v} (ğ”¸ : Type u) [DecidableEq ğ”¸] [UnivLE.{uâ‚, v}] :
    HasLimitsOfSize.{vâ‚, uâ‚} (Bundled.{v} (Nominal ğ”¸)) := by
  refine âŸ¨Î» {J} _ â†¦ âŸ¨Î» {K} â†¦ ?_âŸ©âŸ©
  obtain âŸ¨c, hcâŸ© := ((MulPerm.hasLimits.{vâ‚, uâ‚} ğ”¸).has_limits_of_shape J).has_limit
    (K â‹™ nominalInclusion.{v} ğ”¸)
  obtain âŸ¨hc'âŸ© :=
    (nominalCoreflection_preservesLimits ğ”¸).preservesLimitsOfShape.preservesLimit.preserves hc
  apply @hasLimitOfIso _ _ _ _ _ _ âŸ¨_, hc'âŸ©
  apply (Functor.associator _ _ _).trans
  exact isoWhiskerLeft K (nominalInclusion_nominalCoreflection.{v} ğ”¸)

instance Nominal.hasColimits.{u, v} (ğ”¸ : Type u) [DecidableEq ğ”¸] [Infinite ğ”¸] :
    HasColimitsOfSize.{v, v} (Bundled.{v} (Nominal ğ”¸)) :=
  has_colimits_of_hasCoequalizers_and_coproducts

def Nominal.pairCone.{v} (K : Discrete WalkingPair â¥¤ Bundled.{v} (Nominal ğ”¸)) :
    Cone K where
  pt := âŸ¨K.obj âŸ¨.leftâŸ© Ã— K.obj âŸ¨.rightâŸ©, inferInstanceâŸ©
  Ï€ := {
    app j := match j with
      | âŸ¨.leftâŸ© => âŸ¨Prod.fst, Prod.fst_equivariantâŸ©
      | âŸ¨.rightâŸ© => âŸ¨Prod.snd, Prod.snd_equivariantâŸ©
    naturality j k f := by
      cases Discrete.ext (Discrete.eq_of_hom f)
      simp only [const_obj_obj, Discrete.functor_map_id, Category.id_comp, Category.comp_id]
  }

def Nominal.pairCone_isLimit (K : Discrete WalkingPair â¥¤ Bundled (Nominal ğ”¸)) :
    IsLimit (pairCone K) where
  lift s := âŸ¨Î» x â†¦ âŸ¨s.Ï€.app âŸ¨.leftâŸ© x, s.Ï€.app âŸ¨.rightâŸ© xâŸ©, by
    intro Ï€
    ext x
    apply Prod.ext
    Â· apply (apply_perm_eq (s.Ï€.app âŸ¨.leftâŸ©).prop Ï€ _).trans
      rw [perm_inv_perm]
      rfl
    Â· apply (apply_perm_eq (s.Ï€.app âŸ¨.rightâŸ©).prop Ï€ _).trans
      rw [perm_inv_perm]
      rflâŸ©
  fac s j := by
    obtain âŸ¨j | jâŸ© := j
    case left => rfl
    case right => rfl
  uniq s m h := by
    ext x
    apply Prod.ext
    Â· exact congr_arg (Â· x) (h âŸ¨.leftâŸ©)
    Â· exact congr_arg (Â· x) (h âŸ¨.rightâŸ©)

def Nominal.nominalInclusion_pairCone_isLimit (K : Discrete WalkingPair â¥¤ Bundled (Nominal ğ”¸)) :
    IsLimit ((nominalInclusion ğ”¸).mapCone (pairCone K)) where
  lift s := âŸ¨Î» x â†¦ âŸ¨s.Ï€.app âŸ¨.leftâŸ© x, s.Ï€.app âŸ¨.rightâŸ© xâŸ©, by
    intro Ï€
    ext x
    apply Prod.ext
    Â· apply (apply_perm_eq (s.Ï€.app âŸ¨.leftâŸ©).prop Ï€ _).trans
      rw [perm_inv_perm]
      rfl
    Â· apply (apply_perm_eq (s.Ï€.app âŸ¨.rightâŸ©).prop Ï€ _).trans
      rw [perm_inv_perm]
      rflâŸ©
  fac s j := by
    obtain âŸ¨j | jâŸ© := j
    case left => rfl
    case right => rfl
  uniq s m h := by
    ext x
    apply Prod.ext
    Â· exact congr_arg (Â· x) (h âŸ¨.leftâŸ©)
    Â· exact congr_arg (Â· x) (h âŸ¨.rightâŸ©)

instance nominalInclusion_preservesBinaryProducts.{u, v} (ğ”¸ : Type u) [DecidableEq ğ”¸] :
    PreservesLimitsOfShape (Discrete WalkingPair) (nominalInclusion.{v} ğ”¸) :=
  âŸ¨Î» {K} â†¦ preservesLimit_of_preserves_limit_cone
    (Nominal.pairCone_isLimit K) (Nominal.nominalInclusion_pairCone_isLimit K)âŸ©

def Nominal.emptyCone.{v} (K : Discrete PEmpty â¥¤ Bundled.{v} (Nominal ğ”¸)) :
    Cone K where
  pt := âŸ¨PUnit, inferInstanceâŸ©
  Ï€ := {
    app j := j.as.elim
  }

def Nominal.emptyCone_isLimit.{v} (K : Discrete PEmpty â¥¤ Bundled.{v} (Nominal ğ”¸)) :
    IsLimit (emptyCone K) where
  lift s := âŸ¨Î» _ â†¦ PUnit.unit, Î» _ â†¦ rflâŸ©

def Nominal.nominalInclusion_emptyCone_isLimit.{v} (K : Discrete PEmpty â¥¤ Bundled.{v} (Nominal ğ”¸)) :
    IsLimit ((nominalInclusion ğ”¸).mapCone (emptyCone K)) where
  lift s := âŸ¨Î» _ â†¦ PUnit.unit, Î» _ â†¦ rflâŸ©

instance nominalInclusion_preservesEmpty.{u, v} (ğ”¸ : Type u) [DecidableEq ğ”¸] :
    PreservesLimitsOfShape (Discrete PEmpty) (nominalInclusion.{v} ğ”¸) :=
  âŸ¨Î» {K} â†¦ preservesLimit_of_preserves_limit_cone
    (Nominal.emptyCone_isLimit K) (Nominal.nominalInclusion_emptyCone_isLimit K)âŸ©

instance nominalInclusion_preservesFiniteProducts.{u, v} (ğ”¸ : Type u) [DecidableEq ğ”¸] :
    PreservesFiniteProducts.{v} (nominalInclusion ğ”¸) :=
  âŸ¨Î» _ â†¦ preservesShape_fin_of_preserves_binary_and_terminal _ _âŸ©

def Nominal.equaliserCone.{v} [Infinite ğ”¸] (K : WalkingParallelPair â¥¤ Bundled.{v} (Nominal ğ”¸)) :
    Cone K where
  pt := âŸ¨Equaliser (K.map .left) (K.map .right) (K.map .left).prop (K.map .right).prop,
    inferInstanceâŸ©
  Ï€ := {
    app j := match j with
      | .zero => âŸ¨Equaliser.val (hf := (K.map .left).prop) (hg := (K.map .right).prop),
          Equaliser.val_equivariantâŸ©
      | .one => âŸ¨K.map .left âˆ˜ Equaliser.val
            (hf := (K.map .left).prop) (hg := (K.map .right).prop),
          (K.map .left).prop.comp Equaliser.val_equivariantâŸ©
    naturality j k h := by
      ext x
      cases h
      case left => rfl
      case right =>
        simp only [ConcreteCategory.hom, id_eq, const_obj_obj, const_obj_map, Category.id_comp,
          Function.comp_apply]
        exact Subtype.prop x
      case id =>
        simp only [ConcreteCategory.hom, id_eq, const_obj_obj, walkingParallelPairHom_id,
          CategoryTheory.Functor.map_id, Category.id_comp, const_obj_map, Category.comp_id]
  }

def Nominal.equaliserCone_isLimit.{v} [Infinite ğ”¸] (K : WalkingParallelPair â¥¤ Bundled.{v} (Nominal ğ”¸)) :
    IsLimit (equaliserCone K) where
  lift s := âŸ¨Equaliser.factor
    (K.map .left) (K.map .right) (K.map .left).prop (K.map .right).prop
    (s.Ï€.app _)
    Î» x â†¦ (congr_arg (Â· x) (s.Ï€.naturality .left)).symm.trans
      (congr_arg (Â· x) (s.Ï€.naturality .right)),
    Equaliser.factor_equivariant (s.Ï€.app .zero).propâŸ©
  fac s j := by
    ext x
    cases j
    case zero => rfl
    case one => exact (congr_arg (Â· x) (s.Ï€.naturality .left)).symm
  uniq s m h := by
    ext x
    have := congr_arg (Â· x) (h .zero)
    exact Subtype.coe_injective this

def Nominal.nominalInclusion_equaliserCone_isLimit.{v} [Infinite ğ”¸] (K : WalkingParallelPair â¥¤ Bundled.{v} (Nominal ğ”¸)) :
    IsLimit ((nominalInclusion ğ”¸).mapCone (equaliserCone K)) where
  lift s := âŸ¨Equaliser.factor
    (K.map .left) (K.map .right) (K.map .left).prop (K.map .right).prop
    (s.Ï€.app _)
    Î» x â†¦ (congr_arg (Â· x) (s.Ï€.naturality .left)).symm.trans
      (congr_arg (Â· x) (s.Ï€.naturality .right)),
    Equaliser.factor_equivariant (s.Ï€.app .zero).propâŸ©
  fac s j := by
    ext x
    cases j
    case zero => rfl
    case one => exact (congr_arg (Â· x) (s.Ï€.naturality .left)).symm
  uniq s m h := by
    ext x
    have := congr_arg (Â· x) (h .zero)
    exact Subtype.coe_injective this

instance nominalInclusion_preservesEqualisers.{u, v} (ğ”¸ : Type u) [DecidableEq ğ”¸] [Infinite ğ”¸] :
    PreservesLimitsOfShape WalkingParallelPair (nominalInclusion.{v} ğ”¸) :=
  âŸ¨Î» {K} â†¦ preservesLimit_of_preserves_limit_cone
    (Nominal.equaliserCone_isLimit K) (Nominal.nominalInclusion_equaliserCone_isLimit K)âŸ©

instance nominalInclusion_preservesFiniteLimits.{u, v} (ğ”¸ : Type u) [DecidableEq ğ”¸] [Infinite ğ”¸] :
    PreservesFiniteLimits.{v} (nominalInclusion ğ”¸) :=
  preservesFiniteLimits_of_preservesEqualizers_and_finiteProducts _
