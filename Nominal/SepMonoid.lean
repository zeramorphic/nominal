import Nominal.Abstraction
import Nominal.SepProd

open Finperm Rel SepProd

/-!
# Separated monoids

We define *separated monoids*, which are monoid objects internal to the monoidal category where

* the objects are nominal sets;
* the morphisms are strong equivariant maps (which are support-preserving functions);
* the monoid is separating conjunction.

Examples of separated monoids include the unit and empty types, finite sets of names,
and finite lists of names.

Our key theorem is that if `Î±` is a separated monoid, then the functor `[Î±|ğ”¸](-)` becomes a monad.
-/

@[ext]
class SepMul (ğ”¸ : Type*) [DecidableEq ğ”¸] (Î± : Type*) [MulPerm ğ”¸ Î±] where
  sepMul : Î± âˆ—[ğ”¸] Î± â†’ Î±
  sepMul_equivariant : Equivariant ğ”¸ sepMul
  sepMul_strong : StrongMap ğ”¸ sepMul

export SepMul (sepMul)

/-- The morphism `Unit â†’ Î±` given by `sepUnit` is automatically strong. -/
@[ext]
class SepUnit (ğ”¸ : Type*) [DecidableEq ğ”¸] (Î± : Type*) [HasPerm ğ”¸ Î±] where
  sepUnit : Î±
  sepUnit_equivariant : Equivariant ğ”¸ sepUnit

export SepUnit (sepUnit)

/-- A typeclass for *separated monoids*, which are the internal monoids to the monoidal category
`(Nom, âˆ—[ğ”¸])`. -/
class SepMonoid (ğ”¸ : Type*) [DecidableEq ğ”¸] [Infinite ğ”¸] (Î± : Type*) [Nominal ğ”¸ Î±]
    extends SepMul ğ”¸ Î±, SepUnit ğ”¸ Î± where
  sepUnit_sepMul (x : Î±) : sepMul (leftInj sepUnit sepUnit_equivariant x) = x
  sepMul_sepUnit (x : Î±) : sepMul (rightInj sepUnit sepUnit_equivariant x) = x
  sepMul_assoc (x : (Î± âˆ—[ğ”¸] Î±) âˆ—[ğ”¸] Î±) :
    sepMul (x.first sepMul sepMul_equivariant) =
    sepMul ((assoc Î± Î± Î± x).second sepMul sepMul_equivariant)

instance {ğ”¸ : Type*} [DecidableEq ğ”¸] [Infinite ğ”¸] {Î± : Type*} [DecidableEq Î±] [Nominal ğ”¸ Î±] :
    SepMonoid ğ”¸ (Finset Î±) where
  sepMul x := x.fst âˆª x.snd
  sepMul_equivariant := by
    intro Ï€
    ext x a
    simp only [Function.perm_def, perm_fst, perm_snd, Finset.mem_perm, Finset.mem_union,
      _root_.inv_inv, perm_inv_perm]
  sepMul_strong := by
    apply strongMap_of_supp_eq_supp
    intro x
    ext a
    simp only [Finset.supp_eq, supp_eq]
    aesop
  sepUnit := âˆ…
  sepUnit_equivariant := by intro; rfl
  sepUnit_sepMul x := by simp only [leftInj, Finset.empty_union]
  sepMul_sepUnit x := by simp only [rightInj, leftInj, symm_fst, symm_snd, Finset.union_empty]
  sepMul_assoc x := by simp only [first_fst, first_snd, Finset.union_assoc, assoc, Equiv.coe_fn_mk,
    assoc', second_fst, second_snd]

/-!
# Generalised abstraction monad

If `Î±` is a separated monoid, then the functor `[Î±|ğ”¸](-)` becomes a monad.
-/

namespace Abstract

variable {ğ”¸ Î± Î² Î³ : Type*} [DecidableEq ğ”¸] [Infinite ğ”¸]
  [Nominal ğ”¸ Î±] [Nominal ğ”¸ Î²] [Nominal ğ”¸ Î³]

theorem sepMap_aux (xâ‚ xâ‚‚ : Î± âˆ—[ğ”¸] Î²) (zâ‚ zâ‚‚ : Î³)
    (h : (âŸ¨xâ‚, zâ‚âŸ© : Abstract' ğ”¸ (Î± âˆ—[ğ”¸] Î²) Î³).Rel âŸ¨xâ‚‚, zâ‚‚âŸ©) :
    (âŸ¨xâ‚.fstâŸ©(âŸ¨xâ‚.sndâŸ©zâ‚) : [Î±|ğ”¸][Î²|ğ”¸]Î³) = âŸ¨xâ‚‚.fstâŸ©(âŸ¨xâ‚‚.sndâŸ©zâ‚‚) := by
  obtain âŸ¨Ï€, hÏ€â‚, hÏ€â‚‚âŸ© := h
  cases hÏ€â‚
  rw [mk_eq_iff]
  refine âŸ¨Ï€, rfl, ?_âŸ©
  rw [Finperm.fresh_iff] at hÏ€â‚‚ âŠ¢
  intro a ha
  apply hÏ€â‚‚
  simp only [Abstract.supp_eq, Finset.names_supp_eq, Finset.mem_sdiff, supp_eq, Finset.mem_union,
    not_or] at ha âŠ¢
  tauto

def sepMap : [Î± âˆ—[ğ”¸] Î² | ğ”¸]Î³ â†’ [Î±|ğ”¸][Î²|ğ”¸]Î³ :=
  lift (Î» x z â†¦ âŸ¨x.fstâŸ©(âŸ¨x.sndâŸ©z)) sepMap_aux

theorem sepMap_injective : Function.Injective (sepMap : [Î± âˆ—[ğ”¸] Î² | ğ”¸]Î³ â†’ [Î±|ğ”¸][Î²|ğ”¸]Î³) := by
  intro xâ‚ xâ‚‚ h
  induction xâ‚ using ind
  case mk xâ‚ yâ‚ =>
  induction xâ‚‚ using ind
  case mk xâ‚‚ yâ‚‚ =>
  rw [sepMap, lift_mk, lift_mk, mk_eq_iff, Abstract'.rel_iff] at h
  obtain âŸ¨Ï€, hÏ€â‚, hÏ€â‚‚, hÏ€â‚ƒâŸ© := h
  rw [Abstract'.perm_def, perm_mk, Abstract'.mk.injEq, mk_eq_iff, Abstract'.rel_iff] at hÏ€â‚
  obtain âŸ¨hÏ€â‚, Ï€', hÏ€'â‚, hÏ€'â‚‚, hÏ€'â‚ƒâŸ© := hÏ€â‚
  simp only [Abstract'.perm_def, Abstract'.mk.injEq] at hÏ€'â‚
  simp only [Abstract.supp_eq, supp_perm_eq] at hÏ€â‚‚ hÏ€'â‚‚
  rw [mk_eq_iff]
  refine âŸ¨Ï€' * Ï€, ?_, ?_âŸ©
  Â· simp only [Abstract'.perm_def, Abstract'.mk.injEq, mul_perm]
    refine âŸ¨?_, hÏ€'â‚.2âŸ©
    ext
    Â· rw [perm_fst, perm_fst, perm_eq_of_fresh, hÏ€â‚]
      rw [Finperm.fresh_iff]
      intro a ha
      by_contra ha'
      rw [â† ne_eq, â† Finperm.mem_support_iff] at ha'
      specialize hÏ€'â‚ƒ ha'
      simp only [supp_perm_eq, Finset.mem_union] at hÏ€'â‚ƒ
      obtain h | h := hÏ€'â‚ƒ
      Â· rw [supp_perm_eq, Finset.mem_perm] at ha
        rw [Finset.mem_perm] at h
        have := xâ‚.sep
        rw [fresh_def, Finset.disjoint_iff_ne] at this
        exact this _ ha _ h rfl
      Â· dsimp only at hÏ€â‚
        rw [hÏ€â‚] at ha
        have := xâ‚‚.sep
        rw [fresh_def, Finset.disjoint_iff_ne] at this
        exact this _ ha _ h rfl
    Â· exact hÏ€'â‚.1
  Â· rw [fresh_iff] at hÏ€â‚‚ hÏ€'â‚‚ âŠ¢
    intro a ha
    simp only [supp_eq, Finset.names_supp_eq, Finset.mem_sdiff, Finset.mem_union, not_or] at ha
    have hÏ€a : Ï€ a = a := by
      apply hÏ€â‚‚
      simp only [Finset.names_supp_eq, Finset.mem_sdiff]
      tauto
    have hÏ€a' : Ï€' a = a := by
      apply hÏ€'â‚‚
      symm at hÏ€a
      rw [â† inv_apply_eq_iff_eq] at hÏ€a
      simp only [Finset.names_supp_eq, Finset.mem_sdiff, Finset.mem_perm, perm_name_eq, hÏ€a]
      tauto
    rw [mul_apply, hÏ€a, hÏ€a']

theorem sepMap_surjective : Function.Surjective (sepMap : [Î± âˆ—[ğ”¸] Î² | ğ”¸]Î³ â†’ [Î±|ğ”¸][Î²|ğ”¸]Î³) := by
  intro x
  induction x using ind
  case mk x y =>
  induction y using fresh_induction x
  infer_instance
  case h y z h =>
  use âŸ¨âŸ¨x, y, h.symmâŸ©âŸ©z
  rfl

/-- The equivariant equivalence between `[Î± âˆ—[ğ”¸] Î² | ğ”¸]Î³` and `[Î±|ğ”¸][Î²|ğ”¸]Î³`. -/
noncomputable def sepEquiv : [Î± âˆ—[ğ”¸] Î² | ğ”¸]Î³ â‰ƒ [Î±|ğ”¸][Î²|ğ”¸]Î³ :=
  Equiv.ofBijective sepMap âŸ¨sepMap_injective, sepMap_surjectiveâŸ©

theorem sepEquiv_mk_symm (x : Î±) (y : Î²) (z : Î³) (h : x #[ğ”¸] y) :
    sepEquiv.symm (âŸ¨xâŸ©(âŸ¨yâŸ©z)) = âŸ¨âŸ¨x, y, hâŸ©âŸ©z := by
  rw [Equiv.symm_apply_eq]
  rfl

/-!
We now define the monad structure on `[Î±|ğ”¸]Î²` under the assumption that `Î±` has a
separated monoid structure.
-/

def pure [SepMonoid ğ”¸ Î±] (x : Î²) : [Î±|ğ”¸]Î² :=
  âŸ¨sepUnit ğ”¸âŸ©x

theorem pure_equivariant [SepMonoid ğ”¸ Î±] : Equivariant ğ”¸ (pure : Î² â†’ [Î±|ğ”¸]Î²) := by
  intro Ï€
  ext x
  simp only [Function.perm_def, pure, perm_mk, perm_inv_perm]
  rw [SepUnit.sepUnit_equivariant]

/-- The monadic multiplication. -/
noncomputable def mul [SepMonoid ğ”¸ Î±] : [Î±|ğ”¸][Î±|ğ”¸]Î² â†’ [Î±|ğ”¸]Î² :=
  Abstract.amap sepMul SepMul.sepMul_equivariant SepMul.sepMul_strong âˆ˜ sepEquiv.symm

theorem mul_mk_eq [SepMonoid ğ”¸ Î±] (x y : Î±) (h : y #[ğ”¸] x) (z : Î²) :
    (mul (âŸ¨xâŸ©(âŸ¨yâŸ©z)) : [Î±|ğ”¸]Î²) = âŸ¨sepMul âŸ¨x, y, h.commâŸ©âŸ©z := by
  rw [mul, Function.comp_apply, sepEquiv_mk_symm]
  rfl

theorem mul_equivariant [SepMonoid ğ”¸ Î±] : Equivariant ğ”¸ (mul : [Î±|ğ”¸][Î±|ğ”¸]Î² â†’ [Î±|ğ”¸]Î²) := by
  intro Ï€
  ext x
  induction x using ind
  case mk x y =>
  induction y using fresh_induction x
  infer_instance
  case h y z h =>
  rw [Function.perm_def, perm_mk, perm_mk, mul_mk_eq x y h, mul_mk_eq _ _ (h.perm Ï€â»Â¹)]
  simp only [perm_mk, apply_perm_eq SepMul.sepMul_equivariant, SepProd.perm_def, perm_inv_perm]

/-! We now prove the monad laws. -/

theorem left_unit [SepMonoid ğ”¸ Î±] (x : [Î±|ğ”¸]Î²) :
    mul (pure x) = x := by
  induction x using ind
  case mk x y =>
  rw [pure, mul_mk_eq]
  Â· have := SepMonoid.sepUnit_sepMul (ğ”¸ := ğ”¸) x
    rw [leftInj] at this
    rw [this]
  Â· rw [fresh_def, SepUnit.sepUnit_equivariant.supp_eq_empty]
    exact Î» _ _ â†¦ id

theorem right_unit [SepMonoid ğ”¸ Î±] (x : [Î±|ğ”¸]Î²) :
    mul (x.map pure pure_equivariant.finitelySupported) = x := by
  induction x using ind
  case mk x y =>
  rw [map_mk]
  Â· rw [pure, mul_mk_eq]
    Â· congr 1
      exact SepMonoid.sepMul_sepUnit (ğ”¸ := ğ”¸) x
    Â· rw [fresh_def, SepUnit.sepUnit_equivariant.supp_eq_empty]
      exact Î» _ a _ â†¦ a
  Â· rw [fresh_def, (pure_equivariant (ğ”¸ := ğ”¸) (Î± := Î±) (Î² := Î²)).supp_eq_empty]
    exact Î» _ _ â†¦ id

theorem assoc [SepMonoid ğ”¸ Î±] (x : [Î±|ğ”¸][Î±|ğ”¸][Î±|ğ”¸]Î²) :
    mul (x.map mul mul_equivariant.finitelySupported) = mul (mul x) := by
  induction x using ind
  case mk xâ‚ y =>
  induction y using fresh_induction xâ‚
  infer_instance
  case h xâ‚‚ y hâ‚ =>
  induction y using fresh_induction (xâ‚, xâ‚‚)
  infer_instance
  case h xâ‚ƒ y hâ‚‚ =>
  simp only [Prod.fresh_iff] at hâ‚‚
  rw [map_mk, mul_mk_eq _ _ hâ‚‚.2, mul_mk_eq, mul_mk_eq _ _ hâ‚, mul_mk_eq]
  Â· have := SepMonoid.sepMul_assoc (ğ”¸ := ğ”¸) âŸ¨âŸ¨xâ‚, xâ‚‚, hâ‚.commâŸ©, xâ‚ƒ, ?_âŸ©
    swap
    Â· rw [fresh_def, SepProd.supp_eq, Finset.disjoint_union_left, â† fresh_def, â† fresh_def]
      exact âŸ¨hâ‚‚.1.comm, hâ‚‚.2.commâŸ©
    congr 1
    exact this.symm
  Â· rw [fresh_def, SepMul.sepMul_strong.supp_eq SepMul.sepMul_equivariant,
      supp_eq, Finset.disjoint_union_right, â† fresh_def, â† fresh_def]
    exact hâ‚‚
  Â· rw [fresh_def, SepMul.sepMul_strong.supp_eq SepMul.sepMul_equivariant,
      supp_eq, Finset.disjoint_union_left, â† fresh_def, â† fresh_def]
    exact âŸ¨hâ‚, hâ‚‚.1âŸ©
  Â· rw [fresh_def, mul_equivariant.supp_eq_empty]
    exact Î» _ _ â†¦ id

end Abstract
