import Mathlib.GroupTheory.GroupAction.Support
import Mathlib.Data.Fintype.Card
import Mathlib.Data.Set.Pointwise.SMul
import Mathlib.Data.Set.Finite
import Nominal.Finperm

/-!
We're working from <https://people.cs.nott.ac.uk/pszvc/mgs/MGS2011_nominal_sets.pdf>.
-/

variable {ğ”¸ : Type*} [DecidableEq ğ”¸]

open MulAction Finperm

instance : MulAction (Finperm ğ”¸) ğ”¸ where
  smul Ï€ a := Ï€ a
  one_smul := Finperm.one_apply
  mul_smul := Finperm.mul_apply

@[simp]
theorem Finperm.smul_name_eq (Ï€ : Finperm ğ”¸) (a : ğ”¸) :
    Ï€ â€¢ a = Ï€ a :=
  rfl

theorem Finperm.supports_iff' (ğ”¸ : Type*) [DecidableEq ğ”¸] {Î± : Type*} [MulAction (Finperm ğ”¸) Î±]
    (s : Finset ğ”¸) (x : Î±) :
    Supports (Finperm ğ”¸) (s : Set ğ”¸) x â†” âˆ€ a b, a âˆ‰ s â†’ b âˆ‰ s â†’ swap a b â€¢ x = x := by
  constructor
  Â· intro hs a b ha hb
    apply hs (swap a b)
    intro c hc
    apply swap_apply_of_ne_of_ne <;>
    Â· rintro rfl
      contradiction
  Â· intro h Ï€ hÏ€
    induction Ï€ using swap_induction_right
    case one => rw [one_smul]
    case swap Ï€ a b ha hab ih =>
      rw [mem_support_iff, not_ne_iff] at ha
      rw [mul_smul]
      by_cases ha' : a âˆˆ s
      Â· have := hÏ€ ha'
        rw [mul_smul] at this
        change Ï€ (swap a b a) = a at this
        rw [swap_apply_left] at this
        have := ha.trans this.symm
        rw [EmbeddingLike.apply_eq_iff_eq] at this
        contradiction
      by_cases hb' : b âˆˆ s
      Â· have := hÏ€ hb'
        rw [mul_smul] at this
        change Ï€ (swap a b b) = b at this
        rw [swap_apply_right] at this
        have := ha.symm.trans this
        contradiction
      rw [h a b ha' hb']
      apply ih
      intro c hc
      have := hÏ€ hc
      rwa [smul_name_eq, mul_apply, swap_apply_of_ne_of_ne] at this <;>
      Â· rintro rfl; contradiction

theorem Finperm.supports_iff (ğ”¸ : Type*) [DecidableEq ğ”¸] {Î± : Type*} [MulAction (Finperm ğ”¸) Î±]
    (s : Finset ğ”¸) (x : Î±) :
    Supports (Finperm ğ”¸) (s : Set ğ”¸) x â†” âˆ€ a b, a âˆ‰ s â†’ b âˆ‰ s â†’ a â‰  b â†’ swap a b â€¢ x = x := by
  rw [supports_iff']
  constructor
  Â· tauto
  Â· intro h a b ha hb
    by_cases hab : a = b
    Â· cases hab
      rw [swap_self, one_smul]
    Â· exact h a b ha hb hab

theorem Finperm.inter_supports [Infinite ğ”¸] {Î± : Type*} [MulAction (Finperm ğ”¸) Î±]
    (s t : Finset ğ”¸) (x : Î±)
    (hs : Supports (Finperm ğ”¸) (s : Set ğ”¸) x) (ht : Supports (Finperm ğ”¸) (t : Set ğ”¸) x) :
    Supports (Finperm ğ”¸) ((s âˆ© t : Finset ğ”¸) : Set ğ”¸) x := by
  rw [supports_iff'] at hs ht
  rw [supports_iff]
  intro a b ha hb hab
  obtain âŸ¨c, hcâŸ© := Infinite.exists_not_mem_finset (s âˆª t âˆª {a, b})
  simp at hc
  rw [swap_triple a b c hab (by tauto) (by tauto), mul_smul, mul_smul]
  rw [Finset.mem_inter, not_and] at ha hb
  have : swap a c â€¢ x = x := by
    by_cases ha' : a âˆˆ s
    Â· exact ht a c (ha ha') (by tauto)
    Â· exact hs a c ha' (by tauto)
  have : swap b c â€¢ x = x := by
    by_cases hb' : b âˆˆ s
    Â· exact ht b c (hb hb') (by tauto)
    Â· exact hs b c hb' (by tauto)
  cc

def FinitelySupported (ğ”¸ : Type*) [DecidableEq ğ”¸] {Î± : Type*} [MulAction (Finperm ğ”¸) Î±]
    (x : Î±) : Prop :=
  âˆƒ s : Finset ğ”¸, Supports (Finperm ğ”¸) (s : Set ğ”¸) x

/-- The minimal support of an object, if it exists. -/
noncomputable def supp (ğ”¸ : Type*) [DecidableEq ğ”¸] {Î± : Type*} [MulAction (Finperm ğ”¸) Î±] (x : Î±) :
    Finset ğ”¸ :=
  open scoped Classical in
  if hx : FinitelySupported ğ”¸ x then
    hx.choose.filter (Î» a â†¦ âˆ€ s : Finset ğ”¸, Supports (Finperm ğ”¸) (s : Set ğ”¸) x â†’ a âˆˆ s)
  else
    âˆ…

theorem supp_eq_of_not_finitelySupported {Î± : Type*} [MulAction (Finperm ğ”¸) Î±]
    (x : Î±) (hx : Â¬FinitelySupported ğ”¸ x) :
    supp ğ”¸ x = âˆ… := by
  rw [supp, dif_neg hx]

theorem mem_supp_iff' {Î± : Type*} [MulAction (Finperm ğ”¸) Î±]
    (x : Î±) (hx : FinitelySupported ğ”¸ x) (a : ğ”¸) :
    a âˆˆ supp ğ”¸ x â†” âˆ€ s : Finset ğ”¸, Supports (Finperm ğ”¸) (s : Set ğ”¸) x â†’ a âˆˆ s := by
  classical
  rw [supp, dif_pos hx, Finset.mem_filter, and_iff_right_iff_imp]
  intro ha
  exact ha hx.choose hx.choose_spec

theorem supp_supports' [Infinite ğ”¸] {Î± : Type*} [MulAction (Finperm ğ”¸) Î±]
    (x : Î±) (hx : FinitelySupported ğ”¸ x) :
    Supports (Finperm ğ”¸) ((supp ğ”¸ x) : Set ğ”¸) x := by
  intro Ï€ hÏ€
  obtain âŸ¨s, hsâŸ© := hx
  induction s using Finset.strongInduction
  case H s ih =>
    by_cases ht : âˆƒ t âŠ‚ s, Supports (Finperm ğ”¸) (t : Set ğ”¸) x
    Â· obtain âŸ¨t, htâ‚, htâ‚‚âŸ© := ht
      exact ih t htâ‚ htâ‚‚
    simp only [not_exists, not_and] at ht
    suffices s = supp ğ”¸ x by
      cases this
      exact hs Ï€ hÏ€
    ext a
    rw [mem_supp_iff' x âŸ¨s, hsâŸ©]
    constructor; swap; tauto
    intro ha u hu
    by_contra ha'
    refine ht _ ?_ (inter_supports s u x hs hu)
    rw [Finset.ssubset_iff]
    refine âŸ¨a, ?_, ?_âŸ©
    Â· simp only [Finset.mem_inter, not_and]
      exact Î» _ â†¦ ha'
    Â· intro b
      aesop

theorem supp_minimal {Î± : Type*} [MulAction (Finperm ğ”¸) Î±]
    (x : Î±) (s : Finset ğ”¸) (hs : Supports (Finperm ğ”¸) (s : Set ğ”¸) x) :
    supp ğ”¸ x âŠ† s := by
  intro a ha
  rw [mem_supp_iff' x âŸ¨s, hsâŸ©] at ha
  exact ha s hs

class Nominal (ğ”¸ : Type*) [DecidableEq ğ”¸] (Î± : Type*)
    extends MulAction (Finperm ğ”¸) Î± where
  supported : âˆ€ x : Î±, FinitelySupported ğ”¸ x

namespace Nominal

instance : Nominal ğ”¸ ğ”¸ where
  supported := Î» a â†¦ âŸ¨{a}, Î» _ hÏ€ â†¦ hÏ€ (Finset.mem_singleton_self a)âŸ©

theorem mem_supp_iff {Î± : Type*} [Nominal ğ”¸ Î±]
    (x : Î±) (a : ğ”¸) :
    a âˆˆ supp ğ”¸ x â†” âˆ€ s : Finset ğ”¸, Supports (Finperm ğ”¸) (s : Set ğ”¸) x â†’ a âˆˆ s :=
  mem_supp_iff' x (Nominal.supported x) a

theorem supp_supports (ğ”¸ : Type*) [DecidableEq ğ”¸] [Infinite ğ”¸] {Î± : Type*} [Nominal ğ”¸ Î±] (x : Î±) :
    Supports (Finperm ğ”¸) ((supp ğ”¸ x) : Set ğ”¸) x :=
  supp_supports' x (Nominal.supported x)

@[simp]
theorem name_supp_eq [Infinite ğ”¸] (a : ğ”¸) :
    supp ğ”¸ a = {a} := by
  ext b
  rw [mem_supp_iff]
  constructor
  Â· intro h
    exact h {a} (Î» _ hÏ€ â†¦ hÏ€ (Finset.mem_singleton_self a))
  Â· intro h
    rw [Finset.mem_singleton] at h
    cases h
    intro s hs
    by_contra ha
    obtain âŸ¨b, hbâŸ© := Infinite.exists_not_mem_finset (s âˆª {a})
    rw [Finset.mem_union, Finset.mem_singleton, not_or] at hb
    have := hs (swap a b) ?_
    Â· rw [smul_name_eq, swap_apply_left] at this
      tauto
    Â· intro c hc
      apply swap_apply_of_ne_of_ne <;> aesop

end Nominal

instance {G Î± : Type*} [Group G] [MulAction G Î±] : SMul G (Finset Î±) where
  smul g s := s.map âŸ¨(g â€¢ Â·), MulAction.injective gâŸ©

theorem Finset.smul_def {G Î± : Type*} [Group G] [MulAction G Î±]
    (g : G) (s : Finset Î±) :
    g â€¢ s = s.map âŸ¨(g â€¢ Â·), MulAction.injective gâŸ© :=
  rfl

theorem Finset.mem_smul_iff {G Î± : Type*} [Group G] [MulAction G Î±]
    (g : G) (x : Î±) (s : Finset Î±) :
    x âˆˆ g â€¢ s â†” gâ»Â¹ â€¢ x âˆˆ s := by
  rw [Finset.smul_def]
  aesop

instance {Î± : Type*} [MulAction (Finperm ğ”¸) Î±] : MulAction (Finperm ğ”¸) (Finset Î±) where
  smul Ï€ s := s.map âŸ¨(Ï€ â€¢ Â·), MulAction.injective Ï€âŸ©
  one_smul _ := by
    ext
    simp [Finset.mem_smul_iff]
  mul_smul _ _ _ := by
    ext
    simp [Finset.mem_smul_iff, mul_smul]

theorem Finset.smul_eq_of_smul_eq {Î± Î² : Type*} [Group Î²] [MulAction Î² Î±]
    (s : Finset Î±) (b : Î²) (h : âˆ€ a âˆˆ s, b â€¢ a = a) :
    b â€¢ s = s := by
  ext a
  rw [Finset.mem_smul_iff]
  constructor
  Â· intro ha
    have := h _ ha
    rw [smul_inv_smul] at this
    rwa [â† this] at ha
  Â· intro ha
    have := h _ ha
    rw [smul_eq_iff_eq_inv_smul] at this
    rwa [this] at ha

theorem Finset.finitelySupported [Infinite ğ”¸] {Î± : Type*} [Nominal ğ”¸ Î±] (s : Finset Î±) :
    FinitelySupported ğ”¸ s := by
  use s.biUnion (supp ğ”¸)
  intro Ï€ hÏ€
  apply Finset.smul_eq_of_smul_eq
  intro x hx
  apply Nominal.supp_supports ğ”¸ x
  aesop

instance [Infinite ğ”¸] {Î± : Type*} [Nominal ğ”¸ Î±] : Nominal ğ”¸ (Finset Î±) where
  supported := Finset.finitelySupported

-- TODO: The version in mathlib isn't general enough!
theorem MulAction.Supports.smul' {Î± : Type*} [MulAction (Finperm ğ”¸) Î±]
    {s : Finset ğ”¸} {x : Î±}
    (h : Supports (Finperm ğ”¸) (s : Set ğ”¸) x) (Ï€ : Finperm ğ”¸) :
    Supports (Finperm ğ”¸) ((Ï€ â€¢ s : Finset ğ”¸) : Set ğ”¸) (Ï€ â€¢ x) := by
  intro Ï€' hÏ€'
  have := h (Ï€â»Â¹ * Ï€' * Ï€) ?_
  Â· rwa [mul_smul, mul_smul, inv_smul_eq_iff] at this
  intro a ha
  rw [mul_smul, mul_smul, inv_smul_eq_iff]
  apply hÏ€'
  rwa [smul_name_eq, Finset.mem_coe, Finset.mem_smul_iff, smul_name_eq, inv_apply_self]

theorem MulAction.Supports.of_smul' {Î± : Type*} [MulAction (Finperm ğ”¸) Î±]
    {s : Finset ğ”¸} {x : Î±} {Ï€ : Finperm ğ”¸}
    (h : Supports (Finperm ğ”¸) (s : Set ğ”¸) (Ï€ â€¢ x)) :
    Supports (Finperm ğ”¸) ((Ï€â»Â¹ â€¢ s : Finset ğ”¸) : Set ğ”¸) x := by
  have := h.smul' Ï€â»Â¹
  rwa [inv_smul_smul] at this

theorem FinitelySupported.smul {Î± : Type*} [MulAction (Finperm ğ”¸) Î±] {x : Î±}
    (h : FinitelySupported ğ”¸ x) (Ï€ : Finperm ğ”¸) :
    FinitelySupported ğ”¸ (Ï€ â€¢ x) := by
  obtain âŸ¨s, hsâŸ© := h
  refine âŸ¨Ï€ â€¢ s, hs.smul' Ï€âŸ©

theorem FinitelySupported.of_smul {Î± : Type*} [MulAction (Finperm ğ”¸) Î±] {x : Î±}
    {Ï€ : Finperm ğ”¸} (h : FinitelySupported ğ”¸ (Ï€ â€¢ x)):
    FinitelySupported ğ”¸ x := by
  have := h.smul Ï€â»Â¹
  rwa [inv_smul_smul] at this

def MulAction.StrongSupports (G : Type*) {Î± Î² : Type*} [Group G] [SMul G Î±] [SMul G Î²] (s : Set Î±) (b : Î²) :=
  âˆ€ g : G, (âˆ€ â¦ƒaâ¦„, a âˆˆ s â†’ g â€¢ a = a) â†” g â€¢ b = b

theorem MulAction.StrongSupports.supports {G Î± Î² : Type*} [Group G] [SMul G Î±] [SMul G Î²]
    {s : Set Î±} {b : Î²} (h : StrongSupports G s b) : Supports G s b := by
  intro Ï€ h'
  rwa [â† h]

theorem subset_of_strongSupports [Infinite ğ”¸] {s t : Finset ğ”¸} {Î± : Type*} [MulAction (Finperm ğ”¸) Î±] {x : Î±}
    (hs : StrongSupports (Finperm ğ”¸) (s : Set ğ”¸) x)
    (ht : Supports (Finperm ğ”¸) (t : Set ğ”¸) x) :
    s âŠ† t := by
  intro a ha
  by_contra ha'
  obtain âŸ¨b, hbâŸ© := Infinite.exists_not_mem_finset (t âˆª {a})
  rw [StrongSupports] at hs
  have := ht (swap a b) ?_
  Â· have := (hs (swap a b)).mpr this ha
    aesop
  Â· intro c hc
    rw [smul_name_eq, swap_apply_of_ne_of_ne] <;> aesop

theorem supp_eq_of_strongSupports [Infinite ğ”¸] {Î± : Type*} [MulAction (Finperm ğ”¸) Î±]
    (x : Î±) (s : Finset ğ”¸) (hs : StrongSupports (Finperm ğ”¸) (s : Set ğ”¸) x) :
    supp ğ”¸ x = s := by
  apply subset_antisymm
  Â· apply supp_minimal x s hs.supports
  intro a ha
  rw [mem_supp_iff' x âŸ¨s, hs.supportsâŸ©]
  intro t ht
  exact subset_of_strongSupports hs ht ha

theorem Nominal.mem_supp_iff_names_infinite [Infinite ğ”¸] {Î± : Type*} [Nominal ğ”¸ Î±] (x : Î±) (a : ğ”¸) :
    a âˆˆ supp ğ”¸ x â†” {b | swap a b â€¢ x â‰  x}.Infinite := by
  constructor
  Â· intro h
    by_contra h'
    rw [Set.not_infinite] at h'
    obtain âŸ¨t, htâŸ© := h'.exists_finset
    clear h'
    rw [mem_supp_iff] at h
    have := h t ?_
    Â· rw [ht] at this
      simp at this
    Â· rw [supports_iff]
      intro b c hb hc hbc
      rw [ht, Set.mem_setOf_eq, not_not] at hb hc
      by_cases hab : a = b
      Â· subst hab
        exact hc
      by_cases hac : c = a
      Â· subst hac
        rw [swap_comm, hb]
      Â· rw [swap_triple b c a hbc hac hab, mul_smul, mul_smul, swap_comm b, swap_comm c, hb, hc, hb]
  Â· intro h
    contrapose h
    rw [Set.not_infinite]
    apply (supp ğ”¸ x âˆª {a}).finite_toSet.subset
    intro b hb
    by_contra hb'
    have := supp_supports ğ”¸ x
    rw [supports_iff] at this
    exact hb (this a b h (by aesop) (by aesop))

theorem Nominal.swap_smul_eq_of_swap_smul_eq [Infinite ğ”¸] {Î± : Type*} [Nominal ğ”¸ Î±]
    (x : Î±) (a b c : ğ”¸) (hab : a â‰  b) (hbc : b â‰  c) (hca : c â‰  a) :
    swap a b â€¢ x = swap a c â€¢ x â†’ swap a b â€¢ x = swap b c â€¢ x := by
  have := swap_triple b c a hbc hca hab
  rw [swap_comm b a, swap_comm c a] at this
  rw [this, mul_smul, mul_smul, smul_left_cancel_iff, â† inv_smul_eq_iff, swap_inv]
  tauto

theorem Nominal.swap_smul_injOn [Infinite ğ”¸] {Î± : Type*} [Nominal ğ”¸ Î±] (x : Î±)
    (a : ğ”¸) (ha : a âˆˆ supp ğ”¸ x) :
    Set.InjOn (swap a Â· â€¢ x) ({b | swap a b â€¢ x â‰  x} \ supp ğ”¸ x) := by
  intro b âŸ¨hbâ‚, hbâ‚‚âŸ© c âŸ¨hcâ‚, hcâ‚‚âŸ© h
  by_contra hbc
  have h' := Nominal.swap_smul_eq_of_swap_smul_eq x a b c (by aesop) hbc (by aesop) h
  have := Nominal.supp_supports ğ”¸ x
  rw [supports_iff] at this
  rw [this b c hbâ‚‚ hcâ‚‚ hbc] at h'
  exact hbâ‚ h'

theorem Nominal.mem_supp_iff_range_infinite [Infinite ğ”¸] {Î± : Type*} [Nominal ğ”¸ Î±]
    (x : Î±) (a : ğ”¸) :
    a âˆˆ supp ğ”¸ x â†” (Set.range (swap a Â· â€¢ x)).Infinite := by
  constructor
  Â· intro ha
    apply Set.infinite_of_injOn_mapsTo (swap_smul_injOn x a ha)
    Â· intro b _
      use b
    Â· apply Set.Infinite.diff
      Â· rwa [mem_supp_iff_names_infinite] at ha
      Â· exact Finset.finite_toSet (supp ğ”¸ x)
  Â· intro ha
    rw [mem_supp_iff_names_infinite]
    contrapose ha
    rw [Set.not_infinite] at ha âŠ¢
    have := (ha.image (swap a Â· â€¢ x)).union (Set.finite_singleton x)
    apply this.subset
    rintro _ âŸ¨b, rflâŸ©
    by_cases swap a b â€¢ x = x <;> aesop
