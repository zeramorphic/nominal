import Mathlib.GroupTheory.GroupAction.Support
import Mathlib.Data.Fintype.Card
import Mathlib.Data.Set.Pointwise.SMul
import Mathlib.Data.Set.Finite
import Nominal.Finperm

/-!
We're working from <https://people.cs.nott.ac.uk/pszvc/mgs/MGS2011_nominal_sets.pdf>.
-/

variable {ğ”¸ : Type*} [DecidableEq ğ”¸]

open MulAction Finperm

instance : MulAction (Finperm ğ”¸) ğ”¸ where
  smul Ï€ a := Ï€ a
  one_smul := Finperm.one_apply
  mul_smul := Finperm.mul_apply

@[simp]
theorem Finperm.smul_name_eq (Ï€ : Finperm ğ”¸) (a : ğ”¸) :
    Ï€ â€¢ a = Ï€ a :=
  rfl

theorem Finperm.supports_iff' (ğ”¸ : Type*) [DecidableEq ğ”¸] {Î± : Type*} [MulAction (Finperm ğ”¸) Î±]
    (s : Finset ğ”¸) (x : Î±) :
    Supports (Finperm ğ”¸) (s : Set ğ”¸) x â†” âˆ€ a b, a âˆ‰ s â†’ b âˆ‰ s â†’ swap a b â€¢ x = x := by
  constructor
  Â· intro hs a b ha hb
    apply hs (swap a b)
    intro c hc
    apply swap_apply_of_ne_of_ne <;>
    Â· rintro rfl
      contradiction
  Â· intro h Ï€ hÏ€
    induction Ï€ using swap_induction_right
    case one => rw [one_smul]
    case swap Ï€ a b ha hab ih =>
      rw [mem_support_iff, not_ne_iff] at ha
      rw [mul_smul]
      by_cases ha' : a âˆˆ s
      Â· have := hÏ€ ha'
        rw [mul_smul] at this
        change Ï€ (swap a b a) = a at this
        rw [swap_apply_left] at this
        have := ha.trans this.symm
        rw [EmbeddingLike.apply_eq_iff_eq] at this
        contradiction
      by_cases hb' : b âˆˆ s
      Â· have := hÏ€ hb'
        rw [mul_smul] at this
        change Ï€ (swap a b b) = b at this
        rw [swap_apply_right] at this
        have := ha.symm.trans this
        contradiction
      rw [h a b ha' hb']
      apply ih
      intro c hc
      have := hÏ€ hc
      rwa [smul_name_eq, mul_apply, swap_apply_of_ne_of_ne] at this <;>
      Â· rintro rfl; contradiction

theorem Finperm.supports_iff (ğ”¸ : Type*) [DecidableEq ğ”¸] {Î± : Type*} [MulAction (Finperm ğ”¸) Î±]
    (s : Finset ğ”¸) (x : Î±) :
    Supports (Finperm ğ”¸) (s : Set ğ”¸) x â†” âˆ€ a b, a âˆ‰ s â†’ b âˆ‰ s â†’ a â‰  b â†’ swap a b â€¢ x = x := by
  rw [supports_iff']
  constructor
  Â· tauto
  Â· intro h a b ha hb
    by_cases hab : a = b
    Â· cases hab
      rw [swap_self, one_smul]
    Â· exact h a b ha hb hab

theorem Finperm.inter_supports [Infinite ğ”¸] {Î± : Type*} [MulAction (Finperm ğ”¸) Î±]
    (s t : Finset ğ”¸) (x : Î±)
    (hs : Supports (Finperm ğ”¸) (s : Set ğ”¸) x) (ht : Supports (Finperm ğ”¸) (t : Set ğ”¸) x) :
    Supports (Finperm ğ”¸) ((s âˆ© t : Finset ğ”¸) : Set ğ”¸) x := by
  rw [supports_iff'] at hs ht
  rw [supports_iff]
  intro a b ha hb hab
  obtain âŸ¨c, hcâŸ© := Infinite.exists_not_mem_finset (s âˆª t âˆª {a, b})
  simp at hc
  rw [swap_triple a b c hab (by tauto) (by tauto), mul_smul, mul_smul]
  rw [Finset.mem_inter, not_and] at ha hb
  have : swap a c â€¢ x = x := by
    by_cases ha' : a âˆˆ s
    Â· exact ht a c (ha ha') (by tauto)
    Â· exact hs a c ha' (by tauto)
  have : swap b c â€¢ x = x := by
    by_cases hb' : b âˆˆ s
    Â· exact ht b c (hb hb') (by tauto)
    Â· exact hs b c hb' (by tauto)
  cc

def FinitelySupported (ğ”¸ : Type*) [DecidableEq ğ”¸] {Î± : Type*} [MulAction (Finperm ğ”¸) Î±]
    (x : Î±) : Prop :=
  âˆƒ s : Finset ğ”¸, Supports (Finperm ğ”¸) (s : Set ğ”¸) x

/-- The minimal support of an object, if it exists. -/
noncomputable def supp (ğ”¸ : Type*) [DecidableEq ğ”¸] {Î± : Type*} [MulAction (Finperm ğ”¸) Î±] (x : Î±) :
    Finset ğ”¸ :=
  open scoped Classical in
  if hx : FinitelySupported ğ”¸ x then
    hx.choose.filter (Î» a â†¦ âˆ€ s : Finset ğ”¸, Supports (Finperm ğ”¸) (s : Set ğ”¸) x â†’ a âˆˆ s)
  else
    âˆ…

theorem supp_eq_of_not_finitelySupported {Î± : Type*} [MulAction (Finperm ğ”¸) Î±]
    (x : Î±) (hx : Â¬FinitelySupported ğ”¸ x) :
    supp ğ”¸ x = âˆ… := by
  rw [supp, dif_neg hx]

theorem mem_supp_iff' {Î± : Type*} [MulAction (Finperm ğ”¸) Î±]
    (x : Î±) (hx : FinitelySupported ğ”¸ x) (a : ğ”¸) :
    a âˆˆ supp ğ”¸ x â†” âˆ€ s : Finset ğ”¸, Supports (Finperm ğ”¸) (s : Set ğ”¸) x â†’ a âˆˆ s := by
  classical
  rw [supp, dif_pos hx, Finset.mem_filter, and_iff_right_iff_imp]
  intro ha
  exact ha hx.choose hx.choose_spec

theorem supp_supports' [Infinite ğ”¸] {Î± : Type*} [MulAction (Finperm ğ”¸) Î±]
    (x : Î±) (hx : FinitelySupported ğ”¸ x) :
    Supports (Finperm ğ”¸) ((supp ğ”¸ x) : Set ğ”¸) x := by
  intro Ï€ hÏ€
  obtain âŸ¨s, hsâŸ© := hx
  induction s using Finset.strongInduction
  case H s ih =>
    by_cases ht : âˆƒ t âŠ‚ s, Supports (Finperm ğ”¸) (t : Set ğ”¸) x
    Â· obtain âŸ¨t, htâ‚, htâ‚‚âŸ© := ht
      exact ih t htâ‚ htâ‚‚
    simp only [not_exists, not_and] at ht
    suffices s = supp ğ”¸ x by
      cases this
      exact hs Ï€ hÏ€
    ext a
    rw [mem_supp_iff' x âŸ¨s, hsâŸ©]
    constructor; swap; tauto
    intro ha u hu
    by_contra ha'
    refine ht _ ?_ (inter_supports s u x hs hu)
    rw [Finset.ssubset_iff]
    refine âŸ¨a, ?_, ?_âŸ©
    Â· simp only [Finset.mem_inter, not_and]
      exact Î» _ â†¦ ha'
    Â· intro b
      aesop

theorem supp_minimal [Infinite ğ”¸] {Î± : Type*} [MulAction (Finperm ğ”¸) Î±]
    (x : Î±) (s : Finset ğ”¸) (hs : Supports (Finperm ğ”¸) (s : Set ğ”¸) x) :
    supp ğ”¸ x âŠ† s := by
  intro a ha
  rw [mem_supp_iff' x âŸ¨s, hsâŸ©] at ha
  exact ha s hs

class Nominal (ğ”¸ : Type*) [DecidableEq ğ”¸] [Infinite ğ”¸] (Î± : Type*)
    extends MulAction (Finperm ğ”¸) Î± where
  supported : âˆ€ x : Î±, FinitelySupported ğ”¸ x

variable [Infinite ğ”¸]

namespace Nominal

instance : Nominal ğ”¸ ğ”¸ where
  supported := Î» a â†¦ âŸ¨{a}, Î» _ hÏ€ â†¦ hÏ€ (Finset.mem_singleton_self a)âŸ©

theorem mem_supp_iff {Î± : Type*} [Nominal ğ”¸ Î±]
    (x : Î±) (a : ğ”¸) :
    a âˆˆ supp ğ”¸ x â†” âˆ€ s : Finset ğ”¸, Supports (Finperm ğ”¸) (s : Set ğ”¸) x â†’ a âˆˆ s :=
  mem_supp_iff' x (Nominal.supported x) a

theorem supp_supports {Î± : Type*} [Nominal ğ”¸ Î±] (x : Î±) :
    Supports (Finperm ğ”¸) ((supp ğ”¸ x) : Set ğ”¸) x :=
  supp_supports' x (Nominal.supported x)

@[simp]
theorem name_supp_eq (a : ğ”¸) :
    supp ğ”¸ a = {a} := by
  ext b
  rw [mem_supp_iff]
  constructor
  Â· intro h
    exact h {a} (Î» _ hÏ€ â†¦ hÏ€ (Finset.mem_singleton_self a))
  Â· intro h
    rw [Finset.mem_singleton] at h
    cases h
    intro s hs
    by_contra ha
    obtain âŸ¨b, hbâŸ© := Infinite.exists_not_mem_finset (s âˆª {a})
    rw [Finset.mem_union, Finset.mem_singleton, not_or] at hb
    have := hs (swap a b) ?_
    Â· rw [smul_name_eq, swap_apply_left] at this
      tauto
    Â· intro c hc
      apply swap_apply_of_ne_of_ne <;> aesop

end Nominal

instance {G Î± : Type*} [Group G] [MulAction G Î±] : SMul G (Finset Î±) where
  smul g s := s.map âŸ¨(g â€¢ Â·), MulAction.injective gâŸ©

theorem Finset.smul_def {G Î± : Type*} [Group G] [MulAction G Î±]
    (g : G) (s : Finset Î±) :
    g â€¢ s = s.map âŸ¨(g â€¢ Â·), MulAction.injective gâŸ© :=
  rfl

theorem Finset.mem_smul_iff {G Î± : Type*} [Group G] [MulAction G Î±]
    (g : G) (x : Î±) (s : Finset Î±) :
    x âˆˆ g â€¢ s â†” gâ»Â¹ â€¢ x âˆˆ s := by
  rw [Finset.smul_def]
  aesop

instance {Î± : Type*} [MulAction (Finperm ğ”¸) Î±] : MulAction (Finperm ğ”¸) (Finset Î±) where
  smul Ï€ s := s.map âŸ¨(Ï€ â€¢ Â·), MulAction.injective Ï€âŸ©
  one_smul _ := by
    ext
    simp [Finset.mem_smul_iff]
  mul_smul _ _ _ := by
    ext
    simp [Finset.mem_smul_iff, mul_smul]

theorem Finset.smul_eq_of_smul_eq {Î± Î² : Type*} [Group Î²] [MulAction Î² Î±]
    (s : Finset Î±) (b : Î²) (h : âˆ€ a âˆˆ s, b â€¢ a = a) :
    b â€¢ s = s := by
  ext a
  rw [Finset.mem_smul_iff]
  constructor
  Â· intro ha
    have := h _ ha
    rw [smul_inv_smul] at this
    rwa [â† this] at ha
  Â· intro ha
    have := h _ ha
    rw [smul_eq_iff_eq_inv_smul] at this
    rwa [this] at ha

theorem Finset.finitelySupported {Î± : Type*} [Nominal ğ”¸ Î±] (s : Finset Î±) :
    FinitelySupported ğ”¸ s := by
  use s.biUnion (supp ğ”¸)
  intro Ï€ hÏ€
  apply Finset.smul_eq_of_smul_eq
  intro x hx
  apply Nominal.supp_supports x
  aesop

instance {Î± : Type*} [Nominal ğ”¸ Î±] : Nominal ğ”¸ (Finset Î±) where
  supported := Finset.finitelySupported

-- TODO: The version in mathlib isn't general enough!
theorem MulAction.Supports.smul' {Î± : Type*} [MulAction (Finperm ğ”¸) Î±]
    {s : Finset ğ”¸} {x : Î±}
    (h : Supports (Finperm ğ”¸) (s : Set ğ”¸) x) (Ï€ : Finperm ğ”¸) :
    Supports (Finperm ğ”¸) ((Ï€ â€¢ s : Finset ğ”¸) : Set ğ”¸) (Ï€ â€¢ x) := by
  intro Ï€' hÏ€'
  have := h (Ï€â»Â¹ * Ï€' * Ï€) ?_
  Â· rwa [mul_smul, mul_smul, inv_smul_eq_iff] at this
  intro a ha
  rw [mul_smul, mul_smul, inv_smul_eq_iff]
  apply hÏ€'
  rwa [smul_name_eq, Finset.mem_coe, Finset.mem_smul_iff, smul_name_eq, inv_apply_self]

theorem MulAction.Supports.of_smul' {Î± : Type*} [MulAction (Finperm ğ”¸) Î±]
    {s : Finset ğ”¸} {x : Î±} {Ï€ : Finperm ğ”¸}
    (h : Supports (Finperm ğ”¸) (s : Set ğ”¸) (Ï€ â€¢ x)) :
    Supports (Finperm ğ”¸) ((Ï€â»Â¹ â€¢ s : Finset ğ”¸) : Set ğ”¸) x := by
  have := h.smul' Ï€â»Â¹
  rwa [inv_smul_smul] at this

theorem FinitelySupported.smul {Î± : Type*} [MulAction (Finperm ğ”¸) Î±] {x : Î±}
    (h : FinitelySupported ğ”¸ x) (Ï€ : Finperm ğ”¸) :
    FinitelySupported ğ”¸ (Ï€ â€¢ x) := by
  obtain âŸ¨s, hsâŸ© := h
  refine âŸ¨Ï€ â€¢ s, hs.smul' Ï€âŸ©

theorem FinitelySupported.of_smul {Î± : Type*} [MulAction (Finperm ğ”¸) Î±] {x : Î±}
    {Ï€ : Finperm ğ”¸} (h : FinitelySupported ğ”¸ (Ï€ â€¢ x)):
    FinitelySupported ğ”¸ x := by
  have := h.smul Ï€â»Â¹
  rwa [inv_smul_smul] at this

section Set
open scoped Pointwise

theorem Finset.smul_coe_eq_coe_iff {G Î± : Type*} [Group G] [MulAction G Î±]
    (g : G) (s : Finset Î±) :
    g â€¢ (s : Set Î±) = s â†” g â€¢ s = s := by
  simp only [Set.ext_iff, Finset.mem_coe, Finset.ext_iff,
    Set.mem_smul_set_iff_inv_smul_mem, Finset.mem_smul_iff]

theorem Finset.supports_coe_iff {Î± : Type*} [MulAction (Finperm ğ”¸) Î±] (s : Finset Î±) (t : Set ğ”¸) :
    Supports (Finperm ğ”¸) t (s : Set Î±) â†” Supports (Finperm ğ”¸) t s := by
  unfold Supports
  simp only [Finset.smul_coe_eq_coe_iff]

theorem Finset.coe_finitelySupported_iff {Î± : Type*} [MulAction (Finperm ğ”¸) Î±] (s : Finset Î±) :
    FinitelySupported ğ”¸ (s : Set Î±) â†” FinitelySupported ğ”¸ s := by
  simp only [FinitelySupported, supports_coe_iff]

theorem Set.finitelySupported_of_finite {Î± : Type*} [Nominal ğ”¸ Î±] (s : Set Î±) (hs : s.Finite) :
    FinitelySupported ğ”¸ s := by
  lift s to Finset Î± using hs
  rw [Finset.coe_finitelySupported_iff]
  apply Nominal.supported

omit [Infinite ğ”¸] in
theorem FinitelySupported.compl {Î± : Type*} [MulAction (Finperm ğ”¸) Î±]
    {s : Set Î±} (hs : FinitelySupported ğ”¸ s) :
    FinitelySupported ğ”¸ sá¶œ := by
  obtain âŸ¨t, htâŸ© := hs
  refine âŸ¨t, ?_âŸ©
  intro Ï€ hÏ€
  rw [Set.smul_set_compl, ht Ï€ hÏ€]

end Set

set_option linter.unusedVariables false in
/-- A type alias to endow a type `Î±` with its discrete nominal structure. -/
def Discrete (ğ”¸ : Type*) (Î± : Type*) :=
  Î±

instance {Î± : Type*} : MulAction (Finperm ğ”¸) (Discrete ğ”¸ Î±) where
  smul _ := id
  one_smul _ := rfl
  mul_smul _ _ _ := rfl

instance {Î± : Type*} : Nominal ğ”¸ (Discrete ğ”¸ Î±) where
  supported := Î» _ â†¦ âŸ¨âˆ…, Î» _ _ â†¦ rflâŸ©

class Equivariant (ğ”¸ : Type*) [DecidableEq ğ”¸]
    {Î± Î² : Type*} [MulAction (Finperm ğ”¸) Î±] [MulAction (Finperm ğ”¸) Î²]
    (f : Î± â†’ Î²) : Prop where
  smul_apply : âˆ€ Ï€ : Finperm ğ”¸, âˆ€ a, Ï€ â€¢ f a = f (Ï€ â€¢ a)

attribute [simp] Equivariant.smul_apply

instance {Î± Î² : Type*} [MulAction (Finperm ğ”¸) Î±] [MulAction (Finperm ğ”¸) Î²]
    (f : Discrete ğ”¸ Î± â†’ Discrete ğ”¸ Î²) :
    Equivariant ğ”¸ f := by
  constructor
  intro Ï€ x
  rfl

instance {Î± : Type*} [MulAction (Finperm ğ”¸) Î±] : Equivariant ğ”¸ (supp ğ”¸ : Î± â†’ Finset ğ”¸) := by
  constructor
  intro Ï€ x
  ext a
  rw [Finset.mem_smul_iff]
  by_cases hx : FinitelySupported ğ”¸ x
  Â· rw [mem_supp_iff' x hx, mem_supp_iff' (Ï€ â€¢ x) (hx.smul Ï€)]
    constructor
    Â· intro h s hs
      refine (Finset.mem_map' _).mp (h (Ï€â»Â¹ â€¢ s) ?_)
      have := hs.smul' Ï€â»Â¹
      rwa [inv_smul_smul] at this
    Â· intro h s hs
      have := h (Ï€ â€¢ s) (hs.smul' Ï€)
      rwa [Finset.mem_smul_iff] at this
  Â· rw [supp_eq_of_not_finitelySupported x hx, supp_eq_of_not_finitelySupported]
    Â· simp only [smul_name_eq, Finset.not_mem_empty]
    Â· contrapose! hx
      exact hx.of_smul

theorem MulAction.Supports.map {Î± : Type*} [MulAction (Finperm ğ”¸) Î±]
    {x : Î±} {s : Set ğ”¸} (h : Supports (Finperm ğ”¸) s x)
    {Î² : Type*} [MulAction (Finperm ğ”¸) Î²] (f : Î± â†’ Î²) [Equivariant ğ”¸ f] :
    Supports (Finperm ğ”¸) s (f x) := by
  intro Ï€ hÏ€
  rw [Equivariant.smul_apply, h Ï€ hÏ€]

theorem MulAction.Supports.of_map {Î± : Type*} [MulAction (Finperm ğ”¸) Î±]
    {Î² : Type*} [MulAction (Finperm ğ”¸) Î²]
    {x : Î±} {s : Set ğ”¸} {f : Î± â†’ Î²} [Equivariant ğ”¸ f] (h : Supports (Finperm ğ”¸) s (f x))
    (hf : Function.Injective f) :
    Supports (Finperm ğ”¸) s x := by
  intro Ï€ hÏ€
  have := h Ï€ hÏ€
  rw [Equivariant.smul_apply] at this
  exact hf this

theorem FinitelySupported.map {Î± : Type*} [MulAction (Finperm ğ”¸) Î±]
    {x : Î±} (h : FinitelySupported ğ”¸ x)
    {Î² : Type*} [MulAction (Finperm ğ”¸) Î²] (f : Î± â†’ Î²) [Equivariant ğ”¸ f] :
    FinitelySupported ğ”¸ (f x) := by
  obtain âŸ¨s, hsâŸ© := h
  exact âŸ¨s, hs.map fâŸ©

theorem FinitelySupported.of_map {Î± : Type*} [MulAction (Finperm ğ”¸) Î±]
    {Î² : Type*} [MulAction (Finperm ğ”¸) Î²]
    {x : Î±} {f : Î± â†’ Î²} [Equivariant ğ”¸ f] (h : FinitelySupported ğ”¸ (f x))
    (hf : Function.Injective f) :
    FinitelySupported ğ”¸ x := by
  obtain âŸ¨s, hsâŸ© := h
  exact âŸ¨s, hs.of_map hfâŸ©

theorem supp_apply_subset {Î± Î² : Type*} [Nominal ğ”¸ Î±] [Nominal ğ”¸ Î²]
    (f : Î± â†’ Î²) [Equivariant ğ”¸ f] (x : Î±) :
    supp ğ”¸ (f x) âŠ† supp ğ”¸ x := by
  intro a ha
  rw [Nominal.mem_supp_iff] at ha âŠ¢
  intro s hs
  exact ha s (hs.map f)

theorem supp_apply_eq_of_injective {Î± Î² : Type*} [Nominal ğ”¸ Î±] [Nominal ğ”¸ Î²]
    (f : Î± â†’ Î²) [Equivariant ğ”¸ f] (hf : Function.Injective f) (x : Î±) :
    supp ğ”¸ (f x) = supp ğ”¸ x := by
  apply subset_antisymm
  Â· exact supp_apply_subset f x
  intro a ha
  rw [Nominal.mem_supp_iff] at ha âŠ¢
  intro s hs
  exact ha s (hs.of_map hf)

theorem finitelySupported_of_surjective {Î± Î² : Type*} [Nominal ğ”¸ Î±] [MulAction (Finperm ğ”¸) Î²]
    (f : Î± â†’ Î²) [Equivariant ğ”¸ f] (hf : Function.Surjective f) (y : Î²) :
    FinitelySupported ğ”¸ y := by
  obtain âŸ¨x, rflâŸ© := hf y
  exact (Nominal.supported x).map f

def nominal_of_surjective {Î± Î² : Type*} [Nominal ğ”¸ Î±] [MulAction (Finperm ğ”¸) Î²]
    (f : Î± â†’ Î²) [Equivariant ğ”¸ f] (hf : Function.Surjective f) :
    Nominal ğ”¸ Î² where
  supported := finitelySupported_of_surjective f hf
