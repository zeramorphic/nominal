import Mathlib.Data.Fintype.Card
import Mathlib.Data.Set.Finite.Lattice
import Nominal.Finperm

/-!
We're working from <https://people.cs.nott.ac.uk/pszvc/mgs/MGS2011_nominal_sets.pdf>.
-/

variable {ğ”¸ : Type*} [DecidableEq ğ”¸]

open Finperm

/-- A typeclass for types that have a `Finperm ğ”¸`-action.
We use a different notation `â¬` in order to avoid conflicts with `Pi.perm`. -/
class HasPerm (ğ”¸ : Type*) (Î± : Sort*) where
  /-- Permute this type according to the given finite permutation of names. -/
  perm : Finperm ğ”¸ â†’ Î± â†’ Î±

@[inherit_doc]
infixr:73 " â¬ " => HasPerm.perm

/-! We do the same as Mathlib does for `â¬`: a trick to help some elaboration problems. -/
@[inherit_doc HasPerm.perm]
macro_rules | `($x â¬ $y) => `(leftact% HasPerm.perm $x $y)

/-- A typeclass for types that have a lawful `Finperm ğ”¸`-action. -/
class MulPerm (ğ”¸ : Type*) (Î± : Sort*) [DecidableEq ğ”¸] extends HasPerm ğ”¸ Î± where
  one_perm : âˆ€ x : Î±, (1 : Finperm ğ”¸) â¬ x = x
  mul_perm : âˆ€ Ï€â‚ Ï€â‚‚ : Finperm ğ”¸, âˆ€ x : Î±, (Ï€â‚ * Ï€â‚‚) â¬ x = Ï€â‚ â¬ Ï€â‚‚ â¬ x

export MulPerm (one_perm mul_perm)

attribute [simp] one_perm

theorem MulPerm.injective {Î± : Sort*} [MulPerm ğ”¸ Î±] (Ï€ : Finperm ğ”¸) :
    Function.Injective Î» x : Î± â†¦ Ï€ â¬ x := by
  intro x y h
  have := congr_arg (Ï€â»Â¹ â¬ Â·) h
  simp only [â† mul_perm, inv_mul_cancel, one_perm] at this
  exact this

@[simp]
theorem perm_left_cancel_iff {Î± : Sort*} [MulPerm ğ”¸ Î±] (Ï€ : Finperm ğ”¸) (x y : Î±) :
    Ï€ â¬ x = Ï€ â¬ y â†” x = y :=
  (MulPerm.injective Ï€).eq_iff

@[simp]
theorem inv_perm_perm {Î± : Sort*} [MulPerm ğ”¸ Î±] (Ï€ : Finperm ğ”¸) (x : Î±) :
    Ï€â»Â¹ â¬ Ï€ â¬ x = x := by
  have := mul_perm Ï€â»Â¹ Ï€ x
  rw [inv_mul_cancel, one_perm] at this
  rw [â† this]

@[simp]
theorem perm_inv_perm {Î± : Sort*} [MulPerm ğ”¸ Î±] (Ï€ : Finperm ğ”¸) (x : Î±) :
    Ï€ â¬ Ï€â»Â¹ â¬ x = x := by
  have := mul_perm Ï€ Ï€â»Â¹ x
  rw [mul_inv_cancel, one_perm] at this
  rw [â† this]

theorem perm_eq_iff_eq_inv_perm {Î± : Sort*} [MulPerm ğ”¸ Î±] (Ï€ : Finperm ğ”¸) (x y : Î±) :
    Ï€ â¬ x = y â†” x = Ï€â»Â¹ â¬ y := by
  rw [â† perm_left_cancel_iff Ï€ x (Ï€â»Â¹ â¬ y), perm_inv_perm]

theorem inv_perm_eq_iff {Î± : Sort*} [MulPerm ğ”¸ Î±] (Ï€ : Finperm ğ”¸) (x y : Î±) :
    Ï€â»Â¹ â¬ x = y â†” x = Ï€ â¬ y := by
  rw [â† perm_left_cancel_iff Ï€â»Â¹ x (Ï€ â¬ y), inv_perm_perm]

instance : MulPerm ğ”¸ ğ”¸ where
  perm Ï€ a := Ï€ a
  one_perm := Finperm.one_apply
  mul_perm := Finperm.mul_apply

@[simp]
theorem Finperm.perm_name_eq (Ï€ : Finperm ğ”¸) (a : ğ”¸) :
    Ï€ â¬ a = Ï€ a :=
  rfl

/-- A finite set of names `s` *supports* `x` if `Ï€ â¬ x = x` whenever
`Ï€ a = a` for all `a âˆˆ s`. -/
def Supports {Î± : Sort*} [HasPerm ğ”¸ Î±] (s : Finset ğ”¸) (x : Î±) : Prop :=
  âˆ€ Ï€ : Finperm ğ”¸, (âˆ€ a âˆˆ s, Ï€ a = a) â†’ Ï€ â¬ x = x

omit [DecidableEq ğ”¸] in
theorem Supports.mono {Î± : Sort*} [HasPerm ğ”¸ Î±] {s t : Finset ğ”¸} {x : Î±}
    (h : Supports s x) (h' : s âŠ† t) : Supports t x :=
  Î» Ï€ hÏ€ â†¦ h Ï€ (Î» a ha â†¦ hÏ€ a (h' ha))

theorem Finperm.supports_iff' (ğ”¸ : Type*) [DecidableEq ğ”¸] {Î± : Sort*} [MulPerm ğ”¸ Î±]
    (s : Finset ğ”¸) (x : Î±) :
    Supports s x â†” âˆ€ a b, a âˆ‰ s â†’ b âˆ‰ s â†’ swap a b â¬ x = x := by
  constructor
  Â· intro hs a b ha hb
    apply hs (swap a b)
    intro c hc
    apply swap_apply_of_ne_of_ne <;>
    Â· rintro rfl
      contradiction
  Â· intro h Ï€ hÏ€
    induction Ï€ using swap_induction_right
    case one => rw [one_perm]
    case swap Ï€ a b ha hab ih =>
      rw [mem_support_iff, not_ne_iff] at ha
      rw [mul_perm]
      by_cases ha' : a âˆˆ s
      Â· have := hÏ€ a ha'
        simp only [coe_mul, Function.comp_apply, swap_apply_left] at this
        have := ha.trans this.symm
        rw [EmbeddingLike.apply_eq_iff_eq] at this
        contradiction
      by_cases hb' : b âˆˆ s
      Â· have := hÏ€ b hb'
        simp only [coe_mul, Function.comp_apply, swap_apply_right] at this
        have := ha.symm.trans this
        contradiction
      rw [h a b ha' hb']
      apply ih
      intro c hc
      have := hÏ€ c hc
      rwa [mul_apply, swap_apply_of_ne_of_ne] at this <;>
      Â· rintro rfl; contradiction

theorem Finperm.supports_iff (ğ”¸ : Type*) [DecidableEq ğ”¸] {Î± : Sort*} [MulPerm ğ”¸ Î±]
    (s : Finset ğ”¸) (x : Î±) :
    Supports s x â†” âˆ€ a b, a âˆ‰ s â†’ b âˆ‰ s â†’ a â‰  b â†’ swap a b â¬ x = x := by
  rw [supports_iff']
  constructor
  Â· tauto
  Â· intro h a b ha hb
    by_cases hab : a = b
    Â· cases hab
      rw [swap_self, one_perm]
    Â· exact h a b ha hb hab

theorem Finperm.inter_supports [Infinite ğ”¸] {Î± : Sort*} [MulPerm ğ”¸ Î±]
    (s t : Finset ğ”¸) (x : Î±)
    (hs : Supports s x) (ht : Supports t x) :
    Supports (s âˆ© t) x := by
  rw [supports_iff'] at hs ht
  rw [supports_iff]
  intro a b ha hb hab
  obtain âŸ¨c, hcâŸ© := Infinite.exists_not_mem_finset (s âˆª t âˆª {a, b})
  simp at hc
  rw [swap_triple a b c hab (by tauto), mul_perm, mul_perm]
  rw [Finset.mem_inter, not_and] at ha hb
  have : swap a c â¬ x = x := by
    by_cases ha' : a âˆˆ s
    Â· exact ht a c (ha ha') (by tauto)
    Â· exact hs a c ha' (by tauto)
  have : swap b c â¬ x = x := by
    by_cases hb' : b âˆˆ s
    Â· exact ht b c (hb hb') (by tauto)
    Â· exact hs b c hb' (by tauto)
  cc

/-!
# Finite and empty support
-/

/-- An object is called *equivariant* if it is fixed under all permutations of names. -/
def Equivariant (ğ”¸ : Type*) [DecidableEq ğ”¸] {Î± : Sort*} [HasPerm ğ”¸ Î±] (x : Î±) :=
  âˆ€ Ï€ : Finperm ğ”¸, Ï€ â¬ x = x

/-- An object is called *finitely supported* if it has a finite support. -/
def FinitelySupported (ğ”¸ : Type*) [DecidableEq ğ”¸] {Î± : Sort*} [HasPerm ğ”¸ Î±]
    (x : Î±) : Prop :=
  âˆƒ s : Finset ğ”¸, Supports s x

theorem Equivariant.finitelySupported {Î± : Sort*} [HasPerm ğ”¸ Î±] {x : Î±}
    (h : Equivariant ğ”¸ x) : FinitelySupported ğ”¸ x := by
  use âˆ…
  intro Ï€ _
  rw [h]

/-- The minimal support of an object, if it exists. -/
noncomputable def supp (ğ”¸ : Type*) [DecidableEq ğ”¸] {Î± : Sort*} [MulPerm ğ”¸ Î±] (x : Î±) :
    Finset ğ”¸ :=
  open scoped Classical in
  if hx : FinitelySupported ğ”¸ x then
    hx.choose.filter (Î» a â†¦ âˆ€ s : Finset ğ”¸, Supports s x â†’ a âˆˆ s)
  else
    âˆ…

theorem supp_eq_of_not_finitelySupported {Î± : Sort*} [MulPerm ğ”¸ Î±]
    (x : Î±) (hx : Â¬FinitelySupported ğ”¸ x) :
    supp ğ”¸ x = âˆ… := by
  rw [supp, dif_neg hx]

theorem mem_supp_iff' {Î± : Sort*} [MulPerm ğ”¸ Î±]
    (x : Î±) (hx : FinitelySupported ğ”¸ x) (a : ğ”¸) :
    a âˆˆ supp ğ”¸ x â†” âˆ€ s : Finset ğ”¸, Supports s x â†’ a âˆˆ s := by
  classical
  rw [supp, dif_pos hx, Finset.mem_filter, and_iff_right_iff_imp]
  intro ha
  exact ha hx.choose hx.choose_spec

theorem supp_supports' [Infinite ğ”¸] {Î± : Sort*} [MulPerm ğ”¸ Î±]
    (x : Î±) (hx : FinitelySupported ğ”¸ x) :
    Supports (supp ğ”¸ x) x := by
  intro Ï€ hÏ€
  obtain âŸ¨s, hsâŸ© := hx
  induction s using Finset.strongInduction
  case H s ih =>
    by_cases ht : âˆƒ t âŠ‚ s, Supports t x
    Â· obtain âŸ¨t, htâ‚, htâ‚‚âŸ© := ht
      exact ih t htâ‚ htâ‚‚
    simp only [not_exists, not_and] at ht
    suffices s = supp ğ”¸ x by
      cases this
      exact hs Ï€ hÏ€
    ext a
    rw [mem_supp_iff' x âŸ¨s, hsâŸ©]
    constructor; swap; tauto
    intro ha u hu
    by_contra ha'
    refine ht _ ?_ (inter_supports s u x hs hu)
    rw [Finset.ssubset_iff]
    refine âŸ¨a, ?_, ?_âŸ©
    Â· simp only [Finset.mem_inter, not_and]
      exact Î» _ â†¦ ha'
    Â· intro b
      aesop

theorem supp_minimal {Î± : Sort*} [MulPerm ğ”¸ Î±]
    (x : Î±) (s : Finset ğ”¸) (hs : Supports s x) :
    supp ğ”¸ x âŠ† s := by
  intro a ha
  rw [mem_supp_iff' x âŸ¨s, hsâŸ©] at ha
  exact ha s hs

class Nominal (ğ”¸ : Type*) [DecidableEq ğ”¸] (Î± : Sort*)
    extends MulPerm ğ”¸ Î± where
  supported : âˆ€ x : Î±, FinitelySupported ğ”¸ x

namespace Nominal

instance : Nominal ğ”¸ ğ”¸ where
  supported := Î» a â†¦ âŸ¨{a}, Î» _ hÏ€ â†¦ hÏ€ a (Finset.mem_singleton_self a)âŸ©

theorem mem_supp_iff {Î± : Sort*} [Nominal ğ”¸ Î±]
    (x : Î±) (a : ğ”¸) :
    a âˆˆ supp ğ”¸ x â†” âˆ€ s : Finset ğ”¸, Supports s x â†’ a âˆˆ s :=
  mem_supp_iff' x (Nominal.supported x) a

theorem supp_supports (ğ”¸ : Type*) [DecidableEq ğ”¸] [Infinite ğ”¸] {Î± : Sort*} [Nominal ğ”¸ Î±] (x : Î±) :
    Supports (supp ğ”¸ x) x :=
  supp_supports' x (Nominal.supported x)

theorem supp_subset_iff [Infinite ğ”¸] {Î± : Sort*} [Nominal ğ”¸ Î±] (x : Î±) (s : Finset ğ”¸) :
    supp ğ”¸ x âŠ† s â†” Supports s x :=
  âŸ¨(supp_supports ğ”¸ x).mono, supp_minimal x sâŸ©

@[simp]
theorem name_supp_eq [Infinite ğ”¸] (a : ğ”¸) :
    supp ğ”¸ a = {a} := by
  ext b
  rw [mem_supp_iff]
  constructor
  Â· intro h
    exact h {a} (Î» _ hÏ€ â†¦ hÏ€ a (Finset.mem_singleton_self a))
  Â· intro h
    rw [Finset.mem_singleton] at h
    cases h
    intro s hs
    by_contra ha
    obtain âŸ¨b, hbâŸ© := Infinite.exists_not_mem_finset (s âˆª {a})
    rw [Finset.mem_union, Finset.mem_singleton, not_or] at hb
    have := hs (swap a b) ?_
    Â· rw [perm_name_eq, swap_apply_left] at this
      tauto
    Â· intro c hc
      apply swap_apply_of_ne_of_ne <;> aesop

end Nominal

instance {Î± : Type*} [MulPerm ğ”¸ Î±] : HasPerm ğ”¸ (Finset Î±) where
  perm Ï€ s := s.map âŸ¨(Ï€ â¬ Â·), MulPerm.injective Ï€âŸ©

theorem Finset.perm_def {Î± : Type*} [MulPerm ğ”¸ Î±]
    (Ï€ : Finperm ğ”¸) (s : Finset Î±) :
    Ï€ â¬ s = s.map âŸ¨(Ï€ â¬ Â·), MulPerm.injective Ï€âŸ© :=
  rfl

theorem Finset.mem_perm_iff {Î± : Type*} [MulPerm ğ”¸ Î±]
    (Ï€ : Finperm ğ”¸) (x : Î±) (s : Finset Î±) :
    x âˆˆ Ï€ â¬ s â†” Ï€â»Â¹ â¬ x âˆˆ s := by
  rw [Finset.perm_def]
  aesop

instance {Î± : Type*} [MulPerm ğ”¸ Î±] : MulPerm ğ”¸ (Finset Î±) where
  one_perm _ := by
    ext
    simp [Finset.mem_perm_iff]
  mul_perm _ _ _ := by
    ext
    simp [Finset.mem_perm_iff, mul_perm]

theorem Finset.perm_eq_of_perm_eq {Î± : Type*} [MulPerm ğ”¸ Î±]
    (s : Finset Î±) (Ï€ : Finperm ğ”¸) (h : âˆ€ a âˆˆ s, Ï€ â¬ a = a) :
    Ï€ â¬ s = s := by
  ext a
  rw [Finset.mem_perm_iff]
  constructor
  Â· intro ha
    have := h _ ha
    rw [perm_inv_perm] at this
    rwa [â† this] at ha
  Â· intro ha
    have := h _ ha
    rw [perm_eq_iff_eq_inv_perm] at this
    rwa [this] at ha

theorem Finset.finitelySupported [Infinite ğ”¸] {Î± : Type*} [Nominal ğ”¸ Î±] (s : Finset Î±) :
    FinitelySupported ğ”¸ s := by
  use s.biUnion (supp ğ”¸)
  intro Ï€ hÏ€
  apply Finset.perm_eq_of_perm_eq
  intro x hx
  apply Nominal.supp_supports ğ”¸ x
  aesop

instance [Infinite ğ”¸] {Î± : Type*} [Nominal ğ”¸ Î±] : Nominal ğ”¸ (Finset Î±) where
  supported := Finset.finitelySupported

theorem Supports.perm {Î± : Sort*} [MulPerm ğ”¸ Î±]
    {s : Finset ğ”¸} {x : Î±}
    (h : Supports s x) (Ï€ : Finperm ğ”¸) :
    Supports (Ï€ â¬ s) (Ï€ â¬ x) := by
  intro Ï€' hÏ€'
  have := h (Ï€â»Â¹ * Ï€' * Ï€) ?_
  Â· rwa [mul_perm, mul_perm, inv_perm_eq_iff] at this
  intro a ha
  rw [coe_mul, Function.comp_apply, coe_mul, Function.comp_apply, hÏ€' (Ï€ a), inv_apply_self]
  rwa [Finset.mem_perm_iff, perm_name_eq, inv_apply_self]

theorem Supports.of_perm {Î± : Sort*} [MulPerm ğ”¸ Î±]
    {s : Finset ğ”¸} {x : Î±} {Ï€ : Finperm ğ”¸}
    (h : Supports s (Ï€ â¬ x)) :
    Supports (Ï€â»Â¹ â¬ s) x := by
  have := h.perm Ï€â»Â¹
  rwa [inv_perm_perm] at this

theorem FinitelySupported.perm {Î± : Sort*} [MulPerm ğ”¸ Î±] {x : Î±}
    (h : FinitelySupported ğ”¸ x) (Ï€ : Finperm ğ”¸) :
    FinitelySupported ğ”¸ (Ï€ â¬ x) := by
  obtain âŸ¨s, hsâŸ© := h
  refine âŸ¨Ï€ â¬ s, hs.perm Ï€âŸ©

theorem FinitelySupported.of_perm {Î± : Sort*} [MulPerm ğ”¸ Î±] {x : Î±}
    {Ï€ : Finperm ğ”¸} (h : FinitelySupported ğ”¸ (Ï€ â¬ x)):
    FinitelySupported ğ”¸ x := by
  have := h.perm Ï€â»Â¹
  rwa [inv_perm_perm] at this

@[simp]
theorem Nominal.supp_perm_eq {Î± : Sort*} [Nominal ğ”¸ Î±] (x : Î±) (Ï€ : Finperm ğ”¸) :
    supp ğ”¸ (Ï€ â¬ x) = Ï€ â¬ (supp ğ”¸ x) := by
  ext a
  simp only [mem_supp_iff, Finset.mem_perm_iff]
  constructor
  Â· intro h s hs
    have := h _ (hs.perm Ï€)
    rwa [Finset.mem_perm_iff] at this
  Â· intro h s hs
    have := h (Ï€â»Â¹ â¬ s) ?_
    Â· rwa [Finset.mem_perm_iff, inv_inv, perm_name_eq, perm_name_eq, apply_inv_self] at this
    Â· have := hs.perm Ï€â»Â¹
      rwa [inv_perm_perm] at this

def StrongSupports {Î± : Sort*} [MulPerm ğ”¸ Î±] (s : Finset ğ”¸) (x : Î±) :=
  âˆ€ Ï€ : Finperm ğ”¸, (âˆ€ a âˆˆ s, Ï€ a = a) â†” Ï€ â¬ x = x

theorem StrongSupports.supports {Î± : Sort*} [MulPerm ğ”¸ Î±]
    {s : Finset ğ”¸} {x : Î±} (h : StrongSupports s x) : Supports s x := by
  intro Ï€ h'
  rwa [â† h]

theorem subset_of_strongSupports [Infinite ğ”¸] {s t : Finset ğ”¸} {Î± : Sort*} [MulPerm ğ”¸ Î±] {x : Î±}
    (hs : StrongSupports s x)
    (ht : Supports t x) :
    s âŠ† t := by
  intro a ha
  by_contra ha'
  obtain âŸ¨b, hbâŸ© := Infinite.exists_not_mem_finset (t âˆª {a})
  rw [StrongSupports] at hs
  have := ht (swap a b) ?_
  Â· have := (hs (swap a b)).mpr this a ha
    aesop
  Â· intro c hc
    rw [swap_apply_of_ne_of_ne] <;> aesop

theorem supp_eq_of_strongSupports [Infinite ğ”¸] {Î± : Sort*} [MulPerm ğ”¸ Î±]
    (x : Î±) (s : Finset ğ”¸) (hs : StrongSupports s x) :
    supp ğ”¸ x = s := by
  apply subset_antisymm
  Â· apply supp_minimal x s hs.supports
  intro a ha
  rw [mem_supp_iff' x âŸ¨s, hs.supportsâŸ©]
  intro t ht
  exact subset_of_strongSupports hs ht ha

theorem Nominal.mem_supp_iff_names_infinite [Infinite ğ”¸] {Î± : Sort*} [Nominal ğ”¸ Î±] (x : Î±) (a : ğ”¸) :
    a âˆˆ supp ğ”¸ x â†” {b | swap a b â¬ x â‰  x}.Infinite := by
  constructor
  Â· intro h
    by_contra h'
    rw [Set.not_infinite] at h'
    obtain âŸ¨t, htâŸ© := h'.exists_finset
    clear h'
    rw [mem_supp_iff] at h
    have := h t ?_
    Â· rw [ht] at this
      simp at this
    Â· rw [supports_iff]
      intro b c hb hc hbc
      rw [ht, Set.mem_setOf_eq, not_not] at hb hc
      by_cases hac : c = a
      Â· subst hac
        rw [swap_comm, hb]
      Â· rw [swap_triple b c a hbc hac, mul_perm, mul_perm, swap_comm b, swap_comm c, hb, hc, hb]
  Â· intro h
    contrapose h
    rw [Set.not_infinite]
    apply (supp ğ”¸ x âˆª {a}).finite_toSet.subset
    intro b hb
    by_contra hb'
    have := supp_supports ğ”¸ x
    rw [supports_iff] at this
    exact hb (this a b h (by aesop) (by aesop))

theorem Nominal.swap_perm_eq_of_swap_perm_eq [Infinite ğ”¸] {Î± : Sort*} [Nominal ğ”¸ Î±]
    (x : Î±) (a b c : ğ”¸) (hbc : b â‰  c) (hca : c â‰  a) :
    swap a b â¬ x = swap a c â¬ x â†’ swap a b â¬ x = swap b c â¬ x := by
  have := swap_triple b c a hbc hca
  rw [swap_comm b a, swap_comm c a] at this
  rw [this, mul_perm, mul_perm, perm_left_cancel_iff, â† inv_perm_eq_iff, swap_inv]
  tauto

theorem Nominal.swap_perm_injOn [Infinite ğ”¸] {Î± : Type*} [Nominal ğ”¸ Î±] (x : Î±)
    (a : ğ”¸) (ha : a âˆˆ supp ğ”¸ x) :
    Set.InjOn (swap a Â· â¬ x) ({b | swap a b â¬ x â‰  x} \ supp ğ”¸ x) := by
  intro b âŸ¨hbâ‚, hbâ‚‚âŸ© c âŸ¨hcâ‚, hcâ‚‚âŸ© h
  by_contra hbc
  have h' := Nominal.swap_perm_eq_of_swap_perm_eq x a b c (by aesop) (by aesop) h
  have := Nominal.supp_supports ğ”¸ x
  rw [supports_iff] at this
  rw [this b c hbâ‚‚ hcâ‚‚ hbc] at h'
  exact hbâ‚ h'

/-- TODO: This is not in the source material. -/
theorem Nominal.mem_supp_iff_range_infinite [Infinite ğ”¸] {Î± : Type*} [Nominal ğ”¸ Î±]
    (x : Î±) (a : ğ”¸) :
    a âˆˆ supp ğ”¸ x â†” (Set.range (swap a Â· â¬ x)).Infinite := by
  constructor
  Â· intro ha
    apply Set.infinite_of_injOn_mapsTo (swap_perm_injOn x a ha)
    Â· intro b _
      use b
    Â· apply Set.Infinite.diff
      Â· rwa [mem_supp_iff_names_infinite] at ha
      Â· exact Finset.finite_toSet (supp ğ”¸ x)
  Â· intro ha
    rw [mem_supp_iff_names_infinite]
    contrapose ha
    rw [Set.not_infinite] at ha âŠ¢
    have := (ha.image (swap a Â· â¬ x)).union (Set.finite_singleton x)
    apply this.subset
    rintro _ âŸ¨b, rflâŸ©
    by_cases swap a b â¬ x = x <;> aesop

theorem Finset.subset_supp [Infinite ğ”¸] {Î± : Type*} [Nominal ğ”¸ Î±] (s : Finset Î±)
    (x : Î±) (hx : x âˆˆ s) :
    supp ğ”¸ x âŠ† supp ğ”¸ s := by
  intro a ha
  rw [Nominal.mem_supp_iff_range_infinite] at ha âŠ¢
  contrapose ha
  rw [Set.not_infinite] at ha âŠ¢
  have := ha.biUnion (t := Î» t â†¦ (t : Set Î±)) (Î» _ _ â†¦ finite_toSet _)
  apply this.subset
  rintro _ âŸ¨b, rflâŸ©
  simp only [Set.mem_range, Set.iUnion_exists, Set.iUnion_iUnion_eq', Set.mem_iUnion, mem_coe]
  use b
  rwa [Finset.mem_perm_iff, inv_perm_perm]

theorem Finset.supp_subset [Infinite ğ”¸] {Î± : Type*} [Nominal ğ”¸ Î±] (s : Finset Î±) :
    supp ğ”¸ s âŠ† s.biUnion (supp ğ”¸) := by
  intro a ha
  contrapose ha
  simp only [mem_biUnion, not_exists, not_and,
    Nominal.mem_supp_iff_range_infinite, Set.not_infinite] at ha
  have := (finite_toSet _).biUnion ha
  rw [Nominal.mem_supp_iff_range_infinite, Set.not_infinite]
  apply (this.powerset.preimage (f := Finset.toSet) (Î» _ _ _ _ â†¦ coe_inj.mp)).subset
  rintro _ âŸ¨b, rflâŸ©
  simp only [mem_coe, Set.mem_preimage, Set.mem_powerset_iff]
  rintro x hx
  rw [mem_coe, Finset.mem_perm_iff] at hx
  simp only [Set.mem_iUnion, Set.mem_range]
  exact âŸ¨_, hx, b, by rw [perm_inv_perm]âŸ©

theorem Finset.supp_eq [Infinite ğ”¸] {Î± : Type*} [Nominal ğ”¸ Î±] (s : Finset Î±) :
    supp ğ”¸ s = s.biUnion (supp ğ”¸) := by
  apply subset_antisymm
  Â· exact supp_subset s
  Â· intro a ha
    rw [mem_biUnion] at ha
    obtain âŸ¨x, hx, haâŸ© := ha
    exact subset_supp s x hx ha

theorem Finset.supports_iff [Infinite ğ”¸] {Î± : Type*} [Nominal ğ”¸ Î±] (s : Finset Î±) (t : Finset ğ”¸) :
    Supports t s â†” âˆ€ x âˆˆ s, Supports t x := by
  simp only [â† Nominal.supp_subset_iff, Finset.supp_eq, biUnion_subset_iff_forall_subset]

/-!
# Propositions
-/

instance : MulPerm ğ”¸ Prop where
  perm _ p := p
  one_perm _ := rfl
  mul_perm _ _ _ := rfl

instance (p : Prop) : Equivariant ğ”¸ p :=
  Î» _ â†¦ rfl

@[simp]
theorem perm_prop (Ï€ : Finperm ğ”¸) (p : Prop) :
    Ï€ â¬ p â†” p :=
  Iff.rfl

/-!
# Function types
-/

instance {Î± Î² : Sort*} [HasPerm ğ”¸ Î±] [HasPerm ğ”¸ Î²] :
    HasPerm ğ”¸ (Î± â†’ Î²) where
  perm Ï€ f x := Ï€ â¬ f (Ï€â»Â¹ â¬ x)

@[simp]
theorem Function.perm_def {Î± Î² : Sort*} [HasPerm ğ”¸ Î±] [HasPerm ğ”¸ Î²]
    (f : Î± â†’ Î²) (x : Î±) (Ï€ : Finperm ğ”¸) :
    (Ï€ â¬ f) x = Ï€ â¬ f (Ï€â»Â¹ â¬ x) :=
  rfl

instance {Î± Î² : Sort*} [MulPerm ğ”¸ Î±] [MulPerm ğ”¸ Î²] :
    MulPerm ğ”¸ (Î± â†’ Î²) where
  one_perm f := by
    ext x
    rw [Function.perm_def, inv_one, one_perm, one_perm]
  mul_perm Ï€â‚ Ï€â‚‚ f := by
    ext x
    simp only [Function.perm_def, mul_inv_rev, mul_perm]

theorem Function.equivariant_iff {Î± Î² : Sort*} [MulPerm ğ”¸ Î±] [MulPerm ğ”¸ Î²]
    (f : Î± â†’ Î²) :
    Equivariant ğ”¸ f â†” âˆ€ Ï€ : Finperm ğ”¸, âˆ€ x, Ï€ â¬ f x = f (Ï€ â¬ x) := by
  simp only [Equivariant, funext_iff, perm_def]
  constructor
  Â· intro h Ï€ x
    have := h Ï€ (Ï€ â¬ x)
    rw [inv_perm_perm] at this
    rw [â† this]
  Â· intro h Ï€ x
    rw [â† h Ï€â»Â¹ x, perm_inv_perm]

theorem apply_perm_eq {Î± Î² : Sort*} [MulPerm ğ”¸ Î±] [MulPerm ğ”¸ Î²] {f : Î± â†’ Î²}
    (h : Equivariant ğ”¸ f) (Ï€ : Finperm ğ”¸) (x : Î±) :
    Ï€ â¬ f x = f (Ï€ â¬ x) := by
  rw [Function.equivariant_iff] at h
  rw [h]

theorem applyâ‚‚_perm_eq {Î± Î² Î³ : Sort*} [MulPerm ğ”¸ Î±] [MulPerm ğ”¸ Î²] [MulPerm ğ”¸ Î³] {f : Î± â†’ Î² â†’ Î³}
    (h : Equivariant ğ”¸ f) (Ï€ : Finperm ğ”¸) (x : Î±) (y : Î²) :
    Ï€ â¬ f x y = f (Ï€ â¬ x) (Ï€ â¬ y) := by
  simp only [Function.equivariant_iff, funext_iff, Function.perm_def] at h
  have := h Ï€ x (Ï€ â¬ y)
  rwa [inv_perm_perm] at this

theorem applyâ‚ƒ_perm_eq {Î± Î² Î³ Î´ : Sort*} [MulPerm ğ”¸ Î±] [MulPerm ğ”¸ Î²] [MulPerm ğ”¸ Î³] [MulPerm ğ”¸ Î´]
    {f : Î± â†’ Î² â†’ Î³ â†’ Î´} (h : Equivariant ğ”¸ f) (Ï€ : Finperm ğ”¸) (x : Î±) (y : Î²) (z : Î³) :
    Ï€ â¬ f x y z = f (Ï€ â¬ x) (Ï€ â¬ y) (Ï€ â¬ z) := by
  simp only [Function.equivariant_iff, funext_iff, Function.perm_def] at h
  have := h Ï€ x (Ï€ â¬ y) (Ï€ â¬ z)
  rwa [inv_perm_perm, inv_perm_perm] at this

theorem Function.supports_iff {Î± Î² : Sort*} [MulPerm ğ”¸ Î±] [MulPerm ğ”¸ Î²]
    (f : Î± â†’ Î²) (s : Finset ğ”¸) :
    Supports s f â†”
      âˆ€ Ï€ : Finperm ğ”¸, (âˆ€ a âˆˆ s, Ï€ a = a) â†’
      âˆ€ x, Ï€ â¬ f x = f (Ï€ â¬ x) := by
  simp only [FinitelySupported, Supports, funext_iff, perm_def]
  constructor
  Â· intro hs Ï€ hÏ€ x
    rw [â† hs Ï€â»Â¹, perm_inv_perm, inv_inv]
    intro a ha
    conv_lhs => rw [â† hÏ€ a ha, inv_apply_self]
  Â· intro hs Ï€ hÏ€ x
    rw [hs Ï€ hÏ€, perm_inv_perm]

theorem Function.finitelySupported_iff {Î± Î² : Sort*} [MulPerm ğ”¸ Î±] [MulPerm ğ”¸ Î²]
    (f : Î± â†’ Î²) :
    FinitelySupported ğ”¸ f â†”
      âˆƒ s : Finset ğ”¸, âˆ€ Ï€ : Finperm ğ”¸, (âˆ€ a âˆˆ s, Ï€ a = a) â†’
      âˆ€ x, Ï€ â¬ f x = f (Ï€ â¬ x) := by
  simp only [FinitelySupported, supports_iff]
