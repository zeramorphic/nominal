import Nominal.Instances

/-- The *separated product* of `Î±` and `Î²` with respect to the name set `ğ”¸`. -/
@[ext]
structure SepProd (ğ”¸ : Type*) [DecidableEq ğ”¸] (Î± Î² : Type*) [MulPerm ğ”¸ Î±] [MulPerm ğ”¸ Î²] where
  fst : Î±
  snd : Î²
  sep : fst #[ğ”¸] snd

@[inherit_doc] notation:35 Î± " âˆ—["ğ”¸"] " Î²:34 => SepProd ğ”¸ Î± Î²

namespace SepProd

variable {ğ”¸ : Type*} [DecidableEq ğ”¸] {Î± Î² Î³ Î´ : Type*}

instance [MulPerm ğ”¸ Î±] [MulPerm ğ”¸ Î²] : HasPerm ğ”¸ (Î± âˆ—[ğ”¸] Î²) where
  perm Ï€ x := âŸ¨Ï€ â¬ x.fst, Ï€ â¬ x.snd, x.sep.perm Ï€âŸ©

@[simp]
theorem perm_fst [MulPerm ğ”¸ Î±] [MulPerm ğ”¸ Î²] (x : Î± âˆ—[ğ”¸] Î²) (Ï€ : Finperm ğ”¸) :
    (Ï€ â¬ x).fst = Ï€ â¬ x.fst :=
  rfl

@[simp]
theorem perm_snd [MulPerm ğ”¸ Î±] [MulPerm ğ”¸ Î²] (x : Î± âˆ—[ğ”¸] Î²) (Ï€ : Finperm ğ”¸) :
    (Ï€ â¬ x).snd = Ï€ â¬ x.snd :=
  rfl

instance [MulPerm ğ”¸ Î±] [MulPerm ğ”¸ Î²] : MulPerm ğ”¸ (Î± âˆ—[ğ”¸] Î²) where
  one_perm := by intros; ext <;> simp only [perm_fst, perm_snd, one_perm]
  mul_perm := by intros; ext <;> simp only [perm_fst, perm_snd, mul_perm]

def toProd [MulPerm ğ”¸ Î±] [MulPerm ğ”¸ Î²] (x : Î± âˆ—[ğ”¸] Î²) : Î± Ã— Î² :=
  (x.fst, x.snd)

theorem toProd_injective [MulPerm ğ”¸ Î±] [MulPerm ğ”¸ Î²] :
    Function.Injective (toProd : Î± âˆ—[ğ”¸] Î² â†’ Î± Ã— Î²) := by
  rintro âŸ¨xâ‚, yâ‚âŸ© âŸ¨xâ‚‚, yâ‚‚âŸ© h
  cases h
  rfl

theorem toProd_equivariant [MulPerm ğ”¸ Î±] [MulPerm ğ”¸ Î²] :
    Equivariant ğ”¸ (toProd : Î± âˆ—[ğ”¸] Î² â†’ Î± Ã— Î²) := by
  intro Ï€
  ext x : 1
  simp only [Function.perm_def, toProd, perm_fst, perm_snd, Prod.perm_mk, perm_inv_perm]

instance [Infinite ğ”¸] [Nominal ğ”¸ Î±] [Nominal ğ”¸ Î²] : Nominal ğ”¸ (Î± âˆ—[ğ”¸] Î²) where
  supported x := by
    use supp ğ”¸ x.fst âˆª supp ğ”¸ x.snd
    intro Ï€ hÏ€
    simp only [Finset.mem_union] at hÏ€
    ext
    Â· exact Nominal.supp_supports ğ”¸ x.fst Ï€ (Î» a ha â†¦ hÏ€ a (Or.inl ha))
    Â· exact Nominal.supp_supports ğ”¸ x.snd Ï€ (Î» a ha â†¦ hÏ€ a (Or.inr ha))

@[simp]
theorem supp_eq [Infinite ğ”¸] [Nominal ğ”¸ Î±] [Nominal ğ”¸ Î²] (x : Î± âˆ—[ğ”¸] Î²) :
    supp ğ”¸ x = supp ğ”¸ x.fst âˆª supp ğ”¸ x.snd := by
  rw [â† supp_apply_eq_of_injective toProd toProd_injective toProd_equivariant,
    Prod.supp_mk]
  rfl

/-!
# (Bi)functoriality
-/

def map [Nominal ğ”¸ Î±] [Nominal ğ”¸ Î²] [Nominal ğ”¸ Î³] [Nominal ğ”¸ Î´]
    (f : Î± â†’ Î³) (g : Î² â†’ Î´) (hf : Equivariant ğ”¸ f) (hg : Equivariant ğ”¸ g)
    (x : Î± âˆ—[ğ”¸] Î²) : Î³ âˆ—[ğ”¸] Î´ :=
  âŸ¨f x.fst, g x.snd, by
    have := x.sep
    rw [fresh_def, Finset.disjoint_iff_ne] at this âŠ¢
    intro a ha b hb
    exact this a (supp_apply_subset f hf x.fst ha) b (supp_apply_subset g hg x.snd hb)âŸ©

@[simp]
theorem map_fst [Nominal ğ”¸ Î±] [Nominal ğ”¸ Î²] [Nominal ğ”¸ Î³] [Nominal ğ”¸ Î´]
    {f : Î± â†’ Î³} {g : Î² â†’ Î´} {hf : Equivariant ğ”¸ f} {hg : Equivariant ğ”¸ g}
    (x : Î± âˆ—[ğ”¸] Î²) :
    (map f g hf hg x).fst = f x.fst :=
  rfl

@[simp]
theorem map_snd [Nominal ğ”¸ Î±] [Nominal ğ”¸ Î²] [Nominal ğ”¸ Î³] [Nominal ğ”¸ Î´]
    {f : Î± â†’ Î³} {g : Î² â†’ Î´} {hf : Equivariant ğ”¸ f} {hg : Equivariant ğ”¸ g}
    (x : Î± âˆ—[ğ”¸] Î²) :
    (map f g hf hg x).snd = g x.snd :=
  rfl

def first [Nominal ğ”¸ Î±] [Nominal ğ”¸ Î²] [Nominal ğ”¸ Î³]
    (f : Î± â†’ Î³) (hf : Equivariant ğ”¸ f) : Î± âˆ—[ğ”¸] Î² â†’ Î³ âˆ—[ğ”¸] Î² :=
  map f id hf id_equivariant

@[simp]
theorem first_fst [Nominal ğ”¸ Î±] [Nominal ğ”¸ Î²] [Nominal ğ”¸ Î³]
    {f : Î± â†’ Î³} {hf : Equivariant ğ”¸ f} (x : Î± âˆ—[ğ”¸] Î²) :
    (first f hf x).fst = f x.fst :=
  rfl

@[simp]
theorem first_snd [Nominal ğ”¸ Î±] [Nominal ğ”¸ Î²] [Nominal ğ”¸ Î³]
    {f : Î± â†’ Î³} {hf : Equivariant ğ”¸ f} (x : Î± âˆ—[ğ”¸] Î²) :
    (first f hf x).snd = x.snd :=
  rfl

def second [Nominal ğ”¸ Î±] [Nominal ğ”¸ Î²] [Nominal ğ”¸ Î³]
    (f : Î² â†’ Î³) (hf : Equivariant ğ”¸ f) : Î± âˆ—[ğ”¸] Î² â†’ Î± âˆ—[ğ”¸] Î³ :=
  map id f id_equivariant hf

@[simp]
theorem second_fst [Nominal ğ”¸ Î±] [Nominal ğ”¸ Î²] [Nominal ğ”¸ Î³]
    {f : Î² â†’ Î³} {hf : Equivariant ğ”¸ f} (x : Î± âˆ—[ğ”¸] Î²) :
    (second f hf x).fst = x.fst :=
  rfl

@[simp]
theorem second_snd [Nominal ğ”¸ Î±] [Nominal ğ”¸ Î²] [Nominal ğ”¸ Î³]
    {f : Î² â†’ Î³} {hf : Equivariant ğ”¸ f} (x : Î± âˆ—[ğ”¸] Î²) :
    (second f hf x).snd = f x.snd :=
  rfl

/-!
# Monoid structure
-/

/-- The monoidal symmetry. -/
def symm (Î± Î² : Type*) [MulPerm ğ”¸ Î±] [MulPerm ğ”¸ Î²] :
    Î± âˆ—[ğ”¸] Î² â‰ƒ Î² âˆ—[ğ”¸] Î± where
  toFun x := âŸ¨x.snd, x.fst, x.sep.commâŸ©
  invFun x := âŸ¨x.snd, x.fst, x.sep.commâŸ©
  left_inv _ := rfl
  right_inv _ := rfl

@[simp]
theorem symm_fst [MulPerm ğ”¸ Î±] [MulPerm ğ”¸ Î²] (x : Î± âˆ—[ğ”¸] Î²) :
    (symm Î± Î² x).fst = x.snd :=
  rfl

@[simp]
theorem symm_snd [MulPerm ğ”¸ Î±] [MulPerm ğ”¸ Î²] (x : Î± âˆ—[ğ”¸] Î²) :
    (symm Î± Î² x).snd = x.fst :=
  rfl

theorem symm_equivariant [MulPerm ğ”¸ Î±] [MulPerm ğ”¸ Î²] :
    Equivariant ğ”¸ (symm Î± Î² : Î± âˆ—[ğ”¸] Î² â†’ Î² âˆ—[ğ”¸] Î±) := by
  intro Ï€
  ext x
  Â· simp only [Function.perm_def, perm_fst, symm_fst, perm_snd, perm_inv_perm]
  Â· simp only [Function.perm_def, perm_snd, symm_snd, perm_fst, perm_inv_perm]

def leftDiscrete (Î± Î² : Type*) [MulPerm ğ”¸ Î±] [MulPerm ğ”¸ Î²] [IsDiscrete ğ”¸ Î±] :
    Î± âˆ—[ğ”¸] Î² â‰ƒ Î± Ã— Î² where
  toFun := toProd
  invFun x := âŸ¨x.fst, x.snd, by
    rw [fresh_def, IsDiscrete.supp_eq]
    exact Finset.disjoint_empty_left _âŸ©
  left_inv _ := rfl
  right_inv _ := rfl

@[simp]
theorem leftDiscrete_apply_eq [MulPerm ğ”¸ Î±] [MulPerm ğ”¸ Î²] [IsDiscrete ğ”¸ Î±]
    (x : Î± âˆ—[ğ”¸] Î²) :
    leftDiscrete Î± Î² x = toProd x :=
  rfl

def rightDiscrete (Î± Î² : Type*) [MulPerm ğ”¸ Î±] [MulPerm ğ”¸ Î²] [IsDiscrete ğ”¸ Î²] :
    Î± âˆ—[ğ”¸] Î² â‰ƒ Î± Ã— Î² :=
  (symm Î± Î²).trans ((leftDiscrete Î² Î±).trans (Equiv.prodComm Î² Î±))

@[simp]
theorem rightDiscrete_apply_eq [MulPerm ğ”¸ Î±] [MulPerm ğ”¸ Î²] [IsDiscrete ğ”¸ Î²]
    (x : Î± âˆ—[ğ”¸] Î²) :
    rightDiscrete Î± Î² x = toProd x :=
  rfl

/-- The monoidal left unitor. -/
def leftUnitor (Î± : Type*) [MulPerm ğ”¸ Î±] :
    Unit âˆ—[ğ”¸] Î± â‰ƒ Î± :=
  (leftDiscrete Unit Î±).trans (Equiv.punitProd Î±)

@[simp]
theorem leftUnitor_apply_eq [MulPerm ğ”¸ Î±] (x : Unit âˆ—[ğ”¸] Î±) :
    leftUnitor Î± x = x.snd :=
  rfl

@[simp]
theorem leftUnitor_symm_apply_snd_eq [MulPerm ğ”¸ Î±] (x : Î±) :
    ((leftUnitor Î± : Unit âˆ—[ğ”¸] Î± â‰ƒ Î±).symm x).snd = x :=
  rfl

/-- The monoidal right unitor. -/
def rightUnitor (Î± : Type*) [MulPerm ğ”¸ Î±] :
    Î± âˆ—[ğ”¸] Unit â‰ƒ Î± :=
  (rightDiscrete Î± Unit).trans (Equiv.prodPUnit Î±)

@[simp]
theorem rightUnitor_apply_eq [MulPerm ğ”¸ Î±] (x : Î± âˆ—[ğ”¸] Unit) :
    rightUnitor Î± x = x.fst :=
  rfl

@[simp]
theorem rightUnitor_symm_apply_snd_eq [MulPerm ğ”¸ Î±] (x : Î±) :
    ((rightUnitor Î± : Î± âˆ—[ğ”¸] Unit â‰ƒ Î±).symm x).fst = x :=
  rfl

def assoc' [Infinite ğ”¸] [Nominal ğ”¸ Î±] [Nominal ğ”¸ Î²] [Nominal ğ”¸ Î³]
    (x : (Î± âˆ—[ğ”¸] Î²) âˆ—[ğ”¸] Î³) : Î± âˆ—[ğ”¸] Î² âˆ—[ğ”¸] Î³ where
  fst := x.fst.fst
  snd := âŸ¨x.fst.snd, x.snd, by
    have := x.sep
    rw [fresh_def] at this âŠ¢
    simp only [supp_eq, Finset.disjoint_union_left] at this
    aesopâŸ©
  sep := by
    have := x.sep
    rw [fresh_def] at this âŠ¢
    rw [supp_eq, Finset.disjoint_union_left] at this
    rw [supp_eq, Finset.disjoint_union_right]
    exact âŸ¨x.fst.sep, this.1âŸ©

/-- The monoidal associator. -/
def assoc [Infinite ğ”¸] (Î± Î² Î³ : Type*) [Nominal ğ”¸ Î±] [Nominal ğ”¸ Î²] [Nominal ğ”¸ Î³] :
    (Î± âˆ—[ğ”¸] Î²) âˆ—[ğ”¸] Î³ â‰ƒ Î± âˆ—[ğ”¸] Î² âˆ—[ğ”¸] Î³ where
  toFun := assoc'
  invFun := first (symm Î² Î±) symm_equivariant âˆ˜ symm Î³ (Î² âˆ—[ğ”¸] Î±) âˆ˜
    assoc' âˆ˜ first (symm Î² Î³) symm_equivariant âˆ˜ symm Î± (Î² âˆ—[ğ”¸] Î³)
  left_inv _ := rfl
  right_inv _ := rfl

end SepProd
